"One King to Loot them All" by Onno Brouwer.
[IFID:1E9798FD-68F2-499E-A319-914A4453BB47]

The story headline is "A tale of High Adventure".
The release number is 1.
The story genre is "Fantasy".
The story creation year is 2023.

[
Copyright 2023 Onno Brouwer.
 
This source code is provided for personal, educational use only. The story and text of _One King to Loot them All_ belong to me; you may not use them or create derivative works which contain them. However, you have permission to use the programming techniques of this game in your own works, and you may use the source code excluding game text.

If you want to *play* this game, you're in the wrong place.

Obvious warning: This code contains spoilers! If you haven't played _One King to Loot them All_, and you read this code, you'll certainly ruin the game for yourself. Play it first. I am releasing this code for the benefit of Inform programmers who are familiar with the game and want to know how I did it.

I implemented this game using Inform 7 version 10.1.0. It probably won't work with earlier releases, and I can't make guarantees about future ones.
]

Volume - Extensions

Include Exit Lister by Eric Eve.

Include Undo Output Control by Nathanael Nerode.

Volume - Story Introduction

Use full-length room descriptions, American dialect, and the serial comma.

Release along with the "Parchment" interpreter.
Release along with the library card.

[ The killing blow delivered with the sword
I made this part of the intro hoping that observant players will remember it and
that they realize they made the wrong choice when history starts repeating itself.
Based on the battle between Conan and a demon described in The Phoenix on the Sword,
one of the original short stories about Conan the Cimmerian written by American author
Robert E. Howard and first published in Weird Tales magazine in December 1932.
]

To say frenzied-rage:
	say "The demon sparks in you a frenzied rage. You reverse your sword grip, clenching it tightly, and strike with all your might. Thrusting your sword like a dagger, you drive it deep into the horror, breaking it off at the hilt. The damage is done, and the loathsome creature convulses in agony. You are hurled away, and as you rise on one hand, you see the struggles of the demon cease. The thing disintegrates into a slimy mass, until it dissolves completely, leaving only an outline in blood on the floor".

The story description is "In the old free days, all you needed was a sharp sword and a straight path to your enemies. Overthrowing the old dynasty was easy enough, but you quickly learned that as a King, no path was straight, and your sword was useless. Now, an old enemy has sent you this abomination through a magical portal, and you face death. You feel alive once more.[paragraph break][frenzied-rage].[paragraph break]You hear your blood sing in your ears, and you crave vengeance. Vengeance upon the dark sorcerer who sent this creature that killed your priestess, Lydia, into the very heart of your kingdom. It is time to take the fight to him. You are the Barbarian. You are the King. You are... "

When play begins:
	say "[the story description][paragraph break]".

Volume - Helper Functions

[ Contributions by Zed Lopez ]

To say i: say "[italic type]".
To say b: say "[bold type]".
To say r: say "[roman type]".

To say (n - number) s:
	unless n is 1, say "s".

To decide what K is a random member of (list - list of values of kind K):
	let the count be the number of entries in the list;
	let the index be a random number between one and the count;
	decide on entry index of the list.

To decide what K is a/an/-- (unknown - a value) cast as a/an/-- (name of kind of value K):
	(- {unknown} -).

To say short name of (t - table name):
	let q be "[t]";
	replace the regular expression "^table of " in q with "", case insensitively;
	say the substituted form of q;

To decide if (t - text) is blank: decide on whether or not t is "".

To return: (- return; -).

Volume - Disabling standard commands

[ http://alice-blue.github.io/parser-parer/ ]

Book - Standard actions concerning the actor's possessions

Understand nothing as taking inventory.
Understand nothing as taking.
Understand nothing as removing it from.
Understand nothing as dropping.
Understand nothing as putting it on.
Understand nothing as inserting it into.
Understand nothing as eating.

Book - Standard actions which move the actor

Understand nothing as going.
Understand nothing as entering.
Understand nothing as exiting.
Understand nothing as getting off.

Book - Standard actions concerning the actor's vision

Understand nothing as looking.
Understand nothing as examining.
Understand nothing as looking under.
Understand nothing as searching.
Understand nothing as consulting it about.

Book - Standard actions which change the state of things

Understand nothing as locking it with.
Understand nothing as unlocking it with.
Understand nothing as switching on.
Understand nothing as switching off.
Understand nothing as opening.
Understand nothing as closing.
Understand nothing as wearing.
Understand nothing as taking off.

Book - Standard actions concerning other people

Understand nothing as giving it to.
Understand nothing as showing it to.
Understand nothing as waking.
Understand nothing as throwing it at.
Understand nothing as attacking.
Understand nothing as kissing.
Understand nothing as answering it that.
Understand nothing as telling it about.
Understand nothing as asking it about.
Understand nothing as asking it for.

Book - Standard actions which are checked but then do nothing unless rules intervene

Understand nothing as waiting.
Understand nothing as touching.
Understand nothing as waving.
Understand nothing as pulling.
Understand nothing as pushing.
Understand nothing as turning.
Understand nothing as pushing it to.
Understand nothing as squeezing.

Book - Standard actions which always do nothing unless rules intervene

Understand nothing as saying yes.
Understand nothing as saying no.
Understand nothing as burning.
Understand nothing as waking up.
Understand nothing as thinking.
Understand nothing as smelling.
Understand nothing as listening to.
Understand nothing as tasting.
Understand nothing as cutting.
Understand nothing as jumping.
Understand nothing as tying it to.
Understand nothing as drinking.
Understand nothing as saying sorry.
Understand nothing as swinging.
Understand nothing as rubbing.
Understand nothing as setting it to.
Understand nothing as waving hands.
Understand nothing as buying.
Understand nothing as climbing.
Understand nothing as sleeping.

Book - Standard actions which happen out of world

[ Understand nothing as quitting the game.                  [ quit | q   ] ]
[ Understand nothing as saving the game.                    [ save       ] ]
[ Understand nothing as restoring the game.                 [ restore    ] ]
[ Understand nothing as restarting the game.                [ restart    ] ]
[ Understand nothing as verifying the story file.           [ verify     ] ]
[ Understand nothing as switching the story transcript on.  [ script on  ] ]
[ Understand nothing as switching the story transcript off. [ script off ] ]
[ Understand nothing as requesting the story file version.  [ version    ] ]
Understand nothing as requesting the score.
Understand nothing as preferring abbreviated room descriptions.
Understand nothing as preferring unabbreviated room descriptions.
Understand nothing as preferring sometimes abbreviated room descriptions.
Understand nothing as switching score notification on.
Understand nothing as switching score notification off.
Understand nothing as requesting the pronoun meanings.

Volume - Undo Oops Again by Zed Lopez

[ Undo Oops Again by Zed Lopez begins here. ]

[ "Enables activating or deactivating the special pseudo-commands 'oops', 'again', 'undo', or the abbreviations 'o' or 'g'." ]

Include (-
Constant InvalidToken = 'x,y';
Constant OopsText ='oops';
Constant OopsAbbrev ='o//';
Constant AgainText = 'again';
Constant AgainAbbrev ='g//';
Constant UndoText = 'undo';
-).

Include (-
Global UNDO1__WD = UndoText;
-) replacing "UNDO1__WD".

Include (-
Global UNDO2__WD = InvalidToken;
-) replacing "UNDO2__WD".

Include (-
Global UNDO3__WD = InvalidToken;
-) replacing "UNDO3__WD".

Include (-
Global OOPS1__WD = OopsText;
-) replacing "OOPS1__WD".

Include (-
Global OOPS2__WD = OopsAbbrev;
-) replacing "OOPS2__WD".

Include (-
Global OOPS3__WD = InvalidToken;
-) replacing "OOPS3__WD".

To oops-deactivate: (-
OOPS1__WD = InvalidToken;
OOPS2__WD = InvalidToken;
-).

To oops-activate: (-
OOPS1__WD = OopsText;
OOPS2__WD = OopsAbbrev;
-).

To oops-abbrev-deactivate: (-
OOPS2__WD = InvalidToken;
-).

To oops-abbrev-activate: (-
OOPS2__WD = OopsAbbrev;
-).

Include (-
Global AGAIN1__WD = AgainText;
-) replacing "AGAIN1__WD".

Include (-
Global AGAIN2__WD = AgainAbbrev;
-) replacing "AGAIN2__WD".

Include (-
Global AGAIN3__WD = InvalidToken;
-) replacing "AGAIN3__WD".

To again-deactivate: (-
AGAIN1__WD = InvalidToken;
AGAIN2__WD = InvalidToken;
-).

To again-activate: (-
AGAIN1__WD = AgainText;
AGAIN2__WD = AgainAbbrev;
-).

To again-abbrev-deactivate: (-
AGAIN2__WD = InvalidToken;
-).

To again-abbrev-activate: (-
AGAIN2__WD = AgainAbbrev;
-).

To undo-deactivate: (-
UNDO1__WD = InvalidToken;
-).

To undo-activate: (-
UNDO1__WD = UndoText;
-).

[ Undo Oops Again by Zed Lopez ends here. ]

When play begins:
	undo-deactivate;  [ I use my own undo as a part of the story. ]
	again-deactivate;
	oops-deactivate;

Volume - Parser Errors

To say xyzzy:
	say "You do not fathom this barbaric command. Use the [b]HELP[r] command for guidance. ".
To say shutup:
	say "You swing your weapon to cleave enemies, not to wade through this endless chatter.".
To say speakup:
	say "Did you forget to turn [b]STORY MODE ON[r], or are you testing your barbarian patience?".

Chapter - Block answering rule responses

[ npc, blah ]
[ There is no reply. ]

The block answering rule response (A) is "[xyzzy]"

Chapter - Requested actions require persuasion rule responses

[ npc, command ]
[ [The noun] [have] better things to do. ]

The requested actions require persuasion rule response (A) is "[xyzzy]"

Chapter - Action processing internal rule responses

[ https://zedlopez.github.io/standard_rules/#line1311
The action processing internal rule translates into I6 as
  "ACTION_PROCESSING_INTERNAL_R" with
  "[bracket]That command asks to do something outside of play, so it can
   only make sense from you to me. [The noun] cannot be asked to do this.[close
   bracket]" (A),
  "You must name an object." (B),
  "You may not name an object." (C),
  "You must supply a noun." (D),
  "You may not supply a noun." (E),
  "You must name a second object." (F),
  "You may not name a second object." (G),
  "You must supply a second noun." (H),
  "You may not supply a second noun." (I),
  "(Since something dramatic has happened, your list of commands has been
  cut short.)" (J),
  "I didn't understand that instruction." (K).
Rule for supplying a missing noun while an actor smelling (this is the ambient odour rule):
  now the noun is the touchability ceiling of the player.
Rule for supplying a missing noun while an actor listening (this is the ambient sound rule):
  now the noun is the touchability ceiling of the player.
Rule for supplying a missing noun while an actor going (this is the block vaguely going rule):
  say "You'll have to say which compass direction to go in." (A).
]

[ regard ]
[ You must supply a noun. ]

The action processing internal rule response (D) is "[speakup]"

Chapter - Parser command internal rule response

[ https://zedlopez.github.io/standard_rules/#line1369
The parser command internal rule translates into I6 as
  "PARSER_COMMAND_INTERNAL_R" with
  "Sorry, that can't be corrected." (A),
  "Think nothing of it." (B),
  "'Oops' can only correct a single word." (C),
  "You can hardly repeat that." (D).
]

[ oops o ]
[ "Sorry, that can't be corrected." ]

The parser command internal rule response (A) is "Your sorcery failed to correct your command."

[ again g ]
[ "You can hardly repeat that." ]

The parser command internal rule response (D) is "There can be no repetition when your tale has scarcely begun."

Chapter - Parser error internal rule responses

[ https://zedlopez.github.io/standard_rules/#line1328
The parser error internal rule translates into I6 as
  "PARSER_ERROR_INTERNAL_R" with
  "I didn't understand that sentence." (A),
  "I only understood you as far as wanting to " (B),
  "I only understood you as far as wanting to (go) " (C),
  "I didn't understand that number." (D),
  "[We] [can't] see any such thing." (E),
  "You seem to have said too little!" (F),
  "[We] [aren't] holding that!" (G),
  "You can't use multiple objects with that verb." (H),
  "You can only use multiple objects once on a line." (I),
  "I'm not sure what ['][pronoun i6 dictionary word]['] refers to." (J),
  "[We] [can't] see ['][pronoun i6 dictionary word]['] ([the noun]) at the moment." (K),
  "You excepted something not included anyway!" (L),
  "You can only do that to something animate." (M),
  "That's not a verb I [if American dialect option is
     active]recognize[else]recognise[end if]." (N),
  "That's not something you need to refer to in the course of this game." (O),
  "I didn't understand the way that finished." (P),
  "[if number understood is 0]None[else]Only [number understood in words][end if]
     of those [regarding the number understood][are] available." (Q),
  "That noun did not make sense in this context." (R),
  "To repeat a command like 'frog, jump', just say 'again', not 'frog, again'." (S),
  "You can't begin with a comma." (T),
  "You seem to want to talk to someone, but I can't see whom." (U),
  "You can't talk to [the noun]." (V),
  "To talk to someone, try 'someone, hello' or some such." (W),
  "I beg your pardon?" (X).
]

[ me, ]
[ I didn't understand that sentence. ]

The parser error internal rule response (A) is "[xyzzy]"

[ regard thing wossname ]
[ I only understood you as far as wanting to ]

The parser error internal rule response (B) is "[shutup]Maybe you just wanted to "

[ regard wossname ]
[ You can't see any such thing. ]

The parser error internal rule response (E) is "You find nothing of the sort in this vicinity."

[ regard thing thing ]
[ You can't use multiple objects with that verb. ]

The parser error internal rule response (H) is "You can manage only one thing at at time."

[ wossname ]
[ me, wossname ]
[ That's not a verb I recognize. ]

The parser error internal rule response (N) is "[xyzzy]"

[ , wossname ]
[ You can't begin with a comma. ]

The parser error internal rule response (T) is "[xyzzy]"

[ wossname, ]
[ You seem to want to talk to someone, but I can't see whom ]

The parser error internal rule response (U) is "[xyzzy]"

[ thing, ]
[ You can't talk to [the noun]. ]

The parser error internal rule response (V) is "[xyzzy]"

[ ]
[ I beg your pardon? ]

The parser error internal rule response (X) is "[speakup]"

Volume - Data Types and Global Variables

A room is usually privately-named.
A thing is usually privately-named.

The journeybook is a list of table names that varies.   [ All action tables. ]
The journeybook is {Table of Choices, Table of Order vs Chaos, Table of Good vs Evil}.
The choice is a number that varies.        [ The number of the action table. ]
The journey is a table name that varies.   [ The action table we are using.  ]
The progress is a number that varies.      [ The index in the action table.  ]
Allowed-actions is a list of actions that varies. [ List of allowed actions. ]

Dream-reached is a truth state that varies. [ True if we reached the end of dream. ]
Reversed-flow is a truth state that varies. [ True if we go backwards in time.     ]
RW-restarting is a truth state that varies. [ True if we do a Roger Wilco restart. ]

[ Clothing : a subcategory of wearables which is owned by an actor. ]
[ The owner will auto-wear clothing, any other actor will carry it. ]

Clothing is a kind of wearable thing.
Clothing has a person called owner.
Definition: a person is decent rather than indecent if it is wearing clothing.

[ regarding (the Barbarian equivalent of examining something) ]

A thing has a number called regard-counter.
Definition: a thing is regarded rather than unregarded if its regard-counter is greater than 0.
Definition: a thing is studied rather than unstudied if its regard-counter is greater than 1.

[ looting (the Barbarian equivalent of opening and removing all from a container) ]
[ We use looting to plunder unlocked containers. ]
[ We use smiting to plunder locked containers.   ]

Definition: A thing is lootable rather than unlootable if it is an unlocked container.
Definition: A thing is looted rather than unlooted if it is an open container.

[ presenting (the Barbarian equivalent of giving or showing something to someone) ]

A thing has a number called present-counter.
Definition: a thing is presented rather than unpresented if its present-counter is greater than 0.

[ The player can present himself or anything he has to a recipient.     ]
[ The recipient is local to the room or a party member (Alcaz / Lydia). ]

A room has a person called room-recipient.
The room-recipient of a room is usually Conan. [ Most rooms do not have a recipient. ]

To decide which person is the recipient:
	let R be the room-recipient of the location;
	if R is not Conan, decide on R;               [ Room recipient ]
	if Lydia is in the location, decide on Lydia; [ Lydia ]
	if Alcaz is in the location, decide on Alcaz; [ Alcaz ]
	decide on Conan.                              [ (nobody) ]

[ Presentable thing: The player or something he has. ]

Definition: a thing is presentable rather than unpresentable: 
	if it is the player, decide yes;
	if the player has it, decide yes;
	decide no.

[ treating (the Barbarian equivalent of telling someone ]
[ something, but with the topic decided by the game).   ]

A person has a number called treat-counter.

Definition: a thing is treated rather than untreated if its treat-counter is greater than 0.

[ smiting (the Barbarian equivalent of attacking something) ]

A thing has a number called smite-counter.

[ We assume here that a person is carrying at most one weapon at a time. ]

A weapon is a kind of thing.
Definition: a person is armed rather than unarmed if it is carrying a weapon.
Definition: a weapon is wielded rather than unwielded if it is carried by a person.

To say wielded-weapon of (P - a person):
	let W be a random weapon carried by P;
	if W is nothing:
		say "your fist";
	else:
		say "[W]";

To say your-weapon:
	say "[wielded-weapon of Conan]";

A monster is a kind of animal. [ All monsters are animals. ]
A monster is usually neuter.   [ I prefer using 'it' (instead of the default 'him'). ]
Definition: a person is injured rather than uninjured if its smite-counter > 0.

[ marching (the Barbarian equivalent of going somewhere) ]

A room has a direction called the way back. [ A True Barbarian never retreats. ]
The current room is a room that varies. [ We look if current room is not the location. ]

To move the party to (R - a room):
	let C be the holder of Conan; [ the Barbarian ]
	let A be the holder of Alcaz; [ the Mage      ]
	let L be the holder of Lydia; [ the Priestess ]
	if C is not R:
		move Conan to R, without printing a room description;
		if A is C, move Alcaz to R;
		if L is C, move Lydia to R;

[ denials (for thing-related verbs) ]

A thing has a list of texts called the denials. [ Most of the times actions are denied, ]
An action-index is a kind of value.             [ so we have per-thing customization.   ]
The action-index are regard, seize, loot, present, treat, smite. [ list of thing verbs. ]

[ I had to add these verbs to allow Inform to use them for adaptive text. ]

To appear is a verb.  To bear is a verb.    To demand is a verb. To deny is a verb.
To hunger is a verb.  To know is a verb.    To lie is a verb.    To lurk is a verb.
To present is a verb. To prevent is a verb. To remain is a verb. To seem is a verb.
To stand is a verb.   To surpass is a verb. To tempt is a verb.  To want is a verb.

The generic-denials is a list of texts that varies. [ If all else fails, use these. ]
The generic-denials are {
  [regard] "[one of]You do not find anything suspicious or interesting about [the noun][or][The noun] [don't] seem to hold any secrets or intrigue[or]Upon inspection, [the noun] [appear] to be devoid of any unusual or captivating qualities[or]Your scrutiny reveals nothing unusual or captivating about [the noun][or]A thorough examination of [the noun] yields no signs of suspicion or interest[or][The noun] [seem] ordinary and unremarkable upon closer examination[cycling].",
  [seize] "[if the player has the noun][one of]You already possess [the noun][or]You already have [the noun][or][The noun] [are] already in your possession[or][The noun] [are] already firmly in your grasp[or][The noun] [are] already part of your belongings[or]You're already the owner of [the noun][cycling][else]You cannot seize [the noun][end if].",
  [loot] "[if the noun is looted][one of]You already looted the contents of [the noun][or][Regarding the noun][Possessive] contents have already been thoroughly looted by you[or]You've already emptied out the contents of [the noun][or]There's nothing left to loot from [the noun][or][Regarding the noun][Possessive] valuables have already been claimed by your hand[or]You've already seized and taken all there was from [the noun][cycling][else]You cannot loot [the noun][end if].",
  [present] "You cannot present [the noun].",
  [treat] "You cannot treat with [the noun].",
  [smite] "You cannot smite [the noun]."
}.

[ Look up a denial message based on an action index and a thing.  ]
[ If the thing does not have a denial message, use a generic one. ]

To say the/-- (A - an action-index) denial of (T - a thing):
   let I be A cast as a number;
   let L be the denials of T;
   let N be the number of entries in L;
   if I > N or entry I of L is blank, now L is the generic-denials;
   say entry I of L;

[ refusals (for room-related exits) ]

[ I never use the inside / outside directions, so I treat these as unknown directions.
horizontal : player tries to move in a compass direction without exit {n,ne,e,se,s,sw,w,nw}.
vertical   : player tries to move in the vertical plane without exit {u,d}.
untimely   : player tries to leave the room too soon. We have a monster / unfinished task.
blackhole  : player tries to leave through an open exit which does not really exist.
]

A room has a list of texts called the refusals.
A march-index is a kind of value.
The march-index are horizontal, vertical, untimely, blackhole.

The generic-refusals is a list of texts that varies. [ If all else fails, use these. ]
The generic-refusals are {
  [horizontal] "[one of]Your path is blocked in that direction[or]The way ahead is obstructed in that direction[or]Progress is halted in that direction[or]The path is closed off ahead[or]You encounter an impassable barrier in that direction[or]Forward movement is hindered in that direction[cycling].",
  [vertical] "[one of]There's no way to move in that direction[or]You can't proceed in that direction[or]Moving in that direction is impossible[or]No way to advance in that direction[or]Progress is barred in that direction[or]You're unable to move forward in that direction[cycling].",
  [untimely] "[if a monster (called M) is in the location][one of]To march away in [regarding the M][possessive] presence would be an affront to your honor[or]Turning your back on [the M] and marching away would be a dishonor to your name[or]Leaving [regarding the M][possessive] presence without a fight would stain your honor[or]Retreating in the face of [the M] would tarnish your warrior's honor[or]Walking away from [the M] now would be a blow to your sense of honor[or]Backing down in front of [the M] would mar your reputation for honor and bravery[cycling][else][one of]You realize that you have unfinished business here, and you cannot leave yet[or]Your instincts tell you that there's more to be done here before you can depart[or]You sense that your task here isn't complete, and you can't leave just yet[or]The feeling of unresolved matters keeps you rooted in this place[or]Something tells you that you must accomplish more before you can move on[or]Your warrior's intuition informs you that you can't depart until you've settled matters here[cycling][end if].",
  [blackhole] "[one of]Our path diverges from our purpose, let's realign our course[or]We must refocus, for our current direction veers off our true path[or]Our journey strays from its course, we must correct our heading[or]We're deviating from our intended path; let's steer back to our purpose[or]Our course falters; we must steer our actions toward our true goal[or]The winds of fate carry us astray; let us navigate back to our rightful course[cycling]."
}.

[ Look up a refusal message based on a march index and a room.    ]
[ If the room does not have a refusal message, use a generic one. ]

To say the/-- (M - a march-index) refusal of (R - a room):
   let I be M cast as a number;
   let L be the refusals of R;
   let N be the number of entries in L;
   if I > N or entry I of L is blank, now L is the generic-refusals;
   say entry I of L;

[ feature flags
- ff-unknown  : We have no decision on this feature flag yet, or it is not available.
- ff-enabled  : We have decided to enable this feature.
- ff-disabled : We have decided to disable this feature.
]

The feature flag is a kind of value.
The feature flags are ff-unknown, ff-enabled, ff-disabled.

[ We sometimes need to progress fast forward or fast backward without showing any output. ]

The silencer is a truth state that varies. [ If true, no output is generated. ]

Volume - Actions

[ This function transfers a thing to an object (a container, a person, or a room).
- If the original holder is a container, open it first.
- If the original holder is the player, make it unhandled (re-use initial appearance).
- If the new holder is a person we need some special handling:
  - If the thing is clothing and the new holder owns it, he/she will wear it.
  - Otherwise the person will carry it.
- Otherwise the new holder is a container or a room and we just put the thing in there.
- Finally, if the new holder is a container, we close it.
(We assume that any container will contain at most one thing to keep the logic simple.)
]

To transfer (T - a thing) to (O - an object):
	let H be the holder of T;
	if H is a closed container, now H is open; [ Open container before removing things. ]
	if H is the player, now T is not handled;  [ Reset the handled flag for the PC. ]
	if O is a person: [ the PC or an NPC ]
		if T is clothing and the owner of T is O:
			now O wears T;   [ If you own it, you wear it. ]
		else:
			now O carries T; [ If you do not own it, you carry it. ]
	else: [ A room or a container (I do not use supporters in this game). ]
		now T is in O;
		if O is an open container, now O is closed; [ Close it after adding things. ]

[ This function creates a list of allowed actions for the player.
  choice.progress should always point at the first entry of a command set.
-1 optional command
 0 world command
 1 mandatory command (if we have two of these, then this is the "Single Choice")
 3 member of mandatory command set (for increased flexibility of the "ritual" puzzle)
]

To load the allowed actions:
	now allowed-actions is {};
	now the journey is entry choice plus one of the journeybook;
	let N be the number of rows in the journey;
	if progress <= N:
		let i be progress;
		choose row i in the journey;
		while type entry < 0: [ load optional action, should have no world actions attached ]
			add action entry to allowed-actions;
			increment i;
			choose row i in the journey;
		let S be type entry;
		if S is 1: [ single action ]
			if action entry is Conan marching inside:
				repeat with E running through directions:
					let R be the room E from the location;
					if R is not nothing:
						add Conan marching E to allowed-actions;
			else:
				add action entry to allowed-actions;
		else: [ set of actions (should not include a marching action) ]
			while type entry is S or type entry is 0:
				if type entry is S and flag entry is 0, add action entry to allowed-actions;
				increment i;
				choose row i in the journey;
	else if the choice is 0: [ offering the One Choice ]
		add Conan seizing the sword to allowed-actions; [ the path of Order vs Chaos ]
		add Conan seizing the axe to allowed-actions;   [ the path of Good vs Evil ]
	let A be entry 1 of allowed-actions;
	if A is undoing: [ we have reached the end of the Order vs Chaos flow ]
		now reversed-flow is true;
		now dream-reached is true;

[ This function swaps entries in a set of actions ]

Table of Swapping
action	type	flag	undoes	redoes	object (an object)
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--
Conan waiting	0	0	""	""	--

To copy (n - a number) rows from (i - a number) in (s - a table name) to (j - a number) in (t - a table name):
	while n > 0:
		choose row i in s;
		let a-val be the action entry;
		let t-val be the type entry;
		let f-val be the flag entry;
		let u-val be the undoes entry;
		let r-val be the redoes entry;
		let o-val be an object;
		if there is an object entry, now o-val is the object entry;
		choose row j in t;
		now the action entry is a-val;
		now the type entry is t-val;
		now the flag entry is f-val;
		now the undoes entry is u-val;
		now the redoes entry is r-val;
		now the object entry is o-val;
		increment i;
		increment j;
		decrement n;

To swap entry (i - a number):
	let p be progress;
	if i is p, return;
	let j be i;
	let N be the number of rows in the journey;
	while j < N:
		increment j;
		if type in row j of journey is not 0, break;
	let a be j - i;
	let b be i - p;
	let s be Table of Swapping;
	copy a rows from i in journey to 1 in s;
	copy b rows from p in journey to 1 + a in s;
	copy a + b rows from 1 in s to p in journey;
	
[ This function skips the actions preceding the chosen action ]

To skip the preceding actions:
	let C be the action name part of the current action;
	let N be the number of rows in the journey;
	while progress <= N: [ handle optional and initial actions ]
		choose row progress in the journey;
		let A be the action name part of action entry;
		if A is C:
			now flag entry is 1;
			return;
		if the type entry > 0, break;
		increment progress;
	let i be the progress;
	let S be the type entry;
	let A be the action name part of action entry;
	while A is not C:
		increment i;
		choose row i in the journey;
		if the type entry is 0, next;
		now A is the action name part of action entry;
	now flag entry is 1;
	swap entry i;

[ We want to enforce some ordering of things.
0 closed doors (hides them; we do not really have many doors in the traditional sense).
5 default priority; items which are generally fixed in place (and sometimes can be looted).
6 any other portable items which can potentially be seized by the player.
7 open doors (shows them; things like the magical portal, and the "boat" on the river).
8 any animals: goat, ...
9 any persons: Alcaz, Lydia, ...
10 any monsters: demon, serpent, ...
]

Rule for choosing notable locale objects:
	repeat with item running through things in the location:
		if the item is an animal:
			set the locale priority of the item to 8; [ monster / animal ]
		else if the item is a man or the item is a woman:
			set the locale priority of the item to 9; [ Alcaz or Lydia or Sage ]
		else if the item is portable:		
			set the locale priority of the item to 6; [ portable items ]
		else if the item is not a door:
			set the locale priority of the item to 5; [ fixed in place ]
		else if the item is open:
			set the locale priority of the item to 7; [ open door ]
		else:
			set the locale priority of the item to 0; [ closed door ]

[ Whenever we look (after executing a command which progresses the story), ]
[ we also automatically take inventory to avoid the need for that command. ]

Carry out looking:
	if the first thing held by the player is not nothing:
		say "You are carrying ";
		list the contents of the player, giving inventory information, as a sentence;
		say ".";

Rule for printing inventory details of weapons: [ We tag the weapon as wielded ]
	say " (wielded)[run paragraph on]".

Rule for printing inventory details of persons: [ We tag the person as sacrifice ]
	say " (sacrifice)[run paragraph on]".

Definition: a thing (called P) is payment if P is clothing and the owner of P is not Conan.

Rule for printing inventory details of payment: [ We tag other clothing as payment ]
	say " (payment)[run paragraph on]".

[
After every successful command (except UNDO which manages its own state updating),
we progress the story. Any world actions after the player action are automatically
executed as well. Using separate world actions to change state allowed me to avoid
putting state-changing code into text, simplifying debugging and undoing actions.
]

To move forward:
	now the journey is entry choice plus one of the journeybook;
	let N be the number of rows in the journey;
	increment progress;
	while progress <= N:
		choose row progress in the journey;
		let A be the action name part of action entry;
		if A is not a world action, break;
		now the flag entry is 1;
		try the action entry;
		increment progress;
	load the allowed actions;
	if entry 1 of the allowed-actions is quitting the game:
		end the story finally saying "Thank you for playing ONE KING TO LOOT THEM ALL";
	else:
		if silencer is false and the current room is not the location, try looking;
		now the current room is the location;

Book - World Actions

Definition: an action name (called A) is a world action:
	if A is the transferring it to action, decide yes;
	if A is the swapping it with action, decide yes;
	if A is the toggling action, decide yes;
	if A is the forcing action, decide yes;
	decide no.

Chapter - Transferring it to

Transferring it to is an action applying to one thing and one object.

[ Move a thing to a location, a person, or a container. ]
[ Remember the original holder of the noun for an UNDO. ]

Carry out transferring it to:
	now object in row progress in the journey is the holder of the noun;
	transfer the noun to the second noun;
	choose row progress in the journey;
	if silencer is false and the redoes entry is not "":
		say "[redoes entry][paragraph break]";

Chapter - Swapping it with

Swapping it with is an action applying to two things.

[ Swap two things. To avoid runtime errors, all things are in a location, worn or carried ]
[ by a person, or in a container. Offstage things are held in the black hole room. ]

Carry out swapping it with:
	let N be the holder of the noun;
	let S be the holder of the second noun;
	transfer the noun to S;
	transfer the second noun to N;
	choose row progress in the journey;
	if silencer is false and the redoes entry is not "":
		say "[redoes entry][paragraph break]";

Chapter - Toggling

Toggling is an action applying to one visible thing.

[ Toggle the open/closed state of a thing. Applicable to doors and containers. ]

Carry out toggling:
	if the noun is open:
		now the noun is closed;
		if the noun is a door, now the noun is not apparent;
	else:
		now the noun is open;
		if the noun is a door, now the noun is apparent;
	choose row progress in the journey;
	if silencer is false and the redoes entry is not "":
		say "[redoes entry][paragraph break]";

Chapter - Forcing

Forcing is an action applying to one visible thing.

[ Force the party to march in the given direction. ]
[ If the given direction is 'inside', any will do. ]

Carry out forcing:
	let the origin be the location;
	let the way forward be the noun;
	if the noun is the inside:
		repeat with E running through directions:
			let R be the room E from the location;
			if R is not nothing:
				now the way forward is E;
				break;
	let the way back be the opposite of the way forward;
	let the destination be the room the way forward from the origin;
	now the way back of the destination is the way back; [ Remember the direction. ]
	now object in row progress in the journey is the origin; [ Remember the room. ]
	move the party to the destination;
	choose row progress in the journey;
	if silencer is false and the redoes entry is not "":
		say "[redoes entry][paragraph break]";

Book - Action Commands - For the Barbarian

Chapter - Regarding

Regarding-self is an action applying to nothing.
Understand "inventory" as regarding-self.
Understand "take inventory" as regarding-self.

Carry out regarding-self:
	try regarding Conan.

Regarding-room is an action applying to nothing.
Understand "regard" as regarding-room.
Understand "look" as regarding-room.

Carry out regarding-room:
	try looking.

Regarding is an action applying to one visible thing.
Understand "regard [something]" as regarding.
Understand "look [something]" as regarding.
Understand "examine [something]" as regarding.
Understand "read [something]" as regarding.

[ Checking the regarding action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist ybless its the gate (needed for pronoun handling).
3 Regarding a monster gives some flavored response (instead of "nothing interesting").
4 Regarding anything else with a monster present reminds the player it is important.
5 Regarding a direction gives some flavored response (instead of "nothing interesting").
6 Regarding anything else gives a flavored response (variants of "nothing interesting"),
  with the exception of things which need to be regarded as part of the gameplay,
  in which we need to tell the player off when they regard too early or too late,
  and the player character regarding himself ("almost nothing interesting").
]

Check regarding:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the noun is a monster:
		say "[one of]You scrutinize [the noun] with the wisdom of a seasoned warrior[or]With the discernment of a battle-hardened warrior, you examine [the noun][or]You study [the noun] with the insight of a warrior[or]Applying the discerning gaze of a veteran fighter, you scrutinize [the noun][or]With the astuteness of a seasoned combatant, you carefully observe [the noun][or]Drawing on the experience of a battle-scarred warrior, you analyze [the noun] intently[cycling]." instead;
	if a monster (called M) is in the location and M is not the dragon:
		say "[one of]The presence of [the M] demands your full attention[or]Your focus is drawn entirely to [the M][or]All your senses are honed in on [the M][or]The presence of [the M] compels your undivided attention[or]You find yourself completely engaged by the presence of [the M][or]The presence of [the M] captures your entire attention[cycling]." instead;
	if the noun is a direction:
		say "[one of]You survey the land for treasures ripe for the taking[or]You scour the terrain with the keenness of a hunter, ready to claim your spoils[or]You scour the land for treasures to conquer[or]You sweep your surroundings, looking for treasures awaiting your command[or]You search for the glint of untapped treasures awaiting your grasp[cycling]." instead;
	say "[the regard denial of the noun][paragraph break]" instead;

Carry out regarding:
	skip the preceding actions;
	increment the regard-counter of the noun;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;
	
Chapter - Seizing

Seizing is an action applying to one visible thing.
Understand "seize [something]" as seizing.
Understand "take [something]" as seizing.
Understand the commands "get" as "seize".

[ Checking the seizing action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist ybless its the gate (needed for pronoun handling).
3 We cannot seize things we already have.
4 Seizing a monster gives some flavored response (instead of "cannot take that").
5 Seizing anything else with a monster present reminds the player the monster is important.
6 Seizing a direction gives some flavored response (instead of "cannot take that").
7 Seizing anything else gives a flavored response (variants of "cannot take that").
]

Check seizing:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the player has the noun:
		say "[the seize denial of the noun][paragraph break]" instead;
	if the noun is a monster:
		say "[one of][The noun] [seem] a bit beyond a straighforward clash of muscle and bone[or][The noun] [present] a challenge going beyond a mere clash of strength and sinew[or][The noun] [surpass] the simplicity of a straightforward physical confrontation[or]Facing [the noun] requires more than a brute force encounter of muscle and bone[or]The nature of [the noun] transcends a mere clash of physical might[or][The noun] [demand] a strategy beyond a straightforward display of brawn[cycling]." instead;
	if a monster (called M) is in the location:
		say "[one of]Attempting to seize anything while [the M] [are] close would be a dire risk[or]Venturing to seize in [regarding the M][possessive] presence would court danger[or]Risking a seizure while [the M] [are] near could lead to peril[or]Seizing anything while [the M] [are] close might bring about dire consequences[or]Attempting a seize while [the M] [lurk] nearby would be a dangerous move[or]Seizing amidst [the M]'s proximity would pose a grave threat[cycling]." instead;
	if the noun is a direction:
		say "[one of]You are a warrior, not a plunderer of intangible notions[or]You bear the heart of a fighter, not a scavenger of elusive abstractions[or]Your spirit hungers for battle, not for the intangible spoils of thoughts[or]Your essence is that of a warrior, not a seeker of intangible concepts[or]Your soul is forged in combat, not enticed by intangible abstractions[or]Your nature thrives in combat, not in the pursuit of intangible notions[cycling]." instead;
	say "[the seize denial of the noun][paragraph break]" instead;

[ Additional code is needed to handle the One Choice.
  The action chosen by the player at the end of the preamble decides the journey:
- Choosing the sword leads to the path of Order vs Chaos.
- Choosing the axe leads to the path of Good vs Evil.
]

Carry out seizing:
	let N be the number of rows in the journey;
	if progress > N:
		now progress is 1;
		if the noun is the sword:
			now the choice is 1; [ Order vs Chaos ]
		else:
			now the choice is 2; [ Good vs Evil ]
		now the journey is entry choice plus one of the journeybook;
	skip the preceding actions;
	now object in row progress in the journey is the holder of the noun; [ For UNDO. ]
	transfer the noun to the player;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;

Chapter - Looting

Looting is an action applying to one visible thing.
Understand "loot [something]" as looting.
Understand the commands "search" and "pilfer" as "loot".

[ Checking the looting action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist unless its the gate (needed for pronoun handling).
3 Looting a monster gives some flavored response (instead of "cannot loot that").
4 Looting anything else with a monster present reminds the player the monster is important.
5 Looting a direction gives some flavored response (instead of "cannot loot that").
6 Looting anything else gives a flavored response (variants of "cannot loot that").
]

Check looting:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the noun is a monster:
		say "[one of]Make sure [the noun] [are] no longer moving before attempting to loot [them][or]Wait until [the noun] [lie] still before you consider looting [them][or]Your priorities should lie with defeating [the noun], not looting [them][or]Deal with [the noun] first, looting comes after victory[or]Survive the battle before thinking of looting [the noun][or]Plundering an enemy is a breach of warrior's honor[cycling]." instead;
	if a monster (called M) is in the location:
		say "[one of]Looting can wait until you have dealt with [the M][or]Put your focus on the battle ahead, looting can follow victory[or]Let's deal with [the M] before we get to the spoils[or]Concentrate on the fight at hand, plundering can come later[or]Let's settle this battle before we go searching for trinkets[or]Your priorities should be [regarding the M][possessive] defeat[cycling]." instead;
	if the noun is a direction:
		say "[one of]You are a warrior, not a plunderer of intangible notions[or]You bear the heart of a fighter, not a scavenger of elusive abstractions[or]Your spirit hungers for battle, not for the intangible spoils of thoughts[or]Your essence is that of a warrior, not a seeker of intangible concepts[or]Your soul is forged in combat, not enticed by intangible abstractions[or]Your nature thrives in combat, not in the pursuit of intangible notions[cycling]." instead;
	say "[the loot denial of the noun][paragraph break]" instead;

Carry out looting:
	skip the preceding actions;
	let R be a random thing held by the noun; [ We assume only one thing in there. ]
	now object in row progress in the journey is R; [ Remember gained loot for UNDO. ]
	transfer R to the player;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;

Chapter - Presenting

Presenting it to is an action applying to one visible thing and one visible thing.
Understand "present [something] to [something]" as presenting it to.
Understand "present [something] to" as presenting it to.
Understand "present [something]" as presenting it to.
Understand the commands "p", "give", "offer", "sacrifice" as "present".

[ Checking the presenting action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist unless its the gate (needed for pronoun handling).
3 Presenting a direction gives some flavored response (instead of "cannot present that").
4 Presenting something not in possession of the player gives a generic response.
5 Presenting anything with a monster present reminds the player the monster wants his hide.
6 Presenting something with nobody else in the room gives a generic response.
7 Presenting something gives a flavored response (variants of "cannot present that").
]

Rule for supplying a missing second noun while presenting:
	now the second noun is the recipient.

Check presenting it to:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "If you want to present something, make sure you've got it in your possession first." instead;
	if the noun is a direction:
		say "[one of]Directions can't be handed over like a prize[or]Directions aren't trinkets to pass around casually[or]Directions aren't for trading like coins in a market[or]Don't think of directions as something to be bartered[or]Directions are earned by forging your own path, not given[or]Directions are won through the sweat of your own journey, not given freely[cycling]." instead;
	if the noun is unpresentable:
		say "If you want to present something, make sure you've got it in your possession first." instead;
	if the second noun is a monster (called M):
		say "[one of][The M] [appear] to be more interested in rending flesh[or][Regarding the M][Possessive] eyes gleam with hunger, and it's not for treasure[or]Seems [the M] [hunger] for combat, not for baubles[or][Regarding the M][Possessive] focus is on battle, not on material gains[or]Treasure's the least of [regarding the M][possessive] concerns, survival is [their] aim[or]Right now, it's your hide [the M] [want], not your loot[cycling]." instead;
	if the second noun is not a person or the second noun is an animal:
		say "[one of]Are you trying to bribe [the second noun]? Save your generosity for more sensible things[or]Gifts won't sway [the second noun], you better focus on accomplishable tasks[or]Attempting to bribe [the second noun] is like throwing pearls in the ocean[or][The second noun] [have] no eyes for gifts. Channel your efforts where they will be better spent[or]Save your gifts for worthy adversaries[cycling]." instead;
	if the second noun is Conan:
		say "Self-gifting has its merits, but let's keep moving." instead;
	say "[the present denial of the noun][paragraph break]" instead;

Carry out presenting it to:
	skip the preceding actions;
	increment the present-counter of the noun;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;

Chapter - Treating

Treating is an action applying to one visible thing.
Understand "treat with/to/-- [something]" as treating.
Understand the commands "parley", "t", "talk" as "treat".

[ Checking the treating action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist unless its the gate (needed for pronoun handling).
3 Treating a monster gives some flavored response (instead of "not interested in words").
4 Treating someone else with a monster present reminds the player the monster is important.
  The dragon is a special case to allow the player to engage with party members.
5 Treating a direction gives some flavored response (instead of "cannot talk to the wind").
6 Treating with something gives a flavored response (instead of "cannot talk to it").
7 Treating with someone gives a flavored response (variants of "not interested").
]

Check treating:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the noun is a monster:
		say "[one of]Your words won't sway [the noun][or]Speaking to [the noun] is an exercise in futility[or][The noun] [understand] the language of strength, not diplomacy[or]Save your words for allies, for [the noun] [know] only bloodlust[or]In the eyes of [the noun], your words mean nothing – only your actions[or]Trying to reason with [the noun] like this is like shouting at the wind[cycling]." instead;
	if a monster (called M) is in the location and M is not the dragon:
		say "[one of][Regarding the M][Possessive] presence demands our full attention[or]Let us deal with [the M] before we exchange words[or]Let us focus on dealing with [the M] first[or][Regarding the M][Possessive] threat looms larger than any conversation[or][Regarding the M][Possessive] growls drown out your words[or]We must first ensure our safety against [regarding the M][possessive] fury[cycling]." instead;
	if the noun is a direction:
		say "[one of]Compass directions don't heed words or diplomacy[or]North, south, east, west... they answer only to the instincts of battle[or]The points of the compass care naught for discourse or negotiation[or]Directions are as wild as the wind, following their own course, unmoved by words[or]Words won't sway the compass, only the steel of a warrior's will[or]The compass knows no language but action, no persuasion but the clash of blades[cycling]." instead;
	if the noun is not a person or the noun is an animal:
		say "[one of]Are you trying to negotiate with [the noun]? Save your eloquence for more sensible beings[or]Words won't sway [the noun], you better focus on accomplishable tasks[or]Let's leave negotiations to beings that can actually listen and respond[or]Attempting to parley with [the noun] is like speaking to the wind[or][The noun] [have] no ears for words. Channel your efforts where they will be better spent[or]Words fall on deaf ears when spoken to [the noun]. Save your breath for worthy adversaries[cycling]." instead;
	say "[the treat denial of the noun][paragraph break]" instead;

Carry out treating:
	skip the preceding actions;
	increment the treat-counter of the noun;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;

Chapter - Smiting

Smiting is an action applying to one visible thing.
Understand "smite [something]" as smiting.
Understand the commands "a", "attack", "break", "smash", "hit", "fight", "wreck", "crack", "destroy", "kill", "strike" as "smite".

[ Checking the smiting action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 We treat closed doors as if they do not exist unless its the gate (needed for pronoun handling).
3 Smiting a monster unarmed gives some flavored response (instead of "you need a weapon").
4 Smiting anything else with a monster present reminds the player the monster is important.
5 Smiting a direction gives some flavored response (instead of "cannot attack the wind").
6 Smiting something gives a flavored response (variants of "cannot smite that").
]

Check smiting:
	if the current action is listed in the allowed-actions, make no decision;
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the noun is a monster and Conan is unarmed:
		say "[one of]You have no weapon to smite [the noun] with[or]Find a suitable blade before you face [the noun][or]Without a weapon, [the noun] [remain] untouched[or][The noun] [remain] safe from your blows until you find a weapon[or]Your fists alone won't do much against [the noun]. Seek a weapon[or]This battle must wait until you have armed yourself[cycling]." instead;
	if the noun is not a monster and a monster (called M) is in the location and M is not the dragon:
		say "[one of]Let's focus on [regarding the M][possessive] threat before us[or]Focus on [the M][or]Save your strength for [the M][or]Deal with [the M] first, then we can address other matters[or]Our concern should be [the M] in our path, not other targets[or]Prioritize [the M] at hand; the rest can wait[cycling]." instead;
	if the noun is a direction:
		say "[one of]Conserve your energy for living adversaries[or]Let's reserve our blows for tangible enemies[or]I suggest we save our strikes for more material foes[or]Let's focus that energy on foes that can actually feel the sting of your blows[or]Direct your rage at creatures that can retaliate[or]Focus on foes that can strike back[cycling]." instead;
	say "[the smite denial of the noun][paragraph break]" instead;

Carry out smiting:
	skip the preceding actions;
	increment the smite-counter of the noun;
	if the noun is a container:
		let R be a random thing held by the noun; [ We assume only one thing in there. ]
		now object in row progress in the journey is R; [ Remember the loot we gained. ]
		transfer R to the player;
	if silencer is false, say "[redoes in row progress in the journey][paragraph break]";
	move forward;

Chapter - Marching

Marching-in is an action applying to one visible thing.
Understand "enter [something]" as marching-in.
Understand "enter" as marching-in.

Rule for supplying a missing noun while marching-in:
	if the location is r-throneroom and the portal is open:
		now the noun is the portal; [portal]
	else if the location is r-northbank and the northdoor is open:
		now the noun is the northdoor; [vehicle]
	else if the location is r-southbank and the gate is open:
		now the noun is the gate; [gate]
	else if the location is r-southbank and the southdoor is open:
		now the noun is the southdoor; [vehicle]
	else if the location is r-crypt and the bedroom door is open:
		now the noun is the bedroom door; [portal]
	else if the location is r-hall and the cavern door is open:
		now the noun is the cavern door; [portal]
	else if the location is r-cavern-5 and the crypt door is open:
		now the noun is the crypt door; [portal]
	else:
		say "You do not see anything worthwhile to enter.";

Check marching-in:
	if the location is r-throneroom and the portal is open and the noun is the portal:
		try marching west instead;
	else if the location is r-northbank and the northdoor is open and the noun is the northdoor:
		try marching south instead;
	else if the location is r-southbank and the noun is the gate:
		try marching south instead;
	else if the location is r-southbank and the southdoor is open and the noun is the southdoor:
		try marching north instead;
	else if the location is r-crypt and the bedroom door is open and the noun is the bedroom door:
		try marching north instead;
	else if the location is r-hall and the cavern door is open and the noun is the cavern door:
		try marching down instead;
	else if the location is r-cavern-5 and the crypt door is open and the noun is the crypt door:
		try marching down instead;
	else:
		say "Only gates, portals, and vehicles are worthy of entering." instead;

Marching-out is an action applying to nothing.
Understand "leave" as marching-out.

Check marching-out:
	if the location is r-bedroom, try marching west instead;
	if the location is r-diningroom, try marching west instead;
	if the location is r-throneroom and the portal is open, try marching west instead;
	if the location is r-throneroom, try marching south instead;
	if the location is r-darkriver and the northdoor is open, try marching north instead;
	if the location is r-darkriver and the southdoor is open, try marching south instead;
	if the location is r-guards, try marching up instead;
	if the location is r-pit, try marching up instead;
	if the location is r-corridor, try marching west instead;
	if the location is r-crypt, try marching south instead;
	if the location is r-hall, try marching west instead;
	if the location is r-cavern-5 and the crypt door is open, try marching down instead;
	say "You do not see a way out." instead;

Marching is an action applying to one visible thing.
Understand "march" as marching.
Understand "march [direction]" as marching.
Understand "march [text]" as a mistake ("[xyzzy]").
Understand the commands "go", "walk", "run" as "march".

Rule for supplying a missing noun while marching:
  say "If you want to march somewhere, you'll need to say which direction to march.".

[ Action variables similar to the set used by the going action. ]

The marching action has an object called the origin.        [ the location ]
The marching action has an object called the destination.   [ <calculated> ]
The marching action has a direction called the way forward. [ the noun ]
The marching action has a direction called the way back.    [ the opposite of the noun ]

[ We convert going to marching (going is implied by providing a direction without a verb). ]

The convert going to marching rule is listed first in the check going rules.
Check going (this is the convert going to marching rule):
	convert to the marching action on the noun.

[ Checking the marching action
1 If the action is an allowed action, proceed; otherwise we need to say why it fails.
2 If there is no exit in the provided direction, we provide a suitable flavored refusal:
- Marching up or down invokes the vertical refusal.
- Marching inside or outside is treated as any other unknown direction.
- Marching in a compass direction {n,ne,e,se,s,sw,w,nw} invokes the horizontal refusal.
3 If there is a monster in the location, we consider the marching to be untimely.
4 If we go back to where we came from, we give a flavored response ("no going back").
5 If it is not yet time to march, we consider the marching to be untimely.
6 Otherwise we try to march into the black hole, and we give the blackhole refusal.
]

To decide if we can move onwards:
	repeat with A running through allowed-actions:
		if the action name part of A is the marching action, decide yes;
	decide no;

Check marching:
	if the noun is inside:
		try marching-in nothing instead;
	if the noun is outside:
		try marching-out instead;
	now the origin is the location;
	now the way forward is the noun;
	now the way back is the opposite of the way forward;
	now the destination is the room the way forward from the origin;
	let D be the door the way forward from the origin;
	if D is not nothing and D is closed, now the destination is nothing;
	if the current action is listed in the allowed-actions, make no decision;
	if the destination is nothing:
		if the noun is listed in {up, down}:
			say "[the vertical refusal of the origin][line break][list the exits]" instead;
		else:
			say "[the horizontal refusal of the origin][line break][list the exits]" instead;
	if a monster (called M) is in the location:
		say "[the untimely refusal of the location][paragraph break]" instead;
	if the way forward is the way back of the location:
		say "[one of]Turn your gaze ahead, for there's no honor in retracing your steps[or]Press onward, for the path you've trod lies behind you now[or]Do not falter, as looking back will not unveil new horizons[or]Face the challenges ahead, as returning is not the path of the brave[or]Keep moving forward, for the past has no place in the warrior's journey[or]To retreat now would be to deny the spirit of adventure coursing through your veins[cycling]." instead;
	if we can move onwards:
		say "[the blackhole refusal of the origin][paragraph break]" instead;
	say "[the untimely refusal of the location][paragraph break]" instead;

Carry out marching:
	skip the preceding actions;
	now the way back of the destination is the way back; [ Remember the direction. ]
	now object in row progress in the journey is the origin; [ Remember the room. ]
	if silencer is false:
		let R be the redoes in row progress in the journey;
		if R is "":
			let O be the printed name of the origin in lower case;
			say "You stride purposefully from the [O], your powerful legs carrying you with a determined pace as you head [way forward].";
		else:
			say "[R][paragraph break]";
	move the party to the destination;
	move forward;

Book - Fun Commands - For the Player

Chapter - Licking

Licking is an action applying to one visible thing.
Understand "lick [something]" as licking.

Check licking:
	if the noun is a closed door and (the noun is not the gate or the location is not r-southbank):
		say "You find nothing of the sort in this vicinity." instead;
	if the noun is a monster:
		say "[one of]Attempting to lick [the noun] would be a dire risk[or]Venturing to lick [the noun] would court danger[or]Risking to lick [the noun] could lead to peril[or]Licking [the noun] might bring about dire consequences[or]Attempting to lick [the noun] would be a dangerous move[or]Licking [the noun] would pose a grave threat[cycling]." instead;
	if a monster (called M) is in the location:
		say "[one of]Attempting to lick anything while [the M] [are] close would be a dire risk[or]Venturing to lick in [regarding the M][possessive] presence would court danger[or]Risking a licking while [the M] [are] near could lead to peril[or]Licking anything while [the M] [are] close might bring about dire consequences[or]Attempting a lick while [the M] [lurk] nearby would be a dangerous move[or]Licking amidst [the M]'s proximity would pose a grave threat[cycling]." instead;
	if the noun is a direction:
		say "[one of]You are a warrior, not a licker of intangible notions[or]You bear the heart of a fighter, not a licker of elusive abstractions[or]Your spirit hungers for battle, not for the licks of thoughts[or]Your essence is that of a warrior, not a licker of intangible concepts[or]Your soul is forged in combat, not enticed by licking intangible abstractions[or]Your nature thrives in combat, not in the licking of intangible notions[cycling]." instead;
	say "You are a warrior, not a beta tester trying to lick [the noun]." instead;

Chapter - Xyzzying

Xyzzying is an action applying to nothing.
Understand "xyzzy" as xyzzying.

Check xyzzying:
	if a monster (called M) is in the location:
		say "[one of]Attempting to perform magic while [the M] [are] close would be a dire risk[or]Venturing to perform magic in [regarding the M][possessive] presence would court danger[or]Performing magic while [the M] [are] near could lead to peril[or]Performing any magic while [the M] [are] close might bring about dire consequences[or]Attempting magic while [the M] [lurk] nearby would be a dangerous move[or]Performing magic amidst [the M]'s proximity would pose a grave threat[cycling]." instead;
	if dream-reached is true:
		if the location is r-crypt:
			say "The sage sighs. 'The veil of causality has already been torn.'" instead;
		else:
			say "Across the mists of time, these words come to you: [i]'The veil of causality has already been torn.'[r]" instead;
	if the location is not r-throneroom:
		if the location is r-crypt:
			say "The sage sighs. 'Have you not figured it out yet? This is not the place!'" instead;
		else:
			say "Across the mists of time, these words come to you: [i]'This is not the place.'[r]" instead;
	if progress is not 9 or choice is not 1:
		say "Across the mists of time, these words come to you: [i]'This is not the time. That moment has passed.'[r]" instead;

Carry out xyzzying:
	now dream-reached is true;
	say "You feel your eardrums pop as the pressure in the throne room changes. Across the mists of time, these words come to you: [i]'The veil of causality has been torn. Now go and fulfill your destiny!'[r]";

Book - Meta Commands - For the Tester

[ Taken from Jim Aikin's Inform 7 Handbook. ]

After reading a command (this is the ignore beta-comments rule):
	if the player's command matches the regular expression "^\p" or the player's command matches the regular expression "^\*":
		say "(Noted.)";
		reject the player's command.

Book - Meta Commands - For the Player

Chapter - Abouting

[ My message to the player. ]

Abouting is an action out of world and applying to nothing.
Understand "about" or "credits" or "info" as abouting.

Report abouting:
	say "This story is written by Onno Brouwer for IFComp 2023.[line break]The game concept is inspired by the Single Choice Jam 2023 premise: to build a game with a single choice.[line break]The original story is inspired by the works of Robert E. Howard in general, and the short story 'The Phoenix on the Sword,' in particular, first published in Weird Tales magazine in December 1932.[paragraph break]'One King to Loot them All' is Merciful on the Zarfian cruelty scale. Hints are available via [b]HINT[r].[paragraph break]Original cover art by RION.[paragraph break]Many thanks to:[paragraph break]- The Neo-Interactives Group for organizing the Single Choice Jam which led to the creation of this game.[line break]- Zed Lopez, for all the shared Inform7 lore and expertise.[line break]- Manonamora, for taking the plunge and offering help as a beta tester.[line break]- Pinkunz, for creating the Player Primer.[line break]- All the kind folk at ClubFloyd for further beta testing: Joey 'Jess' Cramsey (Inventor), Roger Carbol (Roger), Oliver Frank (Knight_Otu), David Welbourn (DavidW), (Keltena), (Jade), (Gadzoks), (Fos1), (Pinkunz), and Jacqueline A. Lott (Jacqueline) for organizing the whole thing.[line break]- All the kind folk at intfiction.org who helped me navigate the wonderful world of IF.[paragraph break]This is a work of fiction. Any names or characters, businesses or places, events or incidents, are fictitious. Any resemblance to actual entities, living, dead, undead, or immortal, or actual events is purely coincidental.".

Chapter - Helping

The help mode is a feature flag that varies. [ Default ff-unknown. ]

[ Explaining the commands understood by the game.
  Intentionally a very small set of commands, to make it work for the Single Choice Jam.
  Only after I started this project I learned there was a Barbarian-themed game out there
  which was going for a similar experience, intentionally limiting the set of actions
  available to the player (Treasures of a Slaver's Kingdom by S. John Ross). I checked it
  out and borrowed its help style for my help message. To make my help fit the theme,
  I flavored the explanations for all commands, including the meta commands.
  As a little in-joke, the example nouns are replaced with the allowed action noun.
  So one could do a 'story mode' by asking for a HINT (which gives the verb), and then
  HELP (which gives the noun unless the action is marching). Do not tell anyone! :D
]

Helping is an action out of world and applying to nothing.
Understand "help" or "h" or "verb" or "verbs" or "v" or "command" or "commands" as helping.

The help-nouns is a list of texts that varies.
The help-nouns are {"STATUE","SWORD","CHEST","OFFERING","GUARD","DEMON"}.

To decide whether (A - an action-index) matches (N - an action name):
	if A is regard and N is the regarding action, decide yes;
	if A is seize and N is the seizing action, decide yes;
	if A is loot and N is the looting action, decide yes;
	if A is present and N is the presenting it to action, decide yes;
	if A is treat and N is the treating action, decide yes;
	if A is smite and N is the smiting action, decide yes;
	decide no;

To decide what text is the (A - an action-index) help-noun:
	let I be A cast as a number;
	let AA be the action name part of entry 1 in the allowed-actions;
	let AN be the noun part of entry 1 in the allowed-actions;
	if A matches AA, decide on "[the printed name of AN in upper case]";
	decide on "[entry I of the help-nouns]".

Report helping:
	say "The following 'action commands' are known to the Barbarian: [b]REGARD[r] (scrutinize with suspicion or interest), [b]SEIZE[r] (forcefully take), [b]LOOT[r] (forcefully take the contents of), [b]PRESENT[r] (offer willingly), [b]TREAT WITH[r] (negotiate peacefully), and [b]SMITE[r] (strike with powerful force). These verbs must be followed by a noun, for example: REGARD [regard help-noun]; SEIZE [seize help-noun]; LOOT [loot help-noun]; PRESENT [present help-noun]; TREAT WITH [treat help-noun]; SMITE [smite help-noun].[paragraph break]Type a direction (NORTH, NORTHEAST, EAST, SOUTHEAST, SOUTH, SOUTHWEST, WEST, NORTHWEST, UP, or DOWN) to travel. The proper command is [b]MARCH[r], but direction alone will suffice.[paragraph break]The following 'meta commands' are for the Player rather than the Barbarian: [b]HINT[r] (a whisper of direction at a price - a bounty of pride), [b]UNDO[r] (reverse the flow of time to a moment just passed), [b]QUIT[r] (sheathe your blade and rest your battle-weary soul), [b]SAVE[r] (preserve the essence of your adventure), [b]RESTORE[r] (recall the essence of your preserved adventure), [b]RESTART[r] (abandon your path and make a fresh beginning), and [b]ABOUT[r] (reveal the very essence of this realm)[paragraph break]The following 'uber meta commands' are for the Reader rather than the Player: [b]STORY MODE ON[r] (enable story mode), [b]STORY MODE OFF[r] (disable story mode)[if commentary mode is not ff-unknown], [b]COMMENTARY ON[r] (enable commentary mode), [b]COMMENTARY OFF[r] (disable commentary mode)[end if].";

After printing the banner text:
	if the help mode is ff-unknown:
		say "[i]New players should type [b]HELP[i] for instructions. This game uses non-standard verbs.[r]";
		now the help mode is ff-enabled.

Chapter - Hinting

[ A rather obvious hint for the verb to be used by the player. ]

Hinting is an action out of world.
Understand "hint" or "clue" as hinting.

Report hinting:
	if reversed-flow is true:
		say "[one of]Consider an [b]UNDO[r] to rewind the hands of time[or]A wise adventurer knows when to [b]UNDO[r] and turn back the clock[or]An [b]UNDO[r] could unveil secrets hidden in the past[or]To find what's lost, you must dare to [b]UNDO[r] time[or]Unlock hidden truths by daring to [b]UNDO[r] the flow of time[cycling].";
	else:
		let A be the action name part of entry 1 in the allowed-actions;
		let N be the noun part of entry 1 in the allowed-actions;
		if A is the regarding action, say "[one of]It might be wise to [b]REGARD[r] your surroundings[or]A true warrior must take a moment to [b]REGARD[r] his surroundings[or]A [b]REGARD[r] might reveal something valuable[or]A [b]REGARD[r] might reveal what your eyes initially missed[or][b]REGARD[r] your surroundings and unveil the hidden truths[cycling].";
		if A is the seizing action, say "[one of]Sometimes, to [b]SEIZE[r] something is the only way to truly possess what you need[or]Do not hesitate to [b]SEIZE[r] what is rightfully yours[or][b]SEIZE[r] the opportunity when it presents itself[or]If something is within reach, [b]SEIZE[r] it and make it your own[or]If something nearby can aid your quest, [b]SEIZE[r] it without hesitation[cycling].";
		if A is the looting action, say "[one of]Do not hesitate to explore and [b]LOOT[r] what might hold riches[or]Consider [b]LOOT[r]ing to uncover valuable items[or]Search your surroundings for [b]LOOT[r]able opportunities[or][b]LOOT[r]ing can unveil the rewards of your endeavors[or]A true warrior understands the value of [b]LOOT[r]ing[cycling].";
		if A is the presenting it to action, say "[one of]If you have something fitting, [b]PRESENT[r]ing it might yield valuable results[or]Consider [b]PRESENT[r]ing something to someone who might appreciate it[or][b]PRESENT[r]ing something to the right person at the right time could lead to unexpected benefits[or][b]PRESENT[r]ing something significant could lead to interesting interactions[or][b]PRESENT[r]ing something might reveal insights or create favorable outcomes[cycling].";
		if A is the treating action, say "[one of]Consider [b]TREAT[r]ing with individuals to gather information or seek assistance[or][b]TREAT[r]ing with others might offer insights into their intentions or reveal hidden opportunities[or][b]TREAT[r]ing with someone could help you navigate challenges in unexpected ways[or][b]TREAT[r]ing with individuals might reveal their disposition and offer insights into their trustworthiness[or][b]TREAT[r]ing with someone might unveil options you hadn't considered and open up new paths forward[cycling].";
		if A is the smiting action, say "[if N is a person][one of]Consider to [b]SMITE[r] foes to defend yourself and overcome challenges[or][b]SMITE[r] to turn the tide of battle and vanquish those who stand in your way[or]A well-placed [b]SMITE[r] can shatter barriers and forge your destiny[or][b]SMITE[r] your enemies to assert your warrior's spirit[or][b]SMITE[r] to break through even the most formidable foes and pave the way for your triumph[cycling][else][one of]A decisive [b]SMITE[r] might reveal hidden treasures[or]A [b]SMITE[r] can sometimes uncover valuable secrets[or][b]SMITE[r] to reveal the potential loot that might be concealed within[or]A good [b]SMITE[r] can reveal the riches you seek[or]A strong [b]SMITE[r] can break open more than just enemies[cycling][end if].";
		if A is the marching action, say "[one of]It might be time to [b]MARCH[r] forward and face the challenges lying ahead[or]A purposeful [b]MARCH[r] forward could lead you to the heart of your quest[or]Sometimes the best option is to [b]MARCH[r] onward and uncover what lies in wait[or]The winds of fate whisper that progress is made by [b]MARCH[r]ing forward[or]Consider a determined [b]MARCH[r] to embrace your heroic journey[cycling].";

Chapter - Undoing

[ I handle undo myself for the following reasons:
- I decided to make undo part of the storyline; at the end of the path of Order vs Chaos,
  the player meets the sage, who will suggest to "reverse the flow of time"
  which is what undo does. Then the player uses undo until breaking through the barrier
  where the player originally started, and gets thrown back to the start of the preamble,
  where the player has to make the One Choice.
- I wanted to show the player the effect of the undo, (the action executed in reverse,)
  since it is a major part of the storyline followed by the player, and I wanted to show
  the players feedback for their efforts instead of what undo does (just show the room).
]

Undoing is an action out of world and applying to nothing.
Understand "undo" as undoing.

[ Checking the undoing action
1 If we are at start of the preamble, we give a flavored response ("You cannot undo...").
2 If we are at start of the game and the player hasn't met the sage, we refuse ("Just...").
3 If the allowed action is the smiting action and its target is an injured monster,
  we increase drama by refusing to undo at this point until the monster has been defeated.
  This applies to e.g. the demon killing our priestess when the wrong choice has been made.
  Then the player will see the killing blow delivered with the sword, which matches the
  intro text, hinting at history repeating itself when the player proceeds down this path.
] 

Check undoing:
	if progress is 1 and choice is 0:
		say "[one of]You have just awoken from the realm of dreams. Now is the time to seize your adventure[or]Undoing now would be like retreating before the battle has even begun[or]You've just woken from the dream realm. The time for decisive action is upon you[or]A true warrior faces the unknown with determination, not by retracing their steps[or]The realm of dreams is behind you. Embrace the present challenge without hesitation[cycling]." instead;
	if RW-restarting is true, make no decision;
	if progress is 9 and choice is 1 and dream-reached is false:
		say "You have just embarked upon your adventure." instead;
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the smiting action, make no decision;
	let N be the noun part of entry 1 in the allowed-actions;
	if N is not a monster, make no decision;
	if N is injured, say "[one of]In the heat of battle, there's no turning back[or]The dance of combat waits for no one[or]The battle's fury burns too bright for second chances[or]The gods of battle frown upon retracing steps mid-fight[cycling]." instead;

[ Reversing the flow of time
  We keep undoing when reversing the flow of time until we are about to switch rooms.
  When we are in the palace region, we silence undo messages until we reached the start.
  Then we play a dream message and the game proceeds from the moment the player wakes up.
  In case we perform a Roger Wilco restart, we keep undoing until we reach the game start.
]

To decide if reversion is stopped:
	if reversed-flow is false:
		while progress > 1:
			choose row (progress - 1) in the journey;
			if type entry >= 0 or flag entry > 0, break;
			decrement progress;
		decide yes;
	if progress is 1 and choice is 0:
		now reversed-flow is false;
		if RW-restarting is false:
			now silencer is false;
			say "[i]Time stretches, and events reverse faster than you can follow. Light around you then fades, leaving you surrounded in darkness. Even blinded, you sense a stirring. You feel the corruption growing around you, drawing closer and closer. A menacing power rises from the fortress in the night and approaches, its wings beating in the darkness. The air becomes suffocating and hot, and you sense something strangling you...[r]";
			say "[line break]--- ( press any key ) ---[line break]";
			wait for any key;
			say "You wake up, untangle the sheet from around your throat, throw it aside, and get out of bed.";
		decide yes;
	if RW-restarting is true, decide no;
	if the location is in palace-region:
		now silencer is true;
		decide no;
	let p be the progress;
	while true is true:
		decrement p;
		choose row p in the journey;
		let A be the action name part of action entry;
		if A is the marching action, decide yes;
		if A is not a world action, decide no;

Carry out undoing:
	while true is true:
		decrement progress;
		choose row progress in the journey;
		let A be the action name part of action entry;
		let N be the noun part of action entry;
		let S be the second noun part of action entry;
		if silencer is false and flag entry is not 0 and undoes entry is not "":
			say "[undoes entry][paragraph break]";
		now flag entry is 0;
		if A is the regarding action:
			decrement the regard-counter of N;
		else if A is the seizing action:
			let O be the object entry; [ the location we seized it from ]
			transfer N to O;
		else if A is the looting action:
			let O be the object entry;
			transfer O to N;
		else if A is the presenting it to action:
			decrement the present-counter of N;
		else if A is the treating action:
			decrement the treat-counter of N;
		else if A is the smiting action:
			if N is a container:
				let O be the object entry;
				transfer O to N;
			decrement the smite-counter of N;
			if the smite-counter of N is greater than 0, next;
		else if A is the marching action:
			let the origin be the object entry;
			if silencer is false and the undoes entry is "":
				let O be the printed name of the origin in lower case;
				say "You retrace your steps with a determined air, retreating back to the [O].";
			now the location is unvisited; [ we forget we ever visited it ]
			move the party to the origin;
		else if A is the transferring it to action:
			let O be the object entry;
			transfer N to O;
			next;
		else if A is the swapping it with action:
			let NH be the holder of N;
			let SH be the holder of S;
			transfer S to NH;
			transfer N to SH;
			next;
		else if A is the toggling action:
			if N is open:
				now N is closed;
				if N is a door, now N is not apparent;
			else:
				now N is open;
				if N is a door, now N is apparent;
			next;
		else if A is the forcing action:
			let the origin be the object entry;
			if silencer is false and the undoes entry is "" and the redoes entry is not "":
				let O be the printed name of the origin in lower case;
				say "You retrace your steps with a determined air, retreating back to the [O].";
			now the location is unvisited; [ we forget we ever visited it ]
			move the party to the origin;
			next;
		if progress is 1 and choice is not 0: [ need to switch back to end of preamble ]
			now the choice is 0;
			now the journey is entry choice plus one of the journeybook;
			now progress is the number of rows in the journey;
			increment progress;
		if reversion is stopped, break;
	load the allowed actions;
	if silencer is false and the current room is not the location, try looking;
	now the current room is the location;

Chapter - Starting the walkthrough

The walkthrough mode is a feature flag that varies. [ Default ff-unknown. ]

Starting the walkthrough is an action out of world and applying to nothing.
Understand "story mode on/--" as starting the walkthrough.

Check starting the walkthrough:
	if the walkthrough mode is ff-disabled, make no decision;
	if the walkthrough mode is ff-enabled, say "The story mode has already been enabled. Use [b]STORY MODE OFF[r] to disable the story mode." instead;
	say "The story mode is not available right now." instead;

Carry out starting the walkthrough: now the walkthrough mode is ff-enabled.

Report starting the walkthrough: say "The story mode has been enabled. Press the [b]ENTER[r] key at any time without a command to automatically execute the next command. Use [b]STORY MODE OFF[r] to disable the story mode."

Chapter - Stopping the walkthrough

Stopping the walkthrough is an action out of world and applying to nothing.
Understand "story mode off" as stopping the walkthrough.

Check stopping the walkthrough:
	if the walkthrough mode is ff-enabled, make no decision;
	if the walkthrough mode is ff-disabled, say "The story mode has already been disabled. Use [b]STORY MODE ON[r] to enable the story mode." instead;
	say "The story mode is not available right now." instead;

Carry out stopping the walkthrough: now the walkthrough mode is ff-disabled.

Report stopping the walkthrough: say "The story mode has been disabled. Now pressing the [b]ENTER[r] key at any time without a command will result in a reminder for re-enabling story mode. Use [b]STORY MODE ON[r] to re-enable the story mode."

Chapter - Stepping through the walkthrough

Rule for repairing an empty command: change the text of the player's command to "readme".

Stepping through the walkthrough is an action out of world and applying to nothing.
Understand "readme" as stepping through the walkthrough.

[ Choose an action which progresses the story. ]

Check stepping through the walkthrough:
	if the walkthrough mode is ff-enabled, make no decision;
	say "[speakup]";
	stop the action.

Carry out stepping through the walkthrough:
	if reversed-flow is true:
		try undoing;
	else:
		let A be entry 1 of allowed-actions;
		if progress > the number of rows in the journey:
			let N be the number of entries of allowed-actions;
			now A is entry N of allowed-actions;
		try A;

Chapter - Starting the commentary

The commentary mode is a feature flag that varies. [ Default ff-unknown. ]

Starting the commentary is an action out of world and applying to nothing.
[Understand "commentary on/--" as starting the commentary.]

Check starting the commentary:
	if the commentary mode is ff-disabled, make no decision;
	if the commentary mode is ff-enabled, say "The commentary has already been enabled. Use [b]COMMENTARY OFF[r] to disable the commentary." instead;
	say "The commentary is not available right now." instead;

Carry out starting the commentary: now the commentary mode is ff-enabled.

Report starting the commentary: say "The commentary has been enabled. Use [b]COMMENTARY OFF[r] to disable the commentary."

[
To roger (t - a sayable value):
	if the commentary mode is ff-enabled:
		say "[i](Roger): [t][r][line break]".

To wilco (t - a sayable value):
	if the commentary mode is ff-enabled:
		say "[i](Wilco): [t][r][line break]".
To say >console:
	(- if ( 1 == 1 ) {-open-brace} style underline; -).
To say <:
	(- style roman; {-close-brace} -).
]

Chapter - Stopping the commentary

Stopping the commentary is an action out of world and applying to nothing.
[Understand "commentary off" as stopping the commentary.]

Check stopping the commentary:
	if the commentary mode is ff-enabled, make no decision;
	if the commentary mode is ff-disabled, say "The commentary has already been disabled. Use [b]COMMENTARY ON[r] to enable the commentary." instead;
	say "The commentary is not available right now." instead;

Carry out stopping the commentary: now the commentary mode is ff-disabled.

Report stopping the commentary: say "The commentary has been disabled. Use [b]COMMENTARY ON[r] to re-enable the commentary."

Chapter - Roger Wilco restarting the game

RWrestarting is an action out of world and applying to nothing.
[Understand "restart roger wilco" as RWrestarting.]

Check RWrestarting:
	say "Is the thirst for a fresh beginning coursing through your veins? >";
	if the player consents:
		continue the action;
	stop the action.

[ Performing a Roger Wilco restart
- If this is the first time the player uses this command, enable commentary and walkthrough modes.
- Perform a "restart" of the game.
]

Carry out RWrestarting:
	if commentary mode is ff-unknown:
		now the commentary mode is ff-enabled;
		now the walkthrough mode is ff-enabled;
	now silencer is true;
	if progress > 1 or choice > 0:
		now RW-restarting is true;
		now reversed-flow is true;
		try undoing;
	repeat with I running from 1 to 9:
		let A be entry 1 of allowed-actions;
		try A;
	now silencer is false;
	now dream-reached is false;
	now reversed-flow is false;
	now RW-restarting is false;
	clear the screen;
	say "[paragraph break][paragraph break][the story description][paragraph break]";
	now help mode is ff-unknown;
	say the banner text;
	try looking;

Chapter - Quitting the game

[ A "Are you sure you want to quit? " ]
The quit the game rule response (A) is "Are you resolved to depart from this adventure? "

Chapter - Saving the game

[ A "Save failed." ]
[ B "Ok." ]
The save the game rule response (A) is "The God of Chaos prevented the preservation of your adventure."
The save the game rule response (B) is "You have frozen your heroic tale in time."

Chapter - Restoring the game

[ A "Restore failed." ]
[ B "Ok." ]
The restore the game rule response (A) is "The God of Chaos prevented the restoration of your adventure."
The restore the game rule response (B) is "You have thawed your heroic tale."

Chapter - Restarting the game

[ A "Are you sure you want to restart? " ]
[ B "Failed." ]
The restart the game rule response (A) is "Is the thirst for a fresh beginning coursing through your veins? ".
The restart the game rule response (B) is "The God of Chaos prevented the restart of your journey."

Chapter - Restarting the game

[ A "Are you sure you want to restart? " ]
[ B "Failed." ]
The restart the game rule response (A) is "Is the thirst for a fresh beginning coursing through your veins? ".
The restart the game rule response (B) is "The God of Chaos prevented the restart of your journey."

Chapter - Verifying the story file

[ A "The game file has verified as intact." ]
[ B "The game file did not verify as intact, and may be corrupt." ]
The verify the story file rule response (A) is "The path is clear, onwards with the journey!"
The verify the story file rule response (B) is "The threads of your journey are frayed and broken."

Chapter - Switching the story transcript on

[ A "Transcripting is already on." ]
[ B "Start of a transcript of:" ]
[ C "Attempt to begin transcript failed." ]
The switch the story transcript on rule response (A) is "The spirits of the realm are already chronicling your tale of high adventure."
The switch the story transcript on rule response (B) is "The spirits of the realm start chronicling your tale of:".
The switch the story transcript on rule response (C) is "The spirits of the realm are unable to chronicle your tale of high adventure."

Chapter - Switching the story transcript off

[ A "Transcripting is already off." ]
[ B "[line break]End of transcript." ]
[ C "Attempt to end transcript failed." ]
The switch the story transcript off rule response (A) is "The spirits of the realm have already concealed your tale of high adventure."
The switch the story transcript off rule response (B) is "The spirits of the realm weave a shroud of secrecy around your tale."
The switch the story transcript off rule response (C) is "The spirits of the realm refuse to conceal your tale of high adventure."

Chapter - Requesting the story file version

[ version ]

Volume - World Model

Book - General Action Responses

To say seize-solid:
	say "[one of]Trying to seize [the noun]? Focus on more worthy treasures[or][The noun] [are] unyielding[or]Seizing [the noun] won't earn you any fame[or]Seizing [the noun] won't yield you any glory[or]Seizing [the noun] won't bring you any closer to victory[cycling]. ";

To say loot-solid:
	say "[one of][The noun] [bear] no spoils, but the world is vast and full of riches[or]Looting [the noun] is a futile effort[or]There's nothing to gain from looting [the noun][or]Looting [the noun] won't bring you the spoils you seek[or][The noun] [hold] no hidden treasures for you[cycling]. ";

To say smite-focus:
	say "[one of]Save your strength for battles that truly matter[or]Seek your foes or challenges elsewhere[or]Save your strength for battles that truly test your might[or]Save your strength for battles demanding your full force[or]Save your strength for battles that truly demand your prowess[cycling]. ";

To say smite-solid:
	say "[one of]Smiting [the noun] won't gain you any ground[or][The noun] [remain] impervious to your smites[or][The noun] won't yield to your strikes[or][The noun] [are] not your adversary[cycling]. [smite-focus]";

To say seize-heavy:
	say "There is no need to carry [the noun] around with you. [They] [are] large and heavy, and it's best to leave [them] where [they] [are]. ";

To say smite-furniture:
	say "[one of]Leave the furniture alone and prepare for real battles[or]Furniture is not the foe you seek to conquer[or]The furniture won't yield to your blows[or]Furniture is not worthy of your warrior's skill[or]Fighting furniture won't grant you glory[or]Furniture is not the battlefield where legends are made[cycling]. ";

To say seize-gaseous:
	say "[one of]You reach out your hand, fingers stretching towards [the noun], but your grasp meets only empty air[or]You extend your hand, attempting to seize [the noun], but your fingers grasp nothing but emptiness[or]Your hand reaches out for [the noun], but it evades your touch, as if mocking your attempt[or]You make a futile effort to seize [the noun], your hand closing on thin air[or]You stretch your hand towards [the noun], but it eludes your grasp, intangible and insubstantial, slipping through your fingers like smoke[cycling]. ";

To say loot-gaseous:
	say "[one of]You reach out with a sense of curiosity, attempting to uncover the secrets within [the noun]. However, [the noun] defies your attempts to grasp or manipulate it[or]With a hopeful anticipation, you try to loot from [the noun], but your efforts prove futile as it remains incorporeal and beyond your reach[or]You extend your hand, attempting to seize something tangible from [the noun], but it remains elusive, denying your attempts to gather anything from it[or]You cautiously attempt to loot the contents of [the noun], only to be met with a sense of emptiness and intangibility thwarting your efforts[or]As you attempt to loot from [the noun], it becomes evident that its nature defies conventional interaction, leaving you empty-handed and puzzled[cycling]. ";

To say smite-gaseous:
	say "[one of]You swing [your-weapon] at [the noun] with all your might, but your strike yields no effect[or]As you bring down [your-weapon] upon [the noun], a strange sensation runs through you as your strike passes right through it[or]Your battle instincts drive you to attempt a forceful blow against [the noun], yet [your-weapon] meets no resistance as it sails through the empty space[or]With a determined stance, you strike out at [the noun], only to witness [your-weapon] making contact with nothingness[or]As you launch an attack at [the noun], [your-weapon] cleaves the air, but [the noun] remains unaffected by your efforts[cycling]. [smite-focus]";

To say loot-nothing:
	say "[one of]Attempting to loot the contents of [the noun] reveals nothing of value[or]Your thorough examination of [the noun] yields no hidden treasures or surprises[or][The noun] reveals no hidden caches or valuable items[or]Your search for loot within [the noun] comes up empty-handed[or]You carefully inspect [the noun], but find no lootable contents[cycling]. ";

To say smite-nothing:
	say "[one of]Your attempt to smite [the noun] proves futile, as [the noun] is an inanimate object and holds no threat to you[or]You deliver a blow at [the noun] with all your might, but the blow only serves to showcase your strength rather than causing any harm[or]The impact of your blow against [the noun] sends a shock through your arms, but [the noun] remains undamaged[or]The strike aimed at [the noun] doesn't find its mark, leaving [the noun] untouched and undamaged[or]The force of your blow against [the noun] is absorbed without causing any damage[cycling]. ";

To say smite-broken:
	say "[one of]As you bring your blow down upon [the noun], which is already broken, a sense of futility washes over you[or]Your attempt to smite [the noun] is in vain, as it's already broken[or]Your blow connects with [the noun], but the impact lacks force, for it's already broken[or]With a futile strike, your blow meets [the noun], which has already been broken[or]Your attempt to smite [the noun] only reveals its broken state[cycling]. ";	

To say seize-far:
	say "[one of]Your strength is mighty, but even it cannot bridge the distance to [the noun][or]Though your prowess is unmatched, [the noun] [lie] beyond the grasp of even your powerful arms[or]No mortal can lay claim to [regarding the noun][possessive] distant presence. Your efforts are in vain[or]Your ambition to seize [the noun] is noble, yet [their] distance remains an insurmountable obstacle[or]Though your heart burns with determination, [the noun] [remain] distant and untouched by your grasp[cycling]. ";

To say loot-far:
	say "[one of]You reach towards [the noun], but [their] distance prevents you from reaping [their] rewards[or][The noun] in the distance [tempt] you, but [they] [remain] a prize too far to be claimed[or]Your longing to loot [the noun] is admirable, yet [their] distant presence denies you the spoils[or]Your desire to enrich yourself with [the noun] is strong, but [their] distance keeps [them] safe from your reach[or]A yearning to loot [the noun] stirs within you, but [they] [remain] a distant dream beyond your reach[cycling]. ";

To say smite-far:
	say "[one of]Your desire to smite [the noun] is commendable, but even your mighty arms cannot reach that far[or]You squint at [the noun], but [they] [are] too distant for your strike to find its mark[or]The thought of smiting [the noun] sparks your warrior's heart, yet the distance mocks your intentions[or]You envision a powerful smite upon [the noun], yet the reality of [their] remoteness stays your hand[or]The echoes of your smite ripple through the air, but [the noun] [stand] distant and unscathed[cycling]. ";

To say loot-animal:
	say "Save your plundering for more substantial foes than [the noun]. There's no honor in preying on the innocent. ";

To say smite-animal:
	say "There is no glory in striking down [the noun]. Our battles should be waged against true adversaries, not defenseless creatures. ";

Book - World Map

r-bedroom is a room.
r-diningroom is a room.
r-throneroom is a room.
r-blackhole is a room.

palace-region is a region.
The r-bedroom, r-diningroom, r-throneroom, and r-blackhole are in the palace-region.

r-northbank is a room.
r-darkriver is a room.
r-southbank is a room.

river-region is a region.
The r-northbank and r-southbank are in the river-region.

r-order-1 is a room.
r-order-2 is a room.
r-order-3 is a room.

order-region is a region.
The r-order-1, r-order-2, r-order-3 are in the order-region.

r-chaos-1     is a room.
r-chaos-2ns   is a room.
r-chaos-2ew   is a room.
r-chaos-2senw is a room.
r-chaos-3ns   is a room.
r-chaos-3ew   is a room.
r-chaos-3senw is a room.
r-chaos-3     is a room.

chaos-region is a region.
The r-chaos-1, r-chaos-2ns, r-chaos-2ew, r-chaos-2senw, r-chaos-3ns, r-chaos-3ew, r-chaos-3senw, r-chaos-3 are in the chaos-region.

r-guards is a room.
r-pit is a room.
r-corridor is a room.
r-crypt is a room.
r-hall is a room.
crypt-region is a region.
The r-guards, r-corridor, r-crypt, r-hall are in the crypt-region.

r-cavern-1 is a room.
r-cavern-2 is a room.
r-cavern-3 is a room.
r-cavern-4 is a room.
r-cavern-5 is a room.
cavern-region is a region.
The r-cavern-1, r-cavern-2, r-cavern-3, r-cavern-4, r-cavern-5 are in the cavern-region.

[ We need this rule to transfer things between this room and the player's location ]
Rule for reaching inside r-blackhole: allow access;

Book - Actors

To say present-npc for (p - alcaz): say present-alcaz.
To say present-npc for (p - lydia): say present-lydia.
To say present-npc for (p - ferryman): say present-ferryman.
To say present-npc for (p - idol): say present-idol.
To say present-npc for (p - sage): say present-sage.
To say present-npc for (p - statue): say present-statue.
To say present-npc for (p - nb-ferryman): say "You cannot present something to the ferryman from here, you will need to march south first. ".
To say present-npc for (p - sb-ferryman): say "You cannot present something to the ferryman from here, you will need to march north first. ".
To say present-npc for (p - person): say "Your generosity is ignored. ".

Chapter - Conan

To say my belongings:
	if the first thing held by the player is not nothing:
		say "You are carrying ";
		list the contents of the player, giving inventory information, as a sentence;

The denials of Conan are {
  [regard] "Your muscles ripple beneath your sun-tanned skin. You are definitely as good-looking as ever[if the first thing held by the player is not nothing].[line break][my belongings][end if].",
  [seize] "You want to keep your hands free for wielding weapons and conquering foes, rather than wrestling with your own limbs.",
  [loot] "You prefer to keep your belongings where they are.",
  [present] "[present-npc for second noun]",
  [treat] "[one of]Introspection has its merits, but let's keep moving[or]There is a time for inner dialogue and a time for action[or]Save your wisdom for more pressing matters[cycling].",
  [smite] "[one of]Let's face our enemies with courage, not self-inflicted harm[or]Focus on the enemy, not on your own demise[or]Keep [your-weapon] aimed at your foes, not your own flesh[cycling]."}.
Understand "conan", "barbarian", "king/monarch", "muscular/muscle/muscles", and "man" as Conan.

Chapter - Alcaz

[ Room activities for Alcaz. ]

To say alcaz-room:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action and the recipient is Alcaz:
		say "is holding out his hand to you, expecting you to present something to him";
	else if the location is r-throneroom and A is the marching action:
		say "is ready to step through the portal he just created";
	else if the location is r-throneroom:
		say "is studying the broad dark stain on the floor";
	else if the location is r-northbank:
		say "is studying the swirling mist above the dark river";
	else if the location is r-southbank:
		say "is looking at the gate depicting the eternal struggle between the Gods of Order and Chaos";
	else if the location is in order-region:
		say "is studying the runic script on the marker";
	else if the location is in chaos-region:
		say "is studying the marker, looking for any indication of guidance";
	else if the location is r-guards:
		say "is studying the runes on the stone tablet";
	else if the location is r-hall:
		say "stands before you[if A is the smiting action] enveloped in a sphere of mystical defense[end if], a haunting vision of what he once was. His eyes are now cold and vacant, bearing no trace of recognition";
	else if the location is r-cavern-5 and A is the treating action:
		say "is studying the shielded skeletal dragon with a puzzled look on his face";
	else if the location is r-cavern-5 and A is the marching action:
		say "is urging you to move swiftly, lest the portal closes on you";
	else if the location is r-cavern-5:
		say "is studying the weakened skeletal dragon";
	else if the location is r-crypt:
		say "is bowing in deference to the sage";
	else:
		say "is studying the environment";

To say alcaz-to-mist:
	say "Alcaz becomes gradually less distinct as the mist thickens around him. His hands lift in a graceful gesture, fingers tracing unseen patterns in the air. With each step, Alcaz appears to dissolve into the mist, his form gradually dissipating into nothingness. ";

To say alcaz-from-mist:
	say "From within the mist, a figure materializes, gradually becoming more defined against the shifting backdrop. Alcaz emerges, his form taking shape as if coalescing from the very essence of the mist. As he fully steps out of the mist, his form solidifies, becoming as tangible as before. ";

To say seize-alcaz:
	if the location is r-hall:
		say "Alcaz is out of your reach; as you attempt to approach him, he snaps his fingers, teleporting away. ";
	else:
		say "You extend your hand with the intention of seizing Alcaz, but he steps back, a surprised expression crossing his face. 'Hold,' he says, raising his hand in a placating gesture. 'There's no need for such aggression. We are allies in this battle.' ";

To say loot-alcaz:
	if the location is r-hall:
		say "Alcaz is out of your reach; as you attempt to approach him, he snaps his fingers, teleporting away and taunting you, 'once a thief, always a thief!' ";
	else:
		say "As you try to reach for Alcaz's belongings, he is taken aback by your actions. 'Hold on,' he says, 'I believe there might be a misunderstanding here. We are working together, are we not?' ";

To say present-alcaz:
	if the location is r-hall:
		say "Alcaz is on guard and ignores your attempts to bribe him. ";
	else if the noun is Conan:
		say "You present youself to Alcaz. 'While I appreciate your openness, I believe there are matters of greater importance at hand,' he says. 'Let us focus on the task before us.' ";
	else if the noun is a weapon:
		say "You present [the noun] to Alcaz. 'Ah, the battle-hardened instrument of a warrior,' he says. 'Keep it close, for it is a part of you.' ";
	else:
		say "You present [the noun] to Alcaz. His gaze shifts from your face to [the noun]. 'I don't think [the noun] holds any magical secrets,' he says. ";

To say treat-alcaz:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action and the recipient is Alcaz:
		say "'I believe you have something to share with me,' Alcaz says, holding out his hand expectantly. ";
	else if the location is r-hall:
		say "Alcaz is on guard and ignores your attempts to further distract him. ";
	else if the location is r-cavern-5 and the dragon is in the location:
		say "'I destroyed his shield, he's all yours now,' Alcaz says. ";
	else if the location is r-crypt:
		say "'My lord,' Alcaz says with a grin, 'we are in the esteemed presence of Salcor, the head of our order.' ";
	else if A is the marching action and the location is r-throneroom:
		say "'My lord,' Alcaz says, 'I think it is time to move on. I cannot maintain the portal forever.' ";
	else if the location is r-northbank and Conan does not hold the ring:
		say "'My lord,' Alcaz says, 'We need to arrange payment for the ferryman.' His eyes briefly flicker to the sturdy-looking chest. ";
	else if the location is r-northbank and Conan does not hold the goat:
		say "'My lord,' Alcaz says, 'We need a sacrifice to appease the gods.' His eyes briefly flicker to the goats in the meadow. ";
	else if A is the marching action and the location is not r-crypt:
		say "'My lord,' Alcaz says, 'I think it is time to move on.' ";
	else if the location is r-guards and the tablet is unregarded:
		say "'My lord,' Alcaz says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";
	else if the location is r-guards:
		say "'My lord,' Alcaz says, 'we should treat with [guard-name] to learn which archway we should enter.' ";
	else if the location is r-southbank and A is the looting action:
		say "'My lord,' Alcaz says, 'I think it is time to move on.' ";
	else if the location is in order-region:
		say "'My lord,' Alcaz says, 'we should study the runic script on the marker.' ";
	else if the location is r-cavern-5 and A is the marching action:
		say "'My Lord,' Alcaz says, 'I think it is time to move on.' ";
	else:
		say "[one of]Alcaz appears to be lost in thought and does not respond[or]Alcaz is deep in thought, probably seeking insights from the past to aid us in the present[or]Alcaz is busy analyzing the environment and does not answer[or]Alcaz appears to be immersed in arcane calculations and does not respond[or]Alcaz appears to be focused on something not immediately apparent to you[cycling]. ";

To say smite-alcaz:
	if the location is r-hall:
		say "Alcaz is out of your reach; as you attempt to approach him, he snaps his fingers, teleporting away. ";
	else:
		say "With a sudden surge of determination, you ready [your-weapon] and raise it in the direction of Alcaz. However, his eyes widen in alarm, and he raises his hands, his magic enveloping him with a shimmering energy. 'Wait! Hold on!' he exclaims, 'we're allies, remember? Let's focus on the true threat at hand.' ";

The initial appearance of Alcaz is "Alcaz [alcaz-room].".
The denials of Alcaz are {
  [regard] "Alcaz [alcaz-room].",
  [seize] "[seize-alcaz]",
  [loot] "[loot-alcaz]",
  [present] "",
  [treat] "[treat-alcaz]",
  [smite] "[smite-alcaz]"}.
Understand "Alcaz", "mage", "thin", and "wimp" as Alcaz.

Alcaz wears a robe.
The robe is clothing.
The owner of the robe is Alcaz.
The denials of the robe are {
  [regard] "Alcaz's robe is adorned with intricate embroidery and arcane symbols.",
  [seize] "[loot-alcaz]",
  [loot] "[loot-alcaz]",
  [present] "",
  [treat] "",
  [smite] "[loot-alcaz]"}.
Understand "Alcaz's/-- swirling/-- robe of/-- Alcaz/--" and "intricate/-- embroidery" and "arcane/-- symbol/symbols" as the robe.

Alcaz carries a staff.
The staff is a weapon.
The denials of the staff are {
  [regard] "Alcaz's staff, adorned with a polished crystal orb, exudes an air of ancient wisdom.",
  [seize] "[loot-alcaz]",
  [loot] "[loot-alcaz]",
  [present] "",
  [treat] "",
  [smite] "[loot-alcaz]"}.
Understand "Alcaz's/-- staff of/-- Alcaz/--" and "polished/-- crystal/-- orb" as the staff.

Chapter - Lydia

[ Room activities for Lydia. ]

To say lydia-room:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action and the recipient is Lydia:
		say "is holding out her hand to you, expecting you to present something to her";
	else if a monster (called M) is in the location:
		if the location is r-cavern-3:
			say "looks defiantly at [the M], her hands glowing with the power of her deity";
		else:
			say "looks horrified at [the M]";
	else if the location is r-throneroom:
		say "looks horrified at the broad dark stain on the floor";
	else if the location is r-northbank:
		say "is gazing intently at the mist above the dark river";
	else if the location is r-darkriver:
		say "is watching the dark river with wary eyes";
	else if the location is r-southbank:
		say "is looking at the gate depicting the eternal struggle between the Gods of Order and Chaos";
	else if the location is in order-region:
		say "is trying hard not to look at the runic script on the marker";
	else if the location is in chaos-region:
		say "is trying hard not to look at the growing darkness around the island";
	else if the location is r-guards:
		say "is studying the runes on the stone tablet";
	else if the location is r-hall and Alcaz is in the location:
		if A is the treating action:
			say "is focused on Alcaz, watching his every move";
		else: [ smiting action ]
			say "is enveloped in a sphere of mystical defense and focused on Alcaz, watching his every move";
	else if the location is r-hall and A is the marching action:
		say "is urging you to move swiftly, lest the portal closes on you";
	else if the location is r-hall:
		say "is enraptured by the sacred reliquary";
	else if the location is r-cavern-5 and A is the marching action:
		say "is urging you to move swiftly, lest the portal closes on you";
	else if the location is in cavern-region:
		say "is trying to attune her spiritual awareness";
	else if the location is r-crypt:
		say "is gazing in admiration at the sage";
	else if the location is r-bedroom:
		say "is looking at you";
	else:
		say "is studying the environment";

To say lydia-complains:
	say "[one of]From within the altar, Lydia's muffled voice reaches your ears as she calls out to you. 'My lord, this is not the time for heroic exploits! Get me out of here before whatever is out there decides to dine on a priestess instead!' [or]Lydia's voice rises again from within the altar, her tone more urgent. 'My lord, please focus on our escape! I sense evil closing in, and I'd rather not become its main course!' [or]Lydia's voice echoes from the altar, a touch of annoyance in her words. 'My lord, can't you save the evil bashing for later? A timely rescue would be very much appreciated!' [cycling]";

To say seize-lydia:
	if the player has the noun:
		say "You already carry Lydia. ";
	else:
		say "As you reach out to seize Lydia, her eyes widen in surprise. 'Hold on there,' she says, her voice tinged with uncertainty. 'We're allies in this endeavor, let's keep our focus on the task ahead.' ";

To say loot-lydia:
	say "As you reach out with the intention of looting from Lydia, her eyes widen in surprise. 'Hold on there,' she says, 'what are you trying to do? We're allies, remember?' ";

To say present-lydia:
	let A be the action name part of entry 1 in the allowed-actions;
	if Alcaz is in the location and the location is r-hall:
		if A is the presenting it to action:
			say "You present [the noun] to Lydia. 'My lord,' she says, slightly irritated, 'I asked for that axe handle you keep lugging around.' ";
		else if A is the treating action:
			say "'My lord, please distract him,' Lydia says, and she focuses on Alcaz, probing his defenses. ";
		else:
			say "'My lord,' Lydia says, 'I already have what I need. Now, show your battle prowess!' ";
		return;
	if the noun is Conan:
		say "You present yourself to Lydia. 'Brave warrior,' she says softly, 'your spirit is as noble as your strength. Yet it is our actions that shall speak the loudest.' ";
	else if the noun is a weapon:
		say "You present [the noun] to Lydia. 'A warrior's companion,' she says. 'It's more than just a tool; it's an extension of your strength and determination. May it serve you well in the trials lying ahead.' ";
	else if the noun is the vial:
		if the vial is open:
			say "You present [the noun] to Lydia. Her gaze shifts from your face to [the noun]. 'It has served its purpose,' she says. ";
		else if the location is r-cavern-5 and A is the looting action:
			say "'My lord,' Lydia whispers, 'now is the time to make use of that vial!' ";
		else:
			say "You present [the noun] to Lydia. Her gaze shifts from your face to [the noun]. 'I think you'd better hold on to it. You will know when the time is right to use it,' she says. ";
	else if the noun is the necklace:
		say "Lydia acts surprised, and politely refuses your offer. 'Don't you need this to pay the ferryman?' ";
	else if the noun is Lydia:
		say "'My lord,' Lydia says, 'Did you drink too much wine last night?' ";
	else:
		say "You present [the noun] to Lydia. Her gaze shifts from your face to [the noun]. 'I don't think [the noun] holds any religious significance,' she says. ";

To say treat-lydia:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action and the recipient is Lydia:
		say "'I believe you have something to share with me,' Lydia says, holding out her hand expectantly. ";
	else if a monster (called M) is in the location and M is not the dragon:
		say "Lydia looks horrified at [the M] and does not respond. ";
	else if the location is r-throneroom and A is the marching action:
		say "'My Lord,' Lydia says, 'I think it is time to move on.' ";
	else if the location is r-throneroom and A is the seizing action:
		say "'My Lord,' Lydia says, 'I think you should re-arm yourself.' ";
	else if the player is carrying Lydia:
		say "Lydia is busy trying to get comfortable and does not respond. ";
	else if the location is r-northbank and the northdoor is closed:
		say "'My Lord,' Lydia says, 'We need to call upon the ferryman.' ";
	else if the location is r-northbank and Conan does not hold Lydia:
		say "'My Lord,' Lydia says, 'We need a sacrifice to appease the gods.' ";
	else if the location is r-southbank:
		say "'My Lord,' Lydia says, 'Let us get out of here. I do not like this place.' ";
	else if the location is in order-region:
		say "'My lord,' Lydia says, 'please follow your instincts and get us out of here. I do not like these markers calling out to us.' ";
	else if the location is in chaos-region:
		say "'My lord,' Lydia says, 'please follow your instincts and get us out of here. I do not like these paths failing on us.' ";
	else if the location is r-guards:
		if A is the treating action:
			say "'My lord,' Lydia says, 'approach the Goddess and fulfill her request.' ";
		else if A is the presenting it to action:
			say "'My lord,' Lydia says, 'present your greatest weapon to her.' ";
		else if A is the regarding action:
			say "'My lord,' Lydia says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";	
		else:
			say "'My lord,' Lydia says, 'let us tread the path of Justice to the east.' ";
	else if the location is r-hall:
		if Alcaz is in the location:
			if A is the treating action:
				say "'My lord, please distract him,' Lydia says, and she focuses on Alcaz, probing his defenses. ";
			else:
				say "'My lord,' Lydia says, 'I already have what I need. Now, show your battle prowess!' ";
		else if A is the marching action:
			say "'My lord,' Lydia says, 'we need to move swiftly, lest the portal closes on us.' ";
		else:
			say "'My lord,' Lydia says, 'I feel something important is held within the reliquary, it might aid us in our battle against the necromancer.' ";
	else if the location is r-cavern-1:
		say "'My lord,' Lydia whispers, 'the necromancer is circling us. I sensed his spirit traveling from the southwest to the north.' ";
	else if the location is r-cavern-2:
		say "'My lord,' Lydia whispers, 'the necromancer is circling us. I sensed his spirit traveling from the east to the southwest.' ";
	else if the location is r-cavern-3:
		say "'My lord,' Lydia whispers, 'the necromancer is circling us. I sensed his spirit traveling from the northwest to the east.' ";
	else if the location is r-cavern-4:
		say "'My lord,' Lydia whispers, 'the necromancer has stopped circling us. He is now due north of us.' ";
	else if the location is r-cavern-5 and A is the looting action:
		say "'My lord,' Lydia whispers, 'now is the time to make use of that vial!' ";
	else if the location is r-cavern-5 and A is the smiting action:
		say "'My lord,' Lydia whispers, 'make use of your divine power!' ";
	else if the location is r-cavern-5 and A is the treating action:
		say "'My lord,' Lydia whispers, 'talk to Alcaz, he may be able to help us.' ";
	else if the location is r-cavern-5 and A is the marching action:
		say "'My Lord,' Lydia says, 'I think it is time to move on.' ";
	else if the location is r-crypt:
		say "'My lord,' Lydia says with a serene smile, 'we are in the presence of a vessel of Mitora, a wise and noble being.' ";
	else:
		say "[one of]Lydia is engaged in deep meditation and does not respond[or]Lydia is performing a religious ritual and does not respond[or]Lydia has entered a trance-like state and does not respond[or]Lydia is reciting sacred chants and hymns and does not respond[or]Lydia is engaged in a prayer session and does not respond[cycling]. ";

To say smite-lydia:
	say "With a sudden surge of determination, you ready [your-weapon] and raise it in the direction of Lydia. 'Wait, what are you doing?' she implores, 'we're allies, remember? There's enough darkness out there without us turning against each other.' ";

To say seize-necklace:
	if the player has the noun:
		say entry 2 of the generic-denials;
	else:
		say "[loot-lydia]";

The initial appearance of Lydia is "Lydia [lydia-room].".
The denials of Lydia are {
  [regard] "Lydia [lydia-room].",
  [seize] "[seize-lydia]",
  [loot] "[loot-lydia]",
  [present] "[present-npc for second noun]",
  [treat] "[treat-lydia]",
  [smite] "[smite-lydia]"}.
Understand "Lydia", "priestess", "woman" as Lydia.

Lydia wears a dress.
The dress is clothing.
The owner of the dress is Lydia.
The printed name of the dress is "flowing dress".
The denials of the dress are {
  [regard] "Lydia's dress is a flowing garment, adorned with delicate patterns.",
  [seize] "[loot-lydia]",
  [loot] "[loot-lydia]",
  [present] "",
  [treat] "",
  [smite] "[loot-lydia]"}.
Understand "Lydia's/-- flowing/-- dress/garment of/-- Lydia/--" and "delicate/-- pattern/patterns" as the dress.

Lydia wears a necklace.
The necklace is clothing.
The owner of the necklace is Lydia.
The printed name of the necklace is "gold necklace of Mitora".
The denials of the necklace are {
  [regard] "Lydia's gold necklace features an intricately designed phoenix pendant, reminding you of the phoenix engraved on the sage's tomb.",
  [seize] "[seize-necklace]",
  [loot] "[loot-solid]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "Lydia's/-- gold/-- necklace of/-- Mitora/Lydia/--" and "intricate/intricately/-- design/designed/-- phoenix/-- pendant" and "gold" as the necklace when the holder of the necklace is not the ferryman.

Chapter - Ferryman

To say ferry-demand:
	say "The ferryman's unseen gaze fixes upon you. His hand, gnarled and weathered, emerges from the folds of his robe, fingers outstretched. His demand is unspoken, but his gesture is clear: payment for passage across the boundary. ";

To say ferryman-room:
	if the northdoor is open:
		say "stands at the edge of the boat, his form obscured by his dark robe. Only his gnarled, weathered hands are visible, one of them extended towards you in a demand for payment";
	else if the southdoor is open:
		say "stands at the edge of the boat, his form obscured by his dark robe";
	else if the serpent is in the location:
		say "deftly maneuvers the boat, ensuring that it remains steady amidst the tumultuous waters";
	else:
		say "sits at the helm of the small boat, his black-robed form almost merging with the shadows. His hands, the only visible part of him, move with an eerie grace as they guide the boat through the river's depths. You sense a presence beneath the surface, something massive and ominous stirring in the depths";

To say present-ferryman:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		let P be a random thing carried by the ferryman;
		if P is nothing:
			say "The ferryman ignores [the noun]. You made your payment to the ferryman and then looted it back after reaching the south bank. You will not fool him like that again. ";
		else:
			say "The ferryman ignores [the noun]. You already gave [the P] to him as your payment, and secured passage across the river. ";
	else if the noun is Conan:
		say "The ferryman seems to pause for a moment, his unseen gaze focusing on you. His hand remains motionless. Then, his form seems to shift ever so slightly, as if he's shaking his head in disappointment. ";
	else if the noun is a person:
		say "As you present [the noun] to the ferryman, he retreats slightly and gazes downward, his form tense and almost submissive. He does not want to accept something that is not meant for him to take. ";
	else:
		say "The ferryman's hand pauses mid-motion, his fingers inches away from [the noun]. An air of confusion seems to emanate from beneath his hood as he hesitates. Then, with a slow, deliberate motion, his hand retreats, and his form seems to shift ever so slightly, as if he's shaking his head in disappointment. ";

To say loot-ferryman:
	if the southdoor is closed:
		say "You briefly consider looting the ferryman, but decide to wait until you have safely crossed the river";
	else if lydia is held by the player:
		say "You already reclaimed Lydia's necklace; there is nothing left to loot from him";
	else:
		say "As you cautiously reach towards the ferryman, an unseen energy seems to repel your attempt, a subtle force keeping your hand at bay";

The initial appearance of the ferryman is "The ferryman [ferryman-room].".
The denials of the ferryman are {
  [regard] "Clad in a black cloak, the ferryman himself remains concealed, his identity veiled in shadows[if the southdoor is open and the ferryman is holding the necklace]. Now might be a good time to retrieve Lydia's necklace[end if].",
  [seize] "As your hand reaches out, intent on seizing the ferryman, the air around him seems to shimmer, and you feel a sudden resistance, as if an invisible barrier guards him from your grasp.",
  [loot] "[loot-ferryman].",
  [present] "",
  [treat] "There is no response from the ferryman, no acknowledgment of your attempt to treat with him.",
  [smite] "As you raise your weapon, an aura briefly appears around the ferryman, and your attempt to smite him is met with an invisible barrier."}.
Understand "black/dark/-- cloak" and "ferryman" and "gnarled/-- and/-- weathered/-- hand/hands" as the ferryman.
Rule for deciding the concealed possessions of the ferryman: yes.

Chapter - Idol

To say present-idol:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "As you present [the noun], a strange sensation washes over you. The air around the altar grows heavy, as if some unseen force is pushing against your offering. ";
	else:
		say "As you present [the noun], the altar trembles, its surface crackling with an unsettling darkness. ";

To say treat-idol:
	say "As you attempt to treat with the idol, a strange feeling washes over you. The air seems to thicken, and a faint whispering sound echoes in your ears. However, there is no discernible response from the idol itself. ";

The denials of the idol are {
  [regard] "Above the altar, a towering idol looms, its form reflecting both the chaotic and ordered aspects of the deities it represents.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "[treat-idol]",
  [smite] "[smite-solid]"}.
Understand "towering/looming/-- idol" as the idol.

Chapter - Sage

To say sage-room:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action:
		say "is holding out his hand to you, expecting you to present something to him";
	else:
		say "fixes his piercing gaze on you";

To say present-sage:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the treating action:
		say "The sage politely refuses your offering. He expects you to speak with him instead. ";
	else if A is the undoing action:
		say "The sage politely refuses your offering. He reminds you that your course of action lies in the past, not in the present. ";
	else if A is not the presenting it to action:
		say "The sage politely refuses your offering. He reminds you that you should return to the palace and get some rest. ";
	else if Lydia is in the location:
		say "The sage politely refuses your offering. Only the finest wine is good enough for him. ";
	else if the noun is the bottle:
		say "The sage politely refuses your offering. An empty bottle is not the evidence he seeks. ";
	else if the noun is the loincloth:
		say "The sage politely refuses your offering. Your loincloth is not the evidence he seeks. ";
	else:
		say "The sage politely refuses your offering. He wants to study the weapon, not the man. ";

To say treat-sage:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is the presenting it to action and the recipient is the sage:
		say "'I believe you have something to share with me,' the sage says, holding out his hand expectantly. ";
		return;
	if A is the marching action:
		say "'I am no god,' the sage chuckles. 'Occasionally, a priest blunders into my crypt, and I find it amusing that religious leaders don't expect their gods to talk back to them, much less complain.'[paragraph break]'Now go! You should go back to the palace and get some rest!' the sage says. ";
		return;
	say "'I am no god,' the sage chuckles. 'Occasionally, a priest blunders into my crypt, and I find it amusing that religious leaders don't expect their gods to talk back to them, much less complain.'[paragraph break]'Now reverse the flow of time and fulfill your destiny!' the sage says. ";

The printed name of the sage is "white-bearded figure".
The denials of the sage are {
  [regard] "The sage [sage-room].",
  [seize] "As you reach out to seize the sage, he leans back and easily avoids your grasp. He chuckles softly. 'You act the fool,' he intones. 'Seek knowledge, not power.'",
  [loot] "As you extend your hand towards the sage, a sudden surge of energy radiates from his being. His voice carries a stern warning: 'Looting me is beyond you, Child of Destiny. Seek wisdom, not material gain.'",
  [present] "",
  [treat] "[treat-sage]",
  [smite] "As you raise [your-weapon], preparing to smite the sage, a surge of energy emanates from his presence, blocking your attack. 'Violence begets violence,' he admonishes sternly. 'Seek understanding, not bloodshed.'"}.
Understand "white-bearded/white/-- bearded/-- figure/man/sage" and "piercing/knowing/-- knowing/piercing/-- gaze" and "white/-- beard" as the sage.

Chapter - Statue

To say frozen-statue:
	say "The statue of the Goddess of Justice stands as still and unyielding as stone, unmoved by your attempts to engage with it. ";

To say regard-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	say "At the center of the room stands a serene statue of the Goddess of Justice[if A is the presenting it to action]. The features of the statue appear softened, almost as if she were alive[end if]. ";

To say statue-room:
	if Conan is presented:
		say ". The statue has been displaced, exposing a path to the east";
	else if the statue is treated:
		say "; her features softened, almost as if she were alive";
	else:
		say "";

To say present-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "[frozen-statue]";
	else if the noun is the sword:
		say "'A sword,' she intones, 'a powerful tool. But true greatness lies not in a tool.' ";
	else:
		say "'[A noun],' she intones, 'surely you have a greater weapon to offer.' ";

To say treat-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "[frozen-statue]";
	else:
		say "'To open the path of Justice,' it intones, 'you must present to me your greatest weapon, one embodying the true spirit of justice.' ";

To say touch-statue:
	say "As you touch the statue, a surge of energy emanates from it, causing you to recoil in pain.[paragraph break]'Foolish mortal!' the Goddess's voice thunders. 'Justice is not something taken by force, but sought through righteousness and virtue.' ";

To say seize-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "[seize-solid]";
	else:
		say "[touch-statue]";

To say loot-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "[loot-solid]";
	else:
		say "[touch-statue]";

To say smite-statue:
	let A be the action name part of entry 1 in the allowed-actions;
	if A is not the presenting it to action:
		say "[smite-solid]";
	else:
		say "[touch-statue]";

The printed name of the statue is "Goddess of Justice".
The denials of the statue are {
  [regard] "[regard-statue]",
  [seize] "[seize-statue]",
  [loot] "[loot-statue]",
  [present] "",
  [treat] "[treat-statue]",
  [smite] "[smite-statue]"}.
Understand "serene/-- statue of/-- the/-- Goddess/-- of/-- Justice/--" and "the/-- serene/-- Goddess of/-- Justice/-- statue/--" and "the/-- serene/-- Goddess/-- of/-- Justice statue/--" and "marble" as the statue.

Book - Monsters

Chapter - Demon

To say demon-description:
	if the smite-counter of the demon is 0: [ full health ]
		say "The demon's sinewy muscles ripple with power, each movement a testament to its formidable strength. From its back, jagged wings, tattered and torn, stretch out. At the ends of its elongated fingers, claws gleam like razors, while its bared teeth form a ghastly grin mirroring its insatiable hunger for chaos";
	else if the smite-counter of the demon is 1 and the choice is 1: [ sword once ]
		say "The imposing presence of the demon is now visibly diminished. Dark blood oozes from a deep gash on its demonic flesh. Despite the wound, the demon's red eyes still blaze with a ferocious hunger for battle";
	else if the smite-counter of the demon is 1 and the choice is 2: [ axe once ]
		say "The demon's monstrous features are contorted with a mixture of rage and pain. The demon's movements are noticeably slower, its once-confident aura diminished";
	else if the smite-counter of the demon is 2 and the choice is 1: [ sword twice ]
		say "The demon is visibly battered and weakened. Its hide is marred with deep gashes and oozing wounds, the stench of its blood mingling with the scent of burnt flesh";

The initial appearance of the demon is "[demon-description].".
The printed name of the demon is "demon".
Understand "loathsome", "abomination", "monster", "horror", "creature", "thing", "claw/claws", "clawed hand/hands", "leathery/-- wing/wings/membranes", "burning/-- eye/eyes" and "demon" as the demon.

To say stain-description:
	say "A broad dark stain lies on the floor where the demon died. The demon has left its outline clearly etched in its blood, an outline belonging to no being in a sane and normal world";

The initial appearance of the stain is "[stain-description].".
The printed name of the stain is "stain".
The denials of the stain are {
  [regard] "A broad dark stain lies on the floor where the demon died. The demon has left its outline clearly etched in its blood, an outline belonging to no being in a sane and normal world.",
  [seize] "Your fingers reach out to touch the shape that has been etched onto the palace floor. Your hand hovers just above the surface, but you hesitate making contact. You can almost sense the demon's energy still lingering.",
  [loot] "You extend your hand towards the bloodstained outline of the demon. However, there are no physical contents to be extracted from the bloodstained design.",
  [present] "",
  [treat] "You feel a subtle shift in the air around you. However, there is no clear response, no presence reaching out to meet your efforts.",
  [smite] "A surge of primal anger courses through your veins. With a swift and fierce motion, you strike down, aiming for the stain. However, as your blow meets the floor, it becomes apparent that there is nothing to strike."}.
Understand "broad/-- dark/-- stain", "etched/-- bloody/-- insane/-- outline", "blood" as the stain.

Chapter - Serpent

To say serpent-description:
	if the smite-counter of the serpent is 0: [ full health ]
		say "A colossal serpent with a body as thick as a tree trunk, coils and twists upon itself. Its gaping maw, lined with rows of razor-sharp teeth, opens wide in a silent hiss";
	else if the smite-counter of the serpent is 1: [ axe once ]
		say "The immense body of the serpent is writhing with a mixture of fury and pain. Some of its scales have cracked under the force of your attack, revealing raw, wounded flesh beneath";
	else if the smite-counter of the serpent is 2: [ axe twice ]
		say "The serpent's movements grow sluggish as pain courses through its massive body, covered with jagged wounds oozing dark blood into the water";

The initial appearance of the serpent is "[serpent-description].".
The printed name of the serpent is "serpent".
Understand "colossal/-- grave/-- tide/-- serpent" and "coiling/-- writhing/-- serpentine body" and "gleaming/-- eyes" and "gaping/-- maw" and "rows/-- of/-- razor/razor-sharp/-- sharp/-- teeth" and "darkness" and "head" and "serpent's/-- scale/scales" and "massive/-- form" and "snake" as the serpent.

Chapter - Corpse

The initial appearance of the corpse is "[if lydia is in altar]Three corpses surround you, arms outstretched and fingers curled into gnarled claws. Tattered clothing hangs loosely from their frames, and their steps are uneven, but they are determined to reach you[else]The corpse's flesh is mottled, patches of rot and decay evident on its body. Tattered clothing hangs loosely from its frame, and its limbs move with an unnatural stiffness[end if]."

To say corpse-dies:
	say "The corpse's body collapses under the force of your blow. In an instant, its form bursts into a cloud of noxious particles, carried away by an ethereal wind, which seems to erase the remnants of its cursed existence";

To say smite-corpse:
	say "[one of]You bring [your-weapon] down upon the nearest corpse, the blade sinking into its decaying flesh. The force of your blow cleaves through its brittle bones.[paragraph break][corpse-dies].[paragraph break]The earth stirs, and another corpse emerges from the ground[or]Summoning your barbaric strength, you swing [your-weapon] at one of the corpses. The blade meets its target with a sickening crunch, severing limbs.[paragraph break][corpse-dies].[paragraph break]The ground shifts, and another corpse emerges, drawn forth by some unholy force[or]You bring [your-weapon] down upon one of the corpses. The blade cuts through its decaying flesh, ending its existence with a single blow.[paragraph break][corpse-dies].[paragraph break]The ground rumbles. From the earth, another corpse rises to challenge you[cycling].[paragraph break][lydia-complains]";

The printed name of the corpse is "corpse".
The denials of the corpse are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] "[smite-corpse]"}.
Understand "grotesque/decaying/mottled/-- and/-- disfigured/-- wandering/-- form/figure/creature/corpse" as the corpse.

Chapter - Corpses

The printed name of the corpses is "corpses".
The denials of the corpses are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] ""}.
Understand "grotesque/decaying/mottled/-- and/-- disfigured/-- wandering/-- forms/figures/creatures/corpses" as the corpses.

Chapter - Skeleton

To say smite-skeleton:
	say "[one of]With a swift and precise strike, your sword slices through the skeleton's brittle bones, reducing it to a clattering heap on the ground[or]The clash of steel against bone echoes through the cavern as you deftly cut through the skeleton, sending its limbs scattering in all directions[or]Your sword strikes true, shattering the skeleton into a cloud of bone fragments that rain down around you[or]With a powerful overhead swing, you cleave the skeleton in two, leaving it in two pieces that collapse into a heap[cycling]";

To say smite-skeletons:
	say "[one of]With a single mighty swing of your sword, you cleave through several skeletons at once, shattering their brittle bones into a shower of fragments[or]Your sword arcs through the air, striking true and annihilating a group of skeletons in a single blow, reducing them to a pile of bone shards[or]The power behind your strike is undeniable as your sword cuts a deadly path through the ranks of several skeletons, leaving only scattered bone remnants in its wake[or]As you swing your sword in a wide arc, it connects with a group of skeletons, obliterating them instantly and sending their remains scattering[or]With incredible precision and strength, your sword slices through the skeletal horde, causing multiple skeletons to crumble into pieces[cycling]";

The initial appearance of the skeleton is "Several skeletons encircle you and Lydia, forming a grim assembly of bony figures. Their empty eye sockets seem to glow with malevolence as they inch closer, their skeletal hands reaching out for you."

The printed name of the skeleton is "skeleton".
The denials of the skeleton are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] "[smite-skeleton]. Another skeleton quickly takes its place. Lydia is trying to catch your attention."}.
Understand "skeleton" as the skeleton.

Chapter - Skeletons

The printed name of the skeletons is "skeletons".
The denials of the skeletons are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] "[smite-skeletons]. Another group of skeletons quickly take their place. Lydia is trying to catch your attention."}.
Understand "skeletons" as the skeletons.

Chapter - Dragon

To say dragon-description:
	if the smite-counter of the dragon is 0: [ full health ]
		say "The skeletal dragon is a nightmarish monstrosity. Composed entirely of eerie bone fragments, it stands tall and imposing. This skeletal behemoth radiates an overwhelming aura of dark magic and power";
	else if the smite-counter of the dragon is 1: [ sword once ]
		say "Some of the skeletal components of the skeletal dragon show signs of damage and wear, its overall structure remains intact. It stands tall and menacing, still exuding a powerful and malevolent presence";
	else: [ sword twice or thrice ]
		say "The once-imposing form of the skeletal dragon begins to show significant signs of damage and decay. Several skeletal fragments are now cracked or missing, and its movements are less coordinated. Despite this, the dragon still poses a considerable threat and remains a dangerous adversary[if dragon-shield is in the location]. A spherical shield of dark magic protects the dragon from all attacks[end if]";

To say smite-dragon:
	if Alcaz is in the location:
		say "As you swing your sword at the skeletal dragon, its dark magic shield halts your attack";
	else:
		say "As you swing your sword at the skeletal dragon, you feel the blade encounter unexpected resistance. Despite the brittle and decayed state of the dragon's bones, they possess a dark magic that renders them resilient to your strike";

The initial appearance of the dragon is "[dragon-description].".
The printed name of the dragon is "dragon".
The denials of the dragon are {
  [regard] "[dragon-description].",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] "[smite-dragon]."}.
Understand "bone/skeletal/-- dragon of/-- bones/--" as the dragon.

Book - Palace Region

The palace-floor is a backdrop in the palace-region.
The printed name of the palace-floor is "palace floor".
The denials of the palace-floor are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "marble/-- tiled/-- palace/-- floor/tile/tiles" as the palace-floor.

The palace-wall is a backdrop in the palace-region.
The printed name of the palace-wall is "palace wall".
The denials of the palace-wall are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "marble/-- palace/-- wall/walls" as the palace-wall.

The palace-ceiling is a backdrop in the palace-region.
The printed name of the palace-ceiling is "palace ceiling".
The denials of the palace-ceiling are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "palace/-- ceiling" as the palace-ceiling.

The palace-archway is a backdrop in the palace-region.
The printed name of the palace-archway is "palace archway".
The denials of the palace-archway are {
  [regard] "",
  [seize] "Seizing [the noun] won't open new paths.",
  [loot] "[The noun] holds the promise of passage, not plunder.",
  [present] "",
  [treat] "",
  [smite] "Hitting [the noun] won't earn you any honor."}.
Understand "ornate/-- palace/-- archway/archways" as the palace-archway.

Chapter - Bedroom

The printed name of r-bedroom is "Royal Bedroom".
The description of r-bedroom is "The grandeur of your bedroom exudes an air of opulence and power, providing a space befitting a monarch. An ornate archway leads west to the dining room."

The refusals of r-bedroom are {
  [horizontal] "",
  [vertical] "",
  [untimely] "[if Lydia is in the location]Lydia wants to talk to you and will not let you go[else]You are no exhibitionist, seeking to expose yourself to the world in all your glory[end if].",
  [blackhole] ""}.

Conan is a man in r-bedroom.

[ Note: we need to lowercase 'your', otherwise it will be capitalized everywhere. ]
your bed is a closed openable container in r-bedroom. "[if open]Your bed's sheets and blankets have been further disrupted by your search. The mattress bears the impressions of your knees and hands as you rummaged through the bed in search of your loincloth[else]Your bed shows the signs of your hasty departure, with the sheets thrown aside, revealing a chaotic tangle of blankets and pillows. The mattress retains the imprints of your form, a reminder of the restless slumber you've just emerged from[end if].".
The denials of the bed are {
  [regard] "[if open]You have reclaimed your loincloth from your bed; there are no more treasures to be found in there.[else]You get the feeling that you forgot something when you left your bed in a rush. Maybe you should check if you left anything in it?[end if]",
  [seize] "[seize-heavy]",
  [loot] "[if open]You have reclaimed your loincloth from your bed; there are no more treasures to be found in there.[end if]",
  [present] "",
  [treat] "",
  [smite] "[smite-furniture]"}.
Understand "tangle of/--" and "sheet/sheets", "blanket/blankets", "pillow/pillows", "mattress/impression/impressions/imprint/imprints", "bed" as the bed.

The mirror is a thing in r-bedroom. "A full-length mirror is attached to the wall, boasting generous dimensions[if regarded]. It reflects the image of your mighty form, proudly displaying your primal strength[end if].".
The denials of the mirror are {
  [regard] "[if regarded]You've taken a good look at yourself in that mirror. There are battles to be fought, and you won't waste any more time admiring your reflection[else if Lydia is in the location]You see your mighty muscles, honed by countless battles and a life of adventure, ripple beneath your sun-tanned skin like living armor[else]You are no exhibitionist, seeking to catch a glimpse of yourself in all your glory[end if].",
  [seize] "You realize that [the noun] is firmly attached to the wall, and there's no practical reason to take it with you.",
  [loot] "The mirror holds no treasure, only your own reflection.",
  [present] "",
  [treat] "",
  [smite] "Smashing a mirror won't gain you any loot or fame."}.
Understand "full/length/full-length/generous", "mirror", and "canvas" as the mirror.

your loincloth is clothing in the bed. The owner of the loincloth is Conan.
The denials of the loincloth are {
  [regard] "Made of the finest leather, perfect for guarding your primal essence.",
  [seize] "There's no reason to seize [the noun] while it guards your primal essence.",
  [loot] "There's nothing hidden in [the noun], except the essence of a true barbarian.",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "Trying to smite [the noun]? [smite-focus]"}.
Understand "loin/-- cloth" and "loincloth" as the loincloth.

Table of Choices
action	type	flag	undoes	redoes	object (an object)
Conan looting bed	1	0	"You take off your loincloth and put it under the tangle of blankets, hiding your treasure once more."	"You approach your bed and reach down with strong, calloused hands, fingers deftly sifting through the fabric. You pull your loincloth free from its concealed spot, and you deftly don it."	--
Conan regarding mirror	-1	0	"The mirror's reflection shifts, and the image of your mighty form, proudly displaying your primal strength, fades like a wisp of smoke from the mirror."	"You gaze into your mirror. You see your mighty muscles, honed by countless battles and a life of adventure, ripple beneath your sun-tanned skin like living armor. Your loincloth hangs low on your hips, a symbol of your primal essence, a silent reminder of your untamed nature."	--
Conan marching west	1	0	""	"You stride purposefully from the royal bedroom, your powerful legs carrying you with a determined pace as you head west toward the dining room."	--

When play begins:
	now the walkthrough mode is ff-disabled;
	now the player is Conan;
	now yourself is nowhere;
	now the choice is 0;
	now the journey is entry choice plus one of the journeybook;
	now progress is 1;
	load the allowed actions;
	now the silencer is true;
	repeat with I running from 1 to 9:
		let A be entry 1 of allowed-actions;
		try A;
	now the silencer is false;

Chapter - Dining room

r-diningroom is west of r-bedroom.
The printed name of r-diningroom is "Dining Room".
The description of r-diningroom is "A spacious dining room, fit for a ruler, awaits you. Ornate archways lead east to your bedroom and west to the throne room."

The refusals of r-diningroom are {
  [horizontal] "",
  [vertical] "",
  [untimely] "You wonder whether there might be anything left to pilfer from the table.",
  [blackhole] ""}.

The table is a closed openable container in r-diningroom. "A grand dining table stands at the center of the room, bearing the glorious remnants of last night's feast. [if open]The space where the bottle of wine once sat is now empty, a reminder of your successful acquisition. [end if]Empty goblets and wine-stained dishes remain scattered across the table.".
the denials of the table are {
  [regard] "[if open]There are no more spoils to be had from this table, you have already claimed its prize.[else]You wonder whether there might be anything left to pilfer from the table.[end if]",
  [seize] "[seize-heavy]",
  [loot] "[if open]There are no more spoils to be had from this table, you have already claimed its prize.[end if]",
  [present] "",
  [treat] "",
  [smite] "[smite-furniture]"}.
Understand "grand/-- dining/-- table" and "grand/-- diningtable" and "faint/-- remnant/remnants" and "empty/-- goblet/goblets" and "wine/stained/wine-stained" and "dish/dishes" as the table.

The bottle is a closed openable container in the table.
The printed name of the bottle is "bottle of wine".
The denials of the bottle are {
  [regard] "[if open]The bottle is open and empty[else if the location is r-pit]The bottle is closed and filled with the finest wine. You'd rather drink the wine than just look at it[else]The bottle is closed and filled with the finest wine[end if].",
  [seize] "",
  [loot] "[if open]The bottle is open and empty, there is nothing left to loot from it[else][one of]Now's not the time to drown your worries[or]Wine won't wash away the challenges we face[cycling][end if].",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[if open]The bottle is open and empty, there is no need to smite it[else if the location is r-pit]You'd rather drink the wine than waste it[else]Hold on to that bottle, you might need its contents later[end if]."}.
Understand "wine/-- bottle" and "winebottle" and "bottle of wine" and "finest/-- wine" and "liquid/-- fire" as the bottle.

Some wine is an edible thing in the bottle.
The denials of the wine are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] ""}.

Table of Choices (continued)
action	type	flag	undoes	redoes	object (an object)
Conan looting table	1	0	"You carefully place the bottle of wine back onto the dining table."	"Amidst the remnants, your keen eyes spot a bottle of wine. With a careful hand, you retrieve the bottle."
Conan marching west	1	0	""	"You stride purposefully from the dining room, your powerful legs carrying you with a determined pace as you head west toward the throne room."
Conan toggling portal	0	0	"[portal-closes]"	"[portal-opens]"
Conan transferring demon to r-throneroom	0	0	"The portal begins to waver and distort. Its ethereal energies twist, and a void appears at its center. With a surge of force, the demon is pulled back through the portal, its form becoming indistinct. The demon's clawed fingers vanish into the portal's depths, followed by its wings, and finally its burning eyes."	"A clawed hand reaches through the portal, its fingers clutching the edge of the portal as it pulls itself forward. With a powerful surge, a demon steps forth, framed by the portal's twisting energies. The demon unfurls its wings, the leathery membranes stretching out to span an imposing width. Its eyes, burning like molten coals, fixate on you with malevolent intent."

Chapter - Throne room

r-throneroom is west of r-diningroom.
The printed name of r-throneroom is "Throne Room".
The description of r-throneroom is "The throne room stretches before you, a space of regal grandeur. Dominating the room's north side, the dragon throne commands attention. Ornate archways lead east to the dining room and south to the petition hall."

To say untimely-throneroom:
	if Conan is unarmed:
		say "Venturing into the unknown must wait until you have armed yourself. ";
	else:
		say entry 3 of the generic-refusals;

The refusals of r-throneroom are {
  [horizontal] "",
  [vertical] "",
  [untimely] "[untimely-throneroom]",
  [blackhole] ""}.

The throne is scenery in r-throneroom.
The denials of the throne are {
  [regard] "You can't help but feel a surge of pride and power when regarding your dragon throne[if your sword is in r-throneroom]. Your favorite sword rests on it[end if].",
  [seize] "You consider seizing the throne, but then you remember that the throne is already yours.",
  [loot] "You consider looting the throne, but then you remember that the throne is already yours, and holds no treasures to be plundered.",
  [present] "",
  [treat] "",
  [smite] "You consider smiting the throne, but you quickly dismiss the idea. The throne is a symbol of your rule and the realm you protect."}.
Understand "dragon/-- throne" and "throne/-- of/-- dragons" as the throne.

your sword is a weapon in r-throneroom. "On the throne, a gleaming sword rests, confirming your authority as the rightful ruler of the realm."
The denials of the sword are {
  [regard] "Your favorite sword, made of the finest steel[if smite-counter of chaos-marker is 3]. A fiery phoenix adorns the blade of your sword in a brilliant outline[end if][if conan is unarmed]. It looks like a suitable weapon to arm yourself with[end if].",
  [seize] "",
  [loot] "[loot-nothing]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-nothing]"}.
Understand "gleaming/-- sword" as the sword.

your axe is a weapon in r-throneroom. "A double-bladed axe, a relic of a battle fought a long time ago, hangs on the western wall."
The denials of the axe are {
  [regard] "A double-bladed axe, a relic of a battle fought a long time ago[if conan is unarmed]. It looks like a suitable weapon to arm yourself with[end if].",
  [seize] "",
  [loot] "[loot-nothing]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-nothing]"}.
Understand "double/-- bladed/doublebladed/double-bladed/-- axe/relic" as the axe.

To say portal-opens:
	say "[if location is r-throneroom]A shimmering rift materializes near the western wall of the throne room. Its edges flicker with an eerie light, and the air around it seems to waver as if touched by unseen forces[else]A shimmering radiance materializes to the east, its surface veiled in a shroud of mist. The air crackles with energy as the portal emerges, its edges dancing with hues of eerie light[end if]. ";

To say portal-fading:
	say "The portal, through which the demon originally emerged, shows signs of fading, its once vibrant energy now waning, as if nature itself seeks to close the rift between worlds. ";

To say portal-closes:
	say "[if location is r-throneroom]The portal's edges become increasingly transparent as if being erased from reality. Gradually, the portal recedes until it is but a memory[else]The magical portal that stood to the east gradually fades from existence. Its vibrant hues of eerie light begin to wane, like the dying embers of a fire[end if]. ";

The portal is a closed unopenable not apparent door. "[if location is r-throneroom]Near the west wall, a magical portal shimmers with eerie energy. It stands as a rift between worlds, a shimmering tear in reality. Its edges waver like heat rising from desert sands[else]To the east, a magical portal shimmers with eerie energy. It stands as a rift between worlds, a shimmering tear in reality. Its edges waver like heat rising from desert sands[end if]."
The printed name of the portal is "magical portal".
The denials of the portal are {
  [regard] "The portal is shimmering with eerie energy, a tear in reality.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
The portal is west of r-throneroom.
The portal is east of r-northbank.
Understand "magic/magical/shimmering/-- portal/rift/tear/edge/edges" as the portal when the portal is open.

Table of Order vs Chaos
action	type	flag	undoes	redoes	object (an object)
Conan seizing sword	1	0	"You carefully place your sword back on the throne, and retrace your steps to the east side of the throne room."	"You make a run for your sword on the throne. Grasping the hilt firmly, you feel its power surge through your veins, a familiar weight in your hand. Just as you secure the sword, the demon, sensing your threat, closes the distance in a frenzied assault.[paragraph break]The demon jumps at you, claws outstretched, a whirlwind of malevolent fury. You react quickly, holding your sword to parry the attack. You manage to deflect the demon's strike, narrowly avoiding its vicious claws."
Conan smiting demon	1	0	"The air becomes charged with energy, and a dark mist envelops the area where the outline lingers. Gradually, the demon fully reforms from the bloodstain, its dark form standing once again in the throne room. Your sword emerges whole from the demon's body.[paragraph break]Lydia emerges from the demon's gaping maw, her body covered in a dark slime. Her dress is stained and tattered, but she is alive and seemingly unharmed.[paragraph break]Lydia scrambles to her feet, and as she makes her escape from the throne room, the dark slime disappears from her body, and her dress repairs and cleans itself."	"With a primal roar, you launch an assault on the demon. Your muscles ripple with power as you unleash a mighty blow aimed at the heart of the creature. The demon attempts to parry your strike, but the force behind your attack overwhelms its defenses. Your sword slices into the demon's flesh, leaving a deep gash across its dark form."
Conan transferring Lydia to r-throneroom	0	0	""	"As your sword connects, Lydia, your court priestess, enters the throne room. At the sight of the demon, she lets out a shriek of horror. The demon, drawn by her vulnerability, starts moving menacingly toward Lydia, eyes gleaming with evil intent."
Conan smiting demon	1	0	""	"You lunge at the demon once more, the sword an extension of your primal fury. But the demon barely flinches as your sword slices through its dark essence."
Conan transferring Lydia to r-blackhole	0	0	""	"With a horrifying speed, the demon lunges toward your court priestess, a dark whirlwind of hunger. In an instant, it engulfs Lydia. The demon's shadowy form seems to expand, savoring its victory, leaving you with the weight of loss and the thirst for vengeance burning within you."
Conan smiting demon	1	0	""	"[frenzied-rage].[paragraph break][portal-fading]"
Conan swapping demon with stain	0	0	""	""
Conan swapping sword with hilt	0	0	""	""
Conan seizing axe	1	0	"You carefully hang the battle axe back on the western wall."	"You seize the double-bladed battle axe hanging on the west side of the throne room, feeling the weight of its power in your hands. This will serve as your new weapon, a suitable replacement for the sword you lost."
Conan toggling portal	0	0	"[portal-opens]"	"[portal-closes]"
Conan transferring Alcaz to r-throneroom	0	0	"Alcaz, your court mage, dematerializes from the throne room, his robe fading as he disappears from the scene."	"Alcaz, your court mage, materializes in the throne room, his robe swirling as he surveys the scene. He takes in the remnants of the battle, the lingering aura of magic, and the bloodstained floor. With a concerned expression, he approaches you. 'What happened here, my lord?'"
Conan treating Alcaz	1	0	"The words you exchanged with Alcaz dissolve, and memories fade. Alcaz no longer knows what happened in the throne room."	"You explain the fierce battle with the demon, the tragic loss of Lydia, and the ultimate victory leading to the demon's demise. After you finish recounting the events, he nods solemnly.[paragraph break]'Thank you for sharing this with me, my lord,' Alcaz says. 'I believe there might be a way to re-create the portal, to delve into the mysteries behind this threat. Please entrust me with the weapon you used to defeat the demon.'"
Conan presenting hilt to Alcaz	1	0	"Your sword hilt re-materializes in the hands of Alcaz, and he subsequently returns it to you."	"You hand the hilt of your broken sword to Alcaz. He holds it carefully as he begins to focus his magical powers. You watch intently as he starts to chant ancient incantations, combining the essence of the demon's blood on the floor with the remnants of the sword."
Conan transferring hilt to r-blackhole	0	0	""	"You see the traces of the demon's blood on the floor glow in response to Alcaz's magic, and a soft, ethereal glow surrounds the hilt until it fades into the ethereal plane."
Conan toggling portal	0	0	"[portal-closes]"	"[portal-opens]"
Conan marching west	1	0	"With Alcaz leading the way, you step confidently through the swirling portal, feeling its energies wrap around you."	"You step confidently through the swirling portal, feeling its energies wrap around you. Alcaz follows closely, his presence providing a sense of security in this unfamiliar realm."
Conan toggling portal	0	0	"[portal-opens]"	"[portal-closes]"

Table of Good vs Evil
action	type	flag	undoes	redoes	object (an object)
Conan seizing axe	1	0	"You carefully hang the battle axe back on the western wall, and circle back around the demon to the east side of the throne room."	"You swiftly move around the perimeter of the throne room. Your gaze remains locked on the demon, who is watching your every move with burning eyes. As you seize the double-bladed battle axe, the demon, sensing your threat, closes the distance in a frenzied assault.[paragraph break]The demon jumps at you, claws outstretched, a whirlwind of malevolent fury. You react quickly, holding your axe to parry the attack. You manage to deflect the demon's strike, narrowly avoiding its vicious claws."
Conan smiting demon	1	0	"The air becomes charged with energy, and a dark mist envelops the area where the outline lingers. Gradually, the demon fully reforms from the bloodstain, its dark form standing once again in the throne room. Your axe emerges whole from the demon's body.[paragraph break]Lydia makes her escape from the throne room, the horror radiating from the demon overwhelming her."	"With a primal roar, you swing the double-bladed battle axe with all your might. The weapon cleaves through the air and collides with the demon. You feel the impact through your arms as the axe bites deep into the demon's hide, carving a deep gash."
Conan transferring Lydia to r-throneroom	0	0	""	"As your axe connects, Lydia, your court priestess, enters the throne room. At the sight of the demon, she lets out a shriek of horror. The demon, drawn by her vulnerability, starts moving menacingly toward Lydia, eyes gleaming with evil intent."
Conan smiting demon	1	0	""	"You swing the double-bladed battle axe once more, directing the full force of your fury and skill at the demon. The axe strikes with a resounding impact and shatters, a deadly blow cleaving through the demon's form. The vile creature convulses, before dissolving completely, leaving only an outline in blood on the floor."
Conan swapping demon with stain	0	0	""	""
Conan swapping axe with handle	0	0	""	""
Conan seizing sword	1	0	"You carefully place your sword back on the throne."	"With the broken axe handle in hand, you decide to seize your sword, hoping it will serve as a suitable replacement for your shattered axe.[paragraph break][portal-fading]"
Conan marching west	1	0	"With Lydia leading the way, you step confidently through the swirling portal, feeling its energies wrap around you."	"You step confidently through the swirling portal, feeling its energies wrap around you. Just as you pass the threshold, a warm embrace envelops you from behind, and you realize that Lydia has followed you through.[paragraph break]'I feel that staying with you is safer than remaining in the royal palace, my lord,' Lydia says. 'I shall stand by your side, ready to face whatever challenges lie ahead.'"
Conan toggling portal	0	0	"[portal-opens]"	"[portal-closes]"

Chapter - Black Hole

r-blackhole is south of r-throneroom.
The printed name of r-blackhole is "Black Hole".

Alcaz is a man in r-blackhole.

Lydia is a woman in r-blackhole.

The demon is a monster in r-blackhole.

The stain is a thing in r-blackhole.

The serpent is a monster in r-blackhole.

The corpse is a monster in r-blackhole.

The corpses are scenery monsters in r-blackhole.
The corpses are plural-named.

The skeleton is a monster in r-blackhole.

The skeletons are scenery monsters in r-blackhole.
The skeletons are plural-named.

The dragon is a monster in r-blackhole.

The hilt is a thing in r-blackhole.
The printed name of the hilt is "sword hilt".
The denials of the hilt are {
  [regard] "The hilt is intricately crafted. Without its blade, which disappeared along with the demon, it is merely a pretty bauble[if conan is unarmed]. You should consider re-arming yourself with another weapon[end if].",
  [seize] "",
  [loot] "[loot-nothing]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-broken]"}.
Understand "broken/-- sword/-- hilt" and "pretty/-- bauble" as the hilt.

The handle is a thing in r-blackhole.
The printed name of the handle is "axe handle".
The denials of the handle are {
  [regard] "The axe handle shows signs of extensive use. Without its blades, which disappeared along with the demon, it is merely a pretty piece of steel[if conan is unarmed]. You should consider re-arming yourself with another weapon[end if].",
  [seize] "",
  [loot] "[loot-nothing]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-broken]"}.
Understand "broken/-- axe/-- handle" and "pretty/-- piece/-- of/-- steel piece/--" as the handle.

The dragon-shield is scenery in r-blackhole.
The printed name of the dragon-shield is "shield of dark magic".
The denials of the dragon-shield are {
  [regard] "The shield appears to protect the dragon against all attacks.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "The shield of dark magic remains impervious to your smites."}.
Understand "spherical" and "sphere" and "shield of/-- dark/-- magic/--" and "dark/-- magic/-- shield" as the dragon-shield.

Book - River Region

The sky is a backdrop in the river-region.
The printed name of the sky is "sky".
The denials of the sky are {
  [regard] "The night sky is vast and filled with countless stars.",
  [seize] "[seize-far]",
  [loot] "[loot-far]",
  [present] "",
  [treat] "",
  [smite] "[smite-far]"}.
Understand "dark/night/midnight/-- sky" and "night" as the sky.

The moon is a backdrop in the river-region.
The printed name of the moon is "moon".
The denials of the moon are {
  [regard] "The full moon hangs in the night sky, casting a silvery glow over the land.",
  [seize] "[seize-far]",
  [loot] "[loot-far]",
  [present] "",
  [treat] "",
  [smite] "[smite-far]"}.
Understand "full/-- and/-- radiant/-- moon/moonlight/light" as the moon.

Some stars is a backdrop in the river-region.
The printed name of the stars is "stars".
The denials of the stars are {
  [regard] "The stars twinkle in the dark expanse of the night sky.",
  [seize] "[seize-far]",
  [loot] "[loot-far]",
  [present] "",
  [treat] "",
  [smite] "[smite-far]"}.
Understand "star/stars" as the stars.

The mist is a backdrop. It is in r-northbank, r-darkriver, r-southbank, r-corridor, r-hall, r-cavern-2, r-cavern-3, r-cavern-4, r-cavern-5.
The printed name of the mist is "mist".
The denials of the mist are {
  [regard] "The mist is dense and swirling, almost as if alive.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
Understand "dense/-- swirling/-- mist" and "wispy/-- tendril/tendris" and "hazy/-- shroud" as the mist.

The river is a backdrop. It is in r-northbank, r-darkriver, r-southbank.
The printed name of the river is "river".
The denials of the river are {
  [regard] "A dark and foreboding river, its waters shrouded in dense, swirling mist, winds its way slowly through the landscape.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "dark/-- and/-- foreboding/-- mist/mist-covered/mist-shrouded/-- covered/shrouded/-- water/waters/river/surface/depth/depths" as the river.

Chapter - North Bank

The printed name of r-northbank is "North Bank".
The description of r-northbank is "A dark and foreboding river, its waters shrouded in dense, swirling mist, winds its way slowly through the landscape. The moon is full, casting a pale glow upon the scene. The mist glides across the river like a snake through desert sands."

To say untimely-northbank:
	if Alcaz is in the location:
		if Conan does not hold the ring:
			say "You prepare to board the ferry. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'We need to arrange payment for the ferryman.' ";
		else:
			say "You prepare to board the ferry. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'We need a sacrifice to appease the gods.' ";
	else:
		if Conan does not hold the necklace:
			say "You prepare to board the ferry. Lydia places a hand on your arm, halting your action. 'My lord,' she says firmly. 'We need to arrange payment for the ferryman.' ";
		else:
			say "You prepare to board the ferry. Lydia places a hand on your arm, halting your action. 'My lord,' she says firmly. 'We need a sacrifice to appease the gods.' ";

The refusals of r-northbank are {
  [horizontal] "",
  [vertical] "",
  [untimely] "[untimely-northbank]",
  [blackhole] ""}.

To say seize-goats:
	say "You eye the goats in the meadow, considering the idea of seizing them all. But you're a seasoned warrior, not a shepherd. You decide to focus on a more reasonable course of action. ";

The meadow is a closed transparent openable container in r-northbank. "A meadow stretches out to the horizon. A few goats roam freely, indulging in the lush vegetation."
The denials of the meadow are {
  [regard] "A meadow stretches out to the horizon. A few goats roam freely, indulging in the lush vegetation.",
  [seize] "[seize-solid]",
  [loot] "[seize-goats]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "grassy/lush/-- expanse/meadow/grass/vegetation/ground"  as the meadow.

Some goats are a scenery neuter animal in the meadow.
The denials of the goats are {
  [regard] "A meadow stretches out to the horizon. A few goats roam freely, indulging in the lush vegetation.",
  [seize] "[seize-goats]",
  [loot] "[loot-animal]",
  [present] "",
  [treat] "",
  [smite] "[smite-animal]"}.
Understand "few/-- goats" and "few" as some goats.

To say seize-goat:
	let T be the type in row progress of journey;
	if the player has the noun:
		say entry 2 of the generic-denials;
	else if Conan is carrying Lydia:
		say "You already have a sacrifice; you don't need another. ";
	else if Alcaz is in the location:
		say "You prepare to seize a goat. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'Let us first discuss our course of action before we start seizing things.' ";
	else if T is 3:
		say "Lydia places a hand on your arm, halting your action. 'Are you a shepherd? Do you prefer bringing a goat instead of me?' she says. ";
	else:
		say "'My Lord,' Lydia says, 'what are you planning to do with that goat?'[paragraph break]You reconsider your options. Maybe you better treat with Lydia first. ";

The goat is a scenery neuter animal in r-northbank.
The denials of the goat are {
  [regard] "The goat looks like a suitable sacrifice to appease the gods.",
  [seize] "[seize-goat]",
  [loot] "[loot-animal]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-animal]"}.
Understand "goat" as the goat.

The nb-ferryman is a scenery person in r-northbank.
The printed name of the nb-ferryman is "ferryman".
The denials of the nb-ferryman are {
  [regard] "The ferryman, shrouded in a dark cloak, is awaiting his passenger.",
  [seize] "You cannot seize the ferryman from here, you will need to march south first.",
  [loot] "You cannot loot the ferryman from here, you will need to march south first.",
  [present] "",
  [treat] "You cannot treat with the ferryman from here, you will need to march south first.",
  [smite] "You cannot smite the ferryman from here, you will need to march south first."}.
Understand "black/dark/-- cloak" and "ferryman" and "gnarled/-- and/-- weathered/-- hand/hands" as the nb-ferryman when the northdoor is open.

The north-landing is a thing in r-northbank. "To the south, a small landing extends into the mist-covered river. A copper bell hangs next to the landing, its metal gleaming faintly in the moonlight. A [if chest is open]once sturdy-looking chest lies broken and splintered[else]sturdy-looking chest rests[end if] under the bell."
The printed name of the north-landing is "landing".
The denials of the north-landing are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "small/-- wooden/-- landing" as the north-landing.

The north-river-bank is scenery in r-northbank.
The printed name of the north-river-bank is "river bank".
The denials of the north-river-bank are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "north/-- river/-- bank/landscape/shore" as the north-river-bank.

To say smite-bell:
	let T be the type in row progress of journey;
	if northdoor is open:
		say "There is no need to strike the bell, the ferry is already here. ";
	else if Alcaz is in the location:
		if T is 3:
			say "You raise your axe, preparing to strike the bell. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'Your axe would destroy the bell, not ring it. Please let me ring the bell.' ";
		else:
			say "You raise your axe, preparing to strike the bell. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'Let us first discuss our course of action before we start smiting things.' ";
	else:
		say "'My Lord,' Lydia says, 'what are you planning to do with that bell?'[paragraph break]You reconsider your options. Maybe you better treat with Lydia first. ";

The bell is scenery in r-northbank.
The denials of the bell are {
  [regard] "The copper bell is [if northdoor is open]ringing with a deep, resonating chime[else]silent[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-bell]"}.
Understand "faintly/-- gleaming/-- copper/metal/-- bell" as the bell.

The chest is a closed unopenable scenery container in r-northbank.
The denials of the chest are {
  [regard] "The [if open]once sturdy-looking chest lies broken and splintered.[else]sturdy-looking chest is closed. Maybe something valuable is stored in there?[end if] ",
  [seize] "[seize-heavy]",
  [loot] "[if open]You've already broken the chest open and claimed its contents. There's nothing more to be gained from it[else]You try to pry open the sturdy-looking chest but it seems to be securely locked. You will need a more forceful approach[end if]. ",
  [present] "",
  [treat] "",
  [smite] "[if open]The chest has already been broken open.[else if Alcaz is in the location]You raise [your-weapon], preparing to smite the chest. Alcaz places a hand on your arm, halting your action. 'Hold, warrior,' he says firmly. 'Let us first discuss our course of action before we start smiting things.'[else]You swing [your-weapon] at the chest with all your might, but it is clear that [your-weapon] is not the tool for this task.[end if] "}.

Understand "sturdy/sturdy-looking/-- looking/-- chest/lock" as the chest.
Understand "broken/splinter/splinters/splintered" as the chest when the chest is open.

The ring is clothing in chest.
The printed name of the ring is "gold ring".
The denials of the ring are {
  [regard] "A solid gold ring of high quality.",
  [seize] "",
  [loot] "[loot-nothing]",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[smite-nothing]"}.
Understand "gold/-- ring of/-- gold/-- high/-- quality/--" and "gold" as the ring.
Understand "payment/gold" as the ring when the location is r-northbank or the location is r-darkriver.
Understand "payment/gold" as the necklace when the location is r-northbank or the location is r-darkriver.

To say bank-opens:
	say "From the haze emerges a small boat, gliding toward the landing. The air seems to thicken as the boat nears, the mist clinging to the vessel like a protective shroud. The glimmer of a lantern at the prow illuminates the water's surface, casting an eerie glow dancing with the mist.[paragraph break]Clad in a black cloak, the ferryman himself remains concealed, his identity veiled in shadows. Only his hands are visible, gnarled and weathered, their movements deftly guiding the boat toward the shore. ";
	
To say bank-closes:
	say "The ferryman's boat slowly glides away from the shore, disappearing into the dense mist. The lantern's glow dims, casting an eerie light on the river before it, too, is swallowed by the mist. ";
	
To say river-opens:
	say "The boat glides smoothly towards the landing, its bow gently bumping against the worn wooden landing, as the ferryman guides the boat expertly to a halt. ";

To say river-closes:
	say "The mist thickens around the ferryman and the boat, as the ferryman begins to move the boat away from the landing. The landing starts to fade into the mist, vanishing from view. ";

The northdoor is a closed unopenable not apparent door. "[if location is r-northbank]The boat, moored to the landing, rests like a shadow upon the dark waters. Its occupant, shrouded in a dark cloak, is awaiting its passenger[else]The boat is bumping gently against the small landing to the north. The mist hangs heavy here, giving an eerie quality to the surroundings[end if]."

The printed name of the northdoor is "[if location is r-northbank]ferry[else]small landing[end if]".
The denials of the northdoor are {
  [regard] "[if location is r-northbank]The boat, moored to the landing, rests like a shadow upon the dark waters. Its occupant, shrouded in a dark cloak, is awaiting its passenger[else]Your scrutiny reveals nothing unusual or captivating about the small landing[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-nothing]",
  [present] "",
  [treat] "",
  [smite] "[smite-nothing]"}.
The northdoor is south of r-northbank and north of r-darkriver.
Understand "small/-- boat/ferry/vessel" and "lantern/prow" as the northdoor when the northdoor is open and the location is r-northbank.
Understand "small/-- wooden/-- landing" as the northdoor when the northdoor is open and the location is r-darkriver.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating alcaz	1	0	"The words you exchanged with Alcaz dissolve, and memories fade. You no longer remember the ancient ritual of crossing the dark river."	"'The river you see before us appears to be more than it seems,' Alcaz says. 'It is the boundary of the sorcerer's domain, and you need to follow an ancient ritual to cross it.'[paragraph break]'This looks like it was taken straight out of the legend of the Eternal Crossing,' you say. 'Offer the gold, ring the bell, and prepare a sacrifice, lest the ferryman holds you to stay with him until the end of times.'[paragraph break]'Not very original, I grant you that,' Alcaz says with a smile. 'We need to arrange payment for the ferryman, sound the bell to call upon him, and prepare a sacrifice to appease the gods.'"
Conan smiting chest	3	0	"You drop the gold ring into the chest, which repairs itself and becomes whole again."	"With a determined grip on your axe, you swing it forcefully at the chest before you. The blade strikes the chest, causing it to splinter and crack under the force of your blow, revealing a gold ring within. You claim the ring as your own."
Conan treating Alcaz	3	0	"Alcaz places a hand on the bell, silencing it."	"'Let me ring the bell,' Alcaz says. He strikes the bell, producing a deep, resonating chime echoing through the landscape. The mist stirs as if responding to the call, swirling and eddying."
Conan toggling northdoor	0	0	"[bank-closes]"	"[bank-opens]"
Conan seizing goat	3	0	"You release the goat. You are no shepherd, and you have no need for a goat."	"You swiftly close in on one of the goats grazing in the meadow. Summoning your primal strength, you seize the goat and lift it up from the ground."
Conan marching south	1	0	"With the goat secured in your powerful grasp, you step off the ferryman's boat.[paragraph break][alcaz-from-mist]"	"'You are ready to depart,' Alcaz says. 'Our paths must diverge for now, the ferryman's boat can only carry one at a time. I shall use my arcane arts to find another way across this boundary, and we will meet again at the other side.'[paragraph break][alcaz-to-mist][paragraph break]With the goat secured in your powerful grasp, you step onto the ferryman's boat.[paragraph break][ferry-demand]"
Conan transferring Alcaz to r-blackhole	0	0	""	""

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating Lydia	1	0	"The words you exchanged with Lydia dissolve, and memories fade. You no longer remember the ancient ritual of crossing the dark river."	"'This looks like it was taken straight out of the legend of the Eternal Crossing,' you say. 'Offer the gold, ring the bell, and prepare a sacrifice to appease the gods, lest the ferryman holds you to stay with him until the end of times.'[paragraph break]'Yes, it does look like it, doesn't it?' Lydia says with a smile."
Conan treating Lydia	3	0	"You return the necklace to Lydia, promptly forgetting why you borrowed it in the first place."	"'I see no gold here,' you say. 'Would it be possible for me to borrow your gold necklace for a while? I promise I will return it to you after we cross the river.'[paragraph break]Lydia hands you her gold necklace of Mitora."
Conan transferring necklace to Conan	0	0	""	""
Conan smiting bell	3	0	"You place a hand on the bell, silencing it."	"Stepping up to the bell, you carefully strike it with the hilt of your sword, producing a deep, resonating chime echoing through the landscape. The mist stirs as if responding to the call, swirling and eddying."
Conan toggling northdoor	0	0	"[bank-closes]"	"[bank-opens]"
Conan seizing Lydia	3	0	"You gently put Lydia on her feet again."	"Since the boat is too small to accommodate both you and Lydia sitting in it, you gently lift Lydia into your arms."
Conan marching south	1	0	"With Lydia secured in your powerful grasp, you step off the ferryman's boat."	"With Lydia secured in your powerful grasp, you step onto the ferryman's boat.[paragraph break][ferry-demand]"

Chapter - Dark River

The printed name of r-darkriver is "Dark River".
The description of r-darkriver is "The boat gently rocks on the waters of the dark river, its lantern swinging in rhythm with its currents."
The refusals of r-darkriver are {
  [horizontal] "You do not dare to step off the ferry, for there is only the river below.",
  [vertical] "[if the noun is up]The mist doesn't offer any footholds for climbing[else]You do not dare to step off the ferry, for there is only the river below[end if].",
  [untimely] "'Hold on,' Lydia says, 'you promised to give me back my necklace. Don't think you can just forget about it!'[paragraph break]Your instinct tells you to take a closer look at the ferryman. Something seems off about him.",
  [blackhole] ""}.

The ferryman is a man in r-darkriver.
The room-recipient of r-darkriver is the ferryman.

The boat is scenery in r-darkriver.
The denials of the boat are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-nothing]",
  [present] "",
  [treat] "",
  [smite] "[smite-nothing]"}.
Understand "small/-- boat/ferry/vessel" and "lantern/prow" and "helm" as the boat.

Understand "massive/ominous/presence/stirring" as the river when the location is r-darkriver and the serpent is not in r-darkriver.
Understand "massive/ominous/presence/stirring" as the serpent.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan presenting ring to ferryman	1	0	"The ferryman rises and returns the gold ring to you."	"As you offer the gold ring to the ferryman, his hand takes the payment with a slow and deliberate movement. The ferryman then sits down at the helm of the small boat and pushes the boat away from the small landing. "
Conan transferring ring to ferryman	0	0	""	""
Conan toggling northdoor	0	0	"[river-opens]"	"[river-closes][paragraph break]You sense a presence beneath the surface, something massive and ominous stirring in the depths. "
Conan regarding river	1	0	"The serpent descends back into the depths from which it emerged. The river's surface calms and the mist closes in once more as if concealing the serpent's presence from the world above."	"You feel the ominous presence approaching you from underneath, slowly rising and circling the ferry until it reaches the river's surface. "
Conan transferring serpent to r-darkriver	0	0	""	"A massive serpent with a body as thick as a tree trunk erupts from the waters, raising its head with a deafening roar, its maw lined with rows of razor-sharp teeth. The serpent turns its head towards you and prepares to strike. "
Conan smiting serpent	1	0	"The serpent resurfaces with a thunderous roar, its strength revitalized."	"You tighten your grip on your battle axe, raising your weapon high and bringing it crashing down upon the serpent's head. Your strike is swift and powerful, connecting with a resounding impact. The serpent recoils with a guttural roar of pain, but you will need more smites to take it down."
Conan smiting serpent	1	0	""	"Summoning your inner strength, you strike again, your axe cleaving through the air with a force echoing your resolve. The serpent's scales splinter under the assault, yet it still thrashes and lunges, its massive form a testament to its relentless power. Another smite might take it down."
Conan smiting serpent	1	0	""	"On your third strike, your battle cry rings out as your axe finds its mark once more. With a final, earth-shaking roar rippling through the waters, the serpent retreats, sinking back into the depths from which it emerged. The river's surface calms and the mist closes in once more as if concealing the serpent's presence from the world above."
Conan transferring serpent to r-blackhole	0	0	""	"Breathing heavily, you stand victorious, your axe held high. The danger has passed, at least for now. The ferryman rises and begins to prepare for mooring the small boat."
Conan toggling southdoor	0	0	"[river-closes]"	"[river-opens]"
Conan marching south	1	0	"With the goat secured in your powerful grasp, you step onto the ferryman's boat."	"With the goat secured in your powerful grasp, you step off the ferryman's boat."

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan presenting necklace to ferryman	1	0	"The ferryman rises and returns the necklace to you."	"As you offer the necklace to the ferryman, his hand takes the payment with a slow and deliberate movement. The ferryman then sits down at the helm of the small boat and pushes the boat away from the small landing. "
Conan transferring necklace to ferryman	0	0	""	""
Conan toggling northdoor	0	0	"[river-opens]"	"[river-closes][paragraph break]You sense a presence beneath the surface, something massive and ominous stirring in the depths. "
Conan regarding river	1	0	"Something massive and ominous moves underneath the boat, but does not rise to the surface."	"'Something massive and ominous stirred beneath the surface but did not rise to challenge our crossing,' you note.[paragraph break]Lydia concurs, saying, 'I felt it too. It's as though something is aware of our presence but has opted not to interfere.' "
Conan toggling southdoor	0	0	"[river-closes]"	"[river-opens]"
Conan looting ferryman	1	0	"Lydia returns her necklace to you, and you hide it beneath the ferryman's cloak."	"'My lord,' Lydia says, her voice trembling, 'it is [i]exactly[r] like the legend. There is no bell here, and the ferryman will steal our life force, for there is no way back.'[paragraph break]'In the city, we behand thieves,' you say, 'but it seems you've retained yours, ferryman.' A strange sensation washes over you as if you've walked into a spider's web. 'Or rather,' you continue, as you pull away the ferryman's cloak, 'hands are all you have left.'[paragraph break]Lydia gasps in horror, staring at the skeletal figure of the ferryman as you retrieve her necklace from its bony frame and hand it back to her. 'I fear I was mistaken. We are facing something far worse than a sorcerer. Our enemy appears to be an accursed necromancer,' you conclude, letting the cloak go to cover up the ferryman once more. "
Conan transferring necklace to Lydia	0	0	""	""
Conan marching south	1	0	"With Lydia secured in your powerful grasp, you step onto the ferryman's boat."	"With Lydia secured in your powerful grasp, you step off the ferryman's boat."

Chapter - South Bank

The printed name of r-southbank is "South Bank".
The description of r-southbank is "A dark and foreboding river, its waters shrouded in dense, swirling mist, winds its way slowly through the landscape. The moon is full, casting a pale glow upon the scene. The mist glides across the river like a snake through desert sands.[paragraph break]To the north, a small landing extends into the mist-covered river.[paragraph break]To the south, [if the gate is open]the stone gate stands open, providing passage into darkness beyond[else]a stone gate blocks your way into the fortress of the sorcerer, its surface etched with intricate designs depicting the eternal struggle between the Gods of Order and Chaos. The images seem to move and shift as if they carry a life of their own[end if]."

The refusals of r-southbank are {
  [horizontal] "",
  [vertical] "",
  [untimely] "[altar-complains]You turn around and go back to the altar.",
  [blackhole] ""}.

The south-landing is scenery in r-southbank.
The printed name of the south-landing is "landing".
The denials of the south-landing are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "small/-- wooden/-- landing" as the south-landing.

The south-river-bank is scenery in r-southbank.
The printed name of the south-river-bank is "river bank".
The denials of the south-river-bank are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "south/-- river/-- bank/landscape/shore" as the south-river-bank.

Understand "sacrifice" as Lydia when the holder of Lydia is Conan.
Understand "sacrifice" as the goat when the holder of Lydia is not Conan.

The sb-ferryman is a scenery person in r-southbank.
The printed name of the sb-ferryman is "ferryman".
The denials of the sb-ferryman are {
  [regard] "The ferryman, shrouded in a dark cloak, appears to be waiting for something.",
  [seize] "You cannot seize the ferryman from here, you will need to march north first.",
  [loot] "You cannot loot the ferryman from here, you will need to march north first.",
  [present] "",
  [treat] "You cannot treat with the ferryman from here, you will need to march north first.",
  [smite] "You cannot smite the ferryman from here, you will need to march north first."}.
Understand "black/dark/-- cloak" and "ferryman" and "gnarled/-- and/-- weathered/-- hand/hands" as the sb-ferryman when the southdoor is open.

The southdoor is a closed unopenable not apparent door. "[if location is r-southbank]The boat, moored to the landing, rests like a shadow upon the dark waters. Its occupant, shrouded in a dark cloak, appears to be waiting for something[else]The boat is bumping gently against the small landing to the south. The mist hangs heavy here, giving an eerie quality to the surroundings[end if]."
The printed name of the southdoor is "[if location is r-southbank]ferry[else]small landing[end if]".
The denials of the southdoor are {
  [regard] "[if location is r-southbank]The boat, moored to the landing, rests like a shadow upon the dark waters. Its occupant, shrouded in a dark cloak, appears to be waiting for something[else]Your scrutiny reveals nothing unusual or captivating about the small landing[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-nothing]",
  [present] "",
  [treat] "",
  [smite] "[smite-nothing]"}.
The southdoor is north of r-southbank and south of r-darkriver.
Understand "small/-- boat/ferry/vessel" and "lantern/prow" as the southdoor when the southdoor is open and the location is r-southbank.
Understand "small/-- wooden/-- landing" as the southdoor when the southdoor is open and the location is r-darkriver.

To say gate-opens:
	say "The images on the gate to the south seem to come alive, rearranging their positions. The gate's surface starts to tremble, and a hairline fracture emerges, tracing a path across the carvings. Slowly, the solid stone gives way, revealing a passage into darkness beyond. ";

To say gate-closes:
	say "The images on the gate to the south seem to come alive, rearranging their positions. The gate's surface starts to tremble, and the solid stone gradually closes until only a hairline fracture remains. Then the fracture knits itself together, and the stone's surface smoothes over as the gate reclaims its original form. ";
	
The gate is a closed unopenable not apparent scenery door in r-southbank.
The gate is south of r-southbank and north of r-order-1.
The denials of the gate are {
  [regard] "To the south, [if the gate is open]the stone gate stands open, providing passage into darkness beyond[else]a stone gate blocks your way into the fortress of the sorcerer, its surface etched with intricate designs depicting the eternal struggle between the Gods of Order and Chaos. The images seem to move and shift as if they carry a life of their own[end if].[if the gate is open and the altar is closed][paragraph break][altar-complains][end if] ",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/-- gate of/-- order/chaos/-- and/-- chaos/order/--" and "sorcerer's/-- fortress of/-- the/-- sorcerer" as the gate when the location is r-southbank.

The idol is a neuter scenery person in r-southbank.
The room-recipient of r-southbank is the idol.

The altar is an open unopenable container in r-southbank.
The initial appearance of the altar is "An altar dominates the area[if gate is open and altar is open]. The altar appears to have been looted, for it is open and empty[end if]. Above the altar, a towering idol looms, its form reflecting both the chaotic and ordered aspects of the deities it represents.".

To say goat-complains:
	say "From within the altar, you hear a muffled 'baaaaaaaaaa' from the trapped goat. ";
	
To say altar-complains:
	if the choice is 1:
		say goat-complains;
	else:
		say lydia-complains;

The initial appearance of the altar is "An altar dominates the area[if gate is open and altar is open]. The altar appears to have been looted, for it is open and empty[end if]. Above the altar, a towering idol looms, its form reflecting both the chaotic and ordered aspects of the deities it represents.".

The denials of the altar are {
  [regard] "[if gate is open and altar is open]The altar appears to have been looted, for it is open and empty.[else if gate is open][altar-complains][else]The altar appears to be awaiting its sacrifice.[end if] ",
  [seize] "[seize-solid]",
  [loot] "[if gate is open][entry 3 of generic-denials][else]As you approach the altar, a sensation of anticipation fills the air. It's as if the very stone is aware of the life you hold in your hands.[end if]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/-- altar" as the altar.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan presenting goat to idol	1	0	"The altar's surface glows with an eerie light, and the goat slowly rises through it. You seize the goat, and the surface of the altar darkens, leaving no trace of what transpired here."	"You place the goat upon the altar before the idol. As the goat touches the surface of the altar, the altar's edges begin to glow with an eerie light, and the goat slowly sinks into it. The surface then darkens again, leaving no trace of the offering."
Conan transferring goat to altar	0	0	""	""
Conan toggling gate	0	0	"[gate-closes]"	"[gate-opens]"
Conan transferring corpse to r-southbank	0	0	"The wandering corpse begins to dig into the ground, covering itself with earth as it goes. After a while, the ground before you settles and becomes still."	"The ground before you heaves and shifts as a wandering corpse emerges, clawing its way out of the earth. It slowly rises with outstretched arms and milky white, glazed-over eyes, turning toward you."
Conan smiting corpse	1	0	"A cloud of noxious particles materializes, coalescing into the form of a wandering corpse."	"You swiftly bring [your-weapon] down upon the corpse. The blade connects with its decaying flesh, and the impact sends a shudder through the creature's form, as you sever bone and sinew."
Conan transferring corpse to r-blackhole	0	0	""	"[corpse-dies].[paragraph break][goat-complains]"
Conan toggling southdoor	0	0	"[bank-opens]"	"The ferryman radiates disappointment as he watches the wandering corpse fail to kill you. [bank-closes]"
Conan transferring Alcaz to r-southbank	0	0	"[alcaz-to-mist]"	"[alcaz-from-mist][paragraph break]'Ah, my lord,' Alcaz greets you, 'I see you've faced the challenges of this place head-on, as I knew you would.' Alcaz's expression turns more serious. 'The sorcerer noticed my crossing of the boundary. He sent a ripple of energy, a warning, to deter my passage. It took me some time to circumvent his magical gaze and finally rejoin you here.' "
Conan looting altar	1	0	"As the goat reappears, you swiftly seize it and place it back into the altar, which then closes, trapping the goat within."	"You inspect the altar, searching for any hidden mechanisms or openings. As your fingers graze a specific part of the altar, a hairline crack opens, revealing the innards of the altar. You carefully lift the goat out of the altar and set it free. You are no shepherd, and you have no need for a goat."
Conan transferring goat to r-blackhole	0	0	""	""
Conan marching south	1	0	""	"As you pass through the Gate of Order and Chaos, the darkness envelops you like a shroud. Alcaz raises his staff and murmurs a brief incantation. The staff starts emanating a soft, ethereal glow. Step by step, you traverse the dimly lit path, until you reach a small island in this realm of darkness.[paragraph break]'Be mindful, my lord,' Alcaz warns. 'This place is treacherous, and the boundaries between reality and the unknown are blurred. One false step, and you could be lost to the darkness forever. Let us tread carefully.'"

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan presenting Lydia to idol	1	0	"The altar's surface glows with an eerie light, and Lydia slowly rises through it. You seize Lydia, and the surface of the altar darkens, leaving no trace of what transpired here."	"You place Lydia upon the altar before the idol. As Lydia touches the surface of the altar, the altar's edges begin to glow with an eerie light, and Lydia slowly sinks into it. 'I'll be back,' you say to her, as the surface darkens, leaving no trace of the offering that had been placed upon it."
Conan transferring Lydia to altar	0	0	""	""
Conan toggling gate	0	0	"[gate-closes]"	"[gate-opens]"
Conan transferring corpse to r-southbank	0	0	""	"The ground before you heaves and shifts as a wandering corpse emerges, clawing its way out of the earth. It slowly rises with outstretched arms and milky white, glazed-over eyes, turning toward you."
Conan transferring corpses to r-southbank	0	0	"The wandering corpses begin to dig into the ground, covering themselves with earth as they go. After a while, the ground before you settles and becomes still."	"Two more wandering corpses claw their way out from the earth. They stagger towards you until you are surrounded by the corpses on three sides."
Conan smiting corpses	1	0	"Clouds of noxious particles materialize, coalescing into the forms of three wandering corpses."	"You channel your primal strength into a powerful swing of your sword, cleaving all three corpses in a single smite."
Conan transferring corpses to r-blackhole	0	0	""	"The corpses's bodies collapse under the force of your blow. In an instant, their forms burst into clouds of noxious particles, carried away by an ethereal wind, which seems to erase the remnants of their cursed existence.[paragraph break][lydia-complains]"
Conan transferring corpse to r-blackhole	0	0	""	""
Conan toggling southdoor	0	0	"[bank-opens]"	"The ferryman radiates disappointment as he watches the wandering corpses fail to kill you. [bank-closes]"
Conan looting altar	1	0	"You seize Lydia and, ignoring her protests, place her back into the altar, which then closes, trapping Lydia within."	"You inspect the altar, searching for any hidden mechanisms or openings. As your fingers graze a specific part of the altar, a hairline crack opens, revealing the innards of the altar. You gently lift Lydia out of the altar and put her on her feet again."
Conan transferring Lydia to r-southbank	0	0	""	""
Conan marching south	1	0	""	"As you pass through the Gate of Order and Chaos, the darkness envelops you like a shroud. Lydia touches her necklace, and it starts to emanate a soft, ethereal glow. Step by step, you traverse the dimly lit path, until you reach a small island in this realm of darkness.[paragraph break]'Be careful, my lord,' Lydia warns. 'This place feels treacherous, and the boundaries between reality and the unknown are blurred. One false step, and you could be lost to the darkness forever. Let us tread carefully.'"

Book - Order Region

The order-abyss is a backdrop in the order-region.
The printed name of the order-abyss is "abyss".
The denials of the order-abyss are {
  [regard] "You see nothing but darkness below.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
Understand "abyss" and "void" and "void/-- of/-- darkness" as the order-abyss.

The order-floor is a backdrop in the order-region.
The printed name of the order-floor is "island".
The denials of the order-floor are {
  [regard] "The island, suspended in an endless void of darkness, is made of pitted black stone, possibly of volcanic origin.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "pitted/-- black/-- volcanic/-- stone/-- island/floor" as the order-floor.

The order-path is a backdrop in the order-region.
The printed name of the order-path is "path".
The denials of the order-path are {
  [regard] "The paths are made of pitted black stone, possibly of volcanic origin.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "pitted/-- black/-- volcanic/-- stone/-- path/paths" as the order-path.

To say vertical-order:
	say "[if the noun is up]Up is only darkness[else]You do not dare to step off the island, for there is only darkness below[end if]. ";

To decide which number is the order marker regardings:
	if the location is r-order-1, decide on the regard-counter of r-marker-1;
	if the location is r-order-2, decide on the regard-counter of r-marker-2;
	if the location is r-order-3, decide on the regard-counter of r-marker-3;

To say blackhole-order:
	if Lydia is in the location:
		say "As you attempt to march to [the noun], away from the marker, you feel an unsettling shift beneath your feet. 'My lord,' Lydia says urgently, 'darkness emanates from that direction. Please come back to the island and reconsider.' ";
	else if the location is r-order-3 and the regard-counter of r-marker-3 is 1:
		say "'My lord!' Alcaz calls out, 'wait! I believe you may have only partially deciphered the runes. Please, regard the marker once more, and let us be certain of the path we must take.' ";
	else if the order marker regardings is 0:
		say "'My lord,'	Alcaz says, 'we should decipher the runes on this marker. Straying blindly might lead us into even deeper peril.' ";
	else:
		say "As you attempt to march to [the noun], away from the marker, you feel an unsettling shift beneath your feet. 'My lord,' Alcaz says urgently, 'stay on the path. The darkness here is treacherous, and straying from the marked path could be perilous. Let us continue in the direction the runes have shown us.' ";
		
To say island:
	say "The island, suspended in an endless void of darkness, is made of pitted black stone, possibly of volcanic origin, with paths leading outwards in all compass directions";

To say smite-marker:
	if dream-reached is false:
		say "[smite-solid]";
	else if the location is not r-chaos-3:
		say "Across the mists of time, these words come to you: [i]'This is not the place. Not all paths are lost.'[roman type]";
	else:
		say "The monolithic marker has already been broken. ";

To say marker-desc (d - direction):
	say "An ancient weathered marker dominates the island. Its monolithic surface is etched with intricate runic script, pulsing with an otherworldly energy";
	if Alcaz is in the location:
		if regarded:
			say ", directing you to march [d]";

Chapter - Path of Order Room 1

The printed name of r-order-1 is "Path of Order".
The description of r-order-1 is "[island]."
The refusals of r-order-1 are {
  [horizontal] "",
  [vertical] "[vertical-order]",
  [untimely] "",
  [blackhole] "[blackhole-order]"}.

r-blackhole is a room northeast of r-order-1.
r-blackhole is a room southeast of r-order-1.
r-blackhole is a room south     of r-order-1.
r-blackhole is a room southwest of r-order-1.
r-blackhole is a room west      of r-order-1.
r-blackhole is a room northwest of r-order-1.

The r-marker-1 is a thing in r-order-1. "[marker-desc east]."
The printed name of the r-marker-1 is "marker".
The denials of the r-marker-1 are {
  [regard] "[if Alcaz is in the location]As you gaze once more upon the monolithic marker, the message remains the same, reaffirming its guidance to the east[else]You consider looking at the runes once more, but you decide against it[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-marker]"}.
Understand "ancient/-- weathered/-- stone/-- marker" and "ancient/-- weathered/-- stone" and "intricate/-- runic/-- script/rune/runes" as the r-marker-1.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding r-marker-1	-1	0	"Your memory of reading the marker runes fades, and you no longer remember the direction they indicated."	"The runes appear to be ancient symbols of guidance. Alcaz leans in closer, studying the runes intently, and then nods with understanding. 'These runes are directing us,' he says. 'They point to the east, my lord. It seems that's the way we should proceed.'"
Conan marching east	1	0	""	"You proceed to march eastward, following [if r-marker-1 is regarded]the marked path as indicated by the runes on the marker[else]your instincts[end if]. The pitted black stone pathway seems to stretch endlessly into the darkness, but you remain resolute in your advance.[paragraph break]As you arrive on the next island, Alcaz's staff casts a dim glow over a new marker, carved from the same mysterious material as the previous one. The runic script is intricate and enigmatic, another puzzle begging to be solved."

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding r-marker-1	-1	0	"You briefly hear the enticing voices, but you ignore them."	"You lean in closer, studying the runes intently. It is almost as if the runes are speaking directly to you; you hear whispering voices, encouraging you to listen to them, to hear their stories, to learn what they have seen and heard.[paragraph break]'My lord!' Lydia says urgently and pulls you away. You realize you were gripping the marker with both hands, afraid to let go and lose the soothing sound of the voices.[paragraph break]'I do not like these runes. They feel ... tainted. I think it's best to ignore them for now and rely on your instincts to guide us through this darkness.'"
Conan marching east	1	0	""	"You proceed to march eastward, following your instincts. The pitted black stone pathway seems to stretch endlessly into the darkness, but you remain resolute in your advance.[paragraph break]As you arrive on the next island, Lydia's necklace casts a dim glow over a new marker, carved from the same mysterious material as the previous one. The runic script is intricate and enigmatic, another puzzle begging to be solved.[paragraph break]'My lord,' Lydia cautions, 'stay resolute. Pay no heed to the runes, rely on your instincts!'"

Chapter - Path of Order Room 2

The printed name of r-order-2 is "Path of Order".
The description of r-order-2 is "[island]."
The refusals of r-order-2 are {
  [horizontal] "",
  [vertical] "[vertical-order]",
  [untimely] "",
  [blackhole] "[blackhole-order]"}.

r-order-2 is east of r-order-1.
r-blackhole is a room north     of r-order-2.
r-blackhole is a room northeast of r-order-2.
r-blackhole is a room east      of r-order-2.
r-blackhole is a room southeast of r-order-2.
r-blackhole is a room southwest of r-order-2.
r-blackhole is a room northwest of r-order-2.

The r-marker-2 is a thing in r-order-2. "[marker-desc south]."
The printed name of the r-marker-2 is "marker".
The denials of the r-marker-2 are {
  [regard] "[if Alcaz is in the location]As you gaze once more upon the monolithic marker, the message remains the same, reaffirming its guidance to the south[else]You briefly contemplate regarding the runes, but your better judgment prevails, and you shake off the urge[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-marker]"}.
Understand "ancient/-- weathered/-- stone/-- marker" and "ancient/-- weathered/-- stone" and "intricate/-- runic/-- script/rune/runes" as the r-marker-2.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding r-marker-2	-1	0	"Your memory of reading the marker runes fades, and you no longer remember the direction they indicated."	"With Alcaz's guidance, you discern the message within the runic script. The ancient symbols point to the south, indicating the path you should follow next. Alcaz nods in confirmation. 'To the south, my lord. Let us proceed in that direction.'"
Conan marching south	1	0	""	"With Alcaz leading the way, his staff's glow guiding your path through the dark, you make your way southward to the next island. The path beneath your feet appears to be fashioned from the same pitted black stone.[paragraph break]At the center of the island stands another marker, carved from the same mysterious material as the previous one. The marker's surface bears a new set of runic symbols[if r-marker-2 is regarded], some of which seem familiar[end if]."

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching south	1	0	""	"Following your instincts, you make your way southward to the next island. The path beneath your feet appears to be fashioned from the same pitted black stone.[paragraph break]At the center of the island stands another marker, carved from the same mysterious material as the previous one."

Chapter - Path of Order Room 3

The printed name of r-order-3 is "Path of Order".
The description of r-order-3 is "[island]."
The refusals of r-order-3 are {
  [horizontal] "",
  [vertical] "[vertical-order]",
  [untimely] "",
  [blackhole] "[blackhole-order]"}.

r-order-3 is south of r-order-2.
r-blackhole is a room northeast of r-order-3.
r-blackhole is a room east      of r-order-3.
r-blackhole is a room southeast of r-order-3.
r-blackhole is a room south     of r-order-3.
r-blackhole is a room west      of r-order-3.
r-blackhole is a room northwest of r-order-3.

The r-marker-3 is a thing in r-order-3. "[if studied][marker-desc southwest][else][marker-desc south][end if]."
The printed name of the r-marker-3 is "marker".
The denials of the r-marker-3 are {
  [regard] "[if Alcaz is in the location]As you gaze once more upon the monolithic marker, the message remains the same, reaffirming its guidance to the southwest[else]You briefly contemplate regarding the runes, but your better judgment prevails, and you shake off the urge[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-marker]"}.
Understand "ancient/-- weathered/-- stone/-- marker" and "ancient/-- weathered/-- stone" and "intricate/-- runic/-- script/rune/runes" as the r-marker-3.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding r-marker-3	-1	0	"Your memory of reading the marker runes fades, and you no longer remember the direction they indicated."	"You regard the runic script. You almost immediately recognize the runes indicating south and conclude this must be the way forward. Alcaz seems to be preoccupied with reading other parts of the runic script and does not confirm your conclusion."
Conan regarding r-marker-3	-1	0	"Your memory of reading the additional runes fades, and now you only remember the marker indicating south."	"You regard the runic script again and notice some more runes following the ones indicating south. These are unfamiliar, and you guess they must indicate west, as you haven't seen these earlier. Alcaz, with an approving nod, concurs. 'You are absolutely correct, my lord. Southwest it is, following the path laid out by these runes.'[paragraph break]Alcaz chuckles. 'It seems even the mighty gods have their limitations, my lord. Did you notice that all their words on the markers are composed of nine runes or fewer?'"
Conan marching southwest	1	0	"You retrace your steps, heading to the northeast, and the path reappears beneath your feet as you progress."	"With cautious steps, you and Alcaz traverse the path leading to the next island. As you reach its shore, a tremor runs through the isle. The paths behind, stretching to the northeast, and the one directly ahead, leading southwest, disintegrate before your very eyes. The pitted black stone crumbles into darkness, leaving an impassable abyss in its wake."

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching southwest	1	0	"You retrace your steps, heading to the northeast, and the path reappears beneath your feet as you progress."	"With cautious steps, you and Lydia traverse the path leading to the next island. As you reach its shore, a tremor runs through the isle. The paths behind, stretching to the northeast, and the one directly ahead, leading southwest, disintegrate before your very eyes. The pitted black stone crumbles into darkness, leaving an impassable abyss in its wake.[paragraph break]'My lord,' Lydia exclaims, 'the path... it's gone!'"

Book - Chaos Region

The chaos-abyss is a backdrop in the chaos-region.
The printed name of the chaos-abyss is "abyss".
The denials of the chaos-abyss are {
  [regard] "You see nothing but darkness below.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
Understand "abyss" and "void" and "void/-- of/-- darkness" as the chaos-abyss.

The chaos-floor is a backdrop in the chaos-region.
The printed name of the chaos-floor is "island".
The denials of the chaos-floor are {
  [regard] "The island, suspended in an endless void of darkness, is made of pitted black stone, possibly of volcanic origin.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "pitted/-- black/-- volcanic/-- stone/-- island/floor" as the chaos-floor.

The chaos-path is a backdrop in the chaos-region.
The printed name of the chaos-path is "path".
The denials of the chaos-path are {
  [regard] "The paths are made of pitted black stone, possibly of volcanic origin.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "path/paths" as the chaos-path.
Understand "pitted/-- black/-- volcanic/-- stone/-- path/paths" as the chaos-path.

To say marker:
	if smite-counter of chaos-marker is 0:
		say "The monolithic marker is devoid of runes or markings on its surface";
	else if smite-counter of chaos-marker is 1:
		say "The monolithic marker is littered with small cracks across its surface";
	else if smite-counter of chaos-marker is 2:
		say "The monolithic marker's surface is fractured, revealing glimpses of the ancient stone's interior";
	else:
		say "The monolithic marker has been broken, revealing a path leading downward";

The chaos-marker is a backdrop in the chaos-region.
The printed name of the chaos-marker is "marker".
The denials of the chaos-marker are {
  [regard] "[marker].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-marker]"}.
Understand "ancient/-- weathered/-- stone/-- marker" and "ancient/-- weathered/-- stone" and "monolithic/-- marker" and "monolith" as the chaos-marker.

To say vertical-chaos:
	say "[if the noun is up]Up is only darkness[else]You do not dare to step off the island, for there is only darkness below[end if]. ";

To say horizontal-chaos:
	say "The path to [the noun] ends in darkness, and you can go no further. You return to the island. ";

To say untimely-chaos:
	if Alcaz is in the location:
		say "'My lord,' Alcaz says, 'we should decide on a course of action. Straying blindly might lead us into even deeper peril.' ";
	else if the location is r-chaos-1:
		say "You realize that Lydia has frozen in place, her eyes wide with fear, and her body trembling. ";

Chapter - Path of Chaos Room 1

The printed name of r-chaos-1 is "Path of Chaos".
The description of r-chaos-1 is "[island]. The paths to the northeast and southwest are broken and end in darkness.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-1 are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "[untimely-chaos]",
  [blackhole] ""}.

r-chaos-1     is a room southwest of r-order-3.
r-chaos-2ns   is a room north     of r-chaos-1.
r-chaos-2ew   is a room east      of r-chaos-1.
r-chaos-2senw is a room southeast of r-chaos-1.
r-chaos-2ns   is a room south     of r-chaos-1.
r-chaos-2ew   is a room west      of r-chaos-1.
r-chaos-2senw is a room northwest of r-chaos-1.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating Alcaz	-1	0	"The words you exchanged with Alcaz dissolve, and memories fade[if r-marker-3 is studied]. You no longer remember how the gods responded to your jest[end if]."	"[if r-marker-3 is studied]Alcaz's expression turns from amusement to regret as he realizes the situation. 'My lord,' he says, his voice filled with chagrin, 'it appears the gods have taken our jest seriously. T[else]'My lord,' Alcaz says, 't[end if]here are no runes to guide us. We must rely on our instincts and choose a path.'"
Conan marching inside	1	0	"You retrace your steps, heading to [the way back of the location], and the path reappears beneath your feet as you progress."	"Following the path to [the noun], you and Alcaz forge ahead. The journey across the void is harrowing, but you reach the next island safely.[paragraph break][if r-marker-3 is studied]The gods are not done with you yet, however. [end if]The paths stretching to [the opposite of the noun], and the one directly ahead leading to [the noun] also disintegrate into darkness. The choices narrow, and the uncertainty of your surroundings deepens."
Conan marching inside	1	0	"You retrace your steps, heading to [the way back of the location], and the path reappears beneath your feet as you progress."	"Resolute in your decision, you and Alcaz proceed along [the noun]ern path, making your way to the next island.[paragraph break]Again, the paths behind, stretching to [the opposite of the noun], and the one directly ahead, leading to [the noun], disintegrate into darkness. Now only two paths remain."
Conan marching inside	1	0	"You retrace your steps, heading upward and to [the way back of the location]."	"With your choice made, you and Alcaz continue along [the noun]ern path. It leads you down into what appears to be a guard room."
Conan toggling trap door	0	0	""	""
Conan forcing down	0	0	""	""

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating Lydia	1	0	"The words you exchanged with Lydia dissolve, and memories fade. You no longer remember how you reassured her."	"'Lydia,' you say with calm assurance, 'trust in my instincts, and we'll find our way through this together.'"
Conan marching inside	1	0	"You retrace your steps, heading to [the way back of the location], and the path reappears beneath your feet as you progress."	"Following the path to [the noun], you and Lydia forge ahead. The journey across the void is harrowing, but you reach the next island safely.[paragraph break]The paths stretching to [the opposite of the noun], and the one directly ahead leading to [the noun] also disintegrate into darkness. The choices narrow, and the uncertainty of your surroundings deepens.[paragraph break]'My lord,' Lydia says, 'this place... it's unnerving.'[paragraph break]'Do not be afraid,' you reassure her. 'My instinct tells me we have almost reached the end.'"
Conan marching inside	1	0	"You retrace your steps, heading to [the way back of the location], and the path reappears beneath your feet as you progress."	"Resolute in your decision, you and Lydia proceed along [the noun]ern path, making your way to the next island.[paragraph break]Once again, the paths behind you, stretching to [the opposite of the noun], and the one directly ahead, leading to [the noun], disintegrate into darkness. To your shock, the last two paths leading away from the island also disintegrate into darkness.[paragraph break]'My lord,' Lydia says, 'what do we do now? It seems we have no paths left to follow.'"
Conan forcing inside	0	0	""	""
Conan smiting chaos-marker	1	0	"The fiery phoenix on your sword's blade fades and disappears. The broken fragments of the marker fuse together, mending the marker's form. The air shimmers with arcane energy as the marker is made whole once more."	"Across the mists of time the words of the sage come to you: [i]'When all paths seem lost, strike the monolith thrice, and I will open a path.'[r][paragraph break]As you strike the marker, small cracks appear on the previously smooth surface, and fine stone dust drifts into the air."
Conan smiting chaos-marker	1	0	""	"You strike the marker once more. Chunks of stone chip away, and the once-pristine marker shows signs of wear and distress."
Conan smiting chaos-marker	1	0	""	"As your sword connects with the marker, a fiery phoenix materializes on your sword's blade in a brilliant outline. Lydia gasps, 'The immortal phoenix!' You nod, recognizing the phoenix from the sage's tomb.[paragraph break]With your third strike, the marker succumbs to your relentless assault and shatters into several pieces, revealing a path leading downward."
Conan toggling trap door	0	0	""	""
Conan marching down	1	0	"You retrace your steps, heading back to the Path of Chaos."	"You and Lydia make your way down the revealed path."

Chapter - Path of Chaos Room 2 North South

The printed name of r-chaos-2ns is "Path of Chaos".
The description of r-chaos-2ns is "[island]. The paths to the north, south, northeast, and southwest are broken and end in darkness.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-2ns are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3senw is a room east      of r-chaos-2ns.
r-chaos-3ew   is a room southeast of r-chaos-2ns.
r-chaos-3senw is a room west      of r-chaos-2ns.
r-chaos-3ew   is a room northwest of r-chaos-2ns.

Chapter - Path of Chaos Room 2 East West

The printed name of r-chaos-2ew is "Path of Chaos".
The description of r-chaos-2ew is "[island]. The paths to the east, west, northeast, and southwest are broken and end in darkness.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-2ew are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3senw is a room north     of r-chaos-2ew.
r-chaos-3ns   is a room southeast of r-chaos-2ew.
r-chaos-3senw is a room south     of r-chaos-2ew.
r-chaos-3ns   is a room northwest of r-chaos-2ew.

Chapter - Path of Chaos Room 2 Southeast Northwest

The printed name of r-chaos-2senw is "Path of Chaos".
The description of r-chaos-2senw is "[island]. The paths to the southeast, northwest, northeast, and southwest are broken and end in darkness.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-2senw are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3ew   is a room north     of r-chaos-2senw.
r-chaos-3ns   is a room east      of r-chaos-2senw.
r-chaos-3ew   is a room south     of r-chaos-2senw.
r-chaos-3ns   is a room west      of r-chaos-2senw.

Chapter - Path of Chaos Room 3 North South

The printed name of r-chaos-3ns is "Path of Chaos".
The description of r-chaos-3ns is "[island]. Only the paths to the north and south remain.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-3ns are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3 is a room north of r-chaos-3ns.
r-chaos-3 is a room south of r-chaos-3ns.

Chapter - Path of Chaos Room 3 East West

The printed name of r-chaos-3ew is "Path of Chaos".
The description of r-chaos-3ew is "[island]. Only the paths to the east and west remain.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-3ew are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3 is a room east of r-chaos-3ew.
r-chaos-3 is a room west of r-chaos-3ew.

Chapter - Path of Chaos Room 3 Southeast Northwest

The printed name of r-chaos-3senw is "Path of Chaos".
The description of r-chaos-3senw is "[island]. Only the paths to the southeast and northwest remain.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-3senw are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

r-chaos-3 is a room southeast of r-chaos-3senw.
r-chaos-3 is a room northwest of r-chaos-3senw.

Chapter - Path of Chaos Room 3

The printed name of r-chaos-3 is "Path of Chaos".
The description of r-chaos-3 is "[island]. All paths around you are broken and end in darkness.[paragraph break]An ancient weathered marker dominates the island. [marker]."
The refusals of r-chaos-3 are {
  [horizontal] "[horizontal-chaos]",
  [vertical] "[vertical-chaos]",
  [untimely] "",
  [blackhole] ""}.

The trap door is a closed unopenable not apparent scenery door.
The trap door is down from r-chaos-3.
The trap door is up from r-guards.

Book - Crypt Region

The crypt-floor is a backdrop. It is in r-guards, r-pit, r-corridor, r-crypt, r-hall.
The printed name of the crypt-floor is "floor".
The denials of the crypt-floor are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/rock/rocky/-- floor" as the crypt-floor.

The crypt-wall is a backdrop. It is in r-guards, r-pit, r-corridor, r-crypt, r-hall.
The printed name of the crypt-wall is "wall".
The denials of the crypt-wall are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/rock/rocky/-- wall/walls" as the crypt-wall.

The crypt-ceiling is a backdrop in the crypt-region.
The printed name of the crypt-ceiling is "ceiling".
The denials of the crypt-ceiling are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/rock/rocky/-- ceiling" as the crypt-ceiling.

The crypt-archway is a backdrop in the crypt-region.
The printed name of the crypt-archway is "archway".
The denials of the crypt-archway are {
  [regard] "",
  [seize] "Seizing [the noun] won't open new paths.",
  [loot] "[The noun] holds the promise of passage, not plunder.",
  [present] "",
  [treat] "",
  [smite] "Hitting [the noun] won't earn you any honor."}.
Understand "stone/rock/rocky/-- archway/archways" as the crypt-archway.

Chapter - Guard Room

To say guards-far:
	if the guard is uninjured:
		say "You realize that the two guards are positioned too far apart. Their strategic arrangement makes it impossible for you to interact with both guards at once. You'll need to interact with one of the guards individually. ";
	else:
		say "One of the guards has already fallen to your might. Focus on the one guard left, and choose your next step carefully. ";

To say guard-name:
	if the guard is uninjured:
		say "one of the guards";
	else:
		say "the remaining guard";

To say seize-guard:
	say "As you make a move to seize [guard-name], he raises his massive axe and swings it in a powerful arc, narrowly missing you. He then steps back, his milky eyes locked onto you. ";

To say loot-guard:
	say "As you cautiously approach [guard-name], he raises his axe in a threatening manner. His empty eyes lock onto you, and his skeletal fingers tighten their grip on the hilt of his weapon. ";

To say treat-guard:
	if the guard is treated:
		say "The guard remains silent. Belatedly, you remember that the guard will answer one question, and one question only. ";
	else if alcaz is in the location:
		if the tablet is unregarded:
			say "'My lord,' Alcaz says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";
		else:
			say "You stand before one guard, then the other, unsure of which one to approach. ";
	else if the tablet is unregarded:
		say "'My lord,' Lydia says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";
	else:
		say "'My lord,' Lydia says, 'these guards from the ethereal plane block our path. Should I turn them?'[paragraph break]'No need,' you answer, 'we need to walk the path of Justice, not the paths of Life or Death.' ";
		
To say smite-guard:
	if Lydia is in the location:
		if the tablet is unregarded:
			say "'My lord,' Lydia says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";
		else:
			say "'My lord,' Lydia says, 'these guards from the ethereal plane block our path. Should I turn them?'[paragraph break]'No need,' you answer, 'we need to walk the path of Justice, not the paths of Life or Death.' ";
	else if the guard is treated:
		say "You raise your weapon, ready to strike down the guard who has just given you his answer. Alcaz intervenes, his voice urgent and commanding. 'My lord, we have the knowledge we sought,' Alcaz implores, his eyes locked onto yours. 'There's no need for further violence. Smiting this guard serves no purpose.' ";
	else if the guard is injured:
		say "You raise your weapon, ready to strike down the remaining guard. Alcaz intervenes, his voice urgent and commanding. 'My lord, we need information from this guard,' Alcaz implores, his eyes locked onto yours. 'We must know which archway we should enter. Smiting him now will leave us in the dark.' ";
	else:
		say "'My lord,' Alcaz says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";

The printed name of r-guards is "Guard Room".
The room-recipient of r-guards is the statue.

The statue is a scenery woman in r-guards.
The room-recipient of r-guards is the statue.

To say untimely-guards:
	if Alcaz is in the location:
		say "'My lord,' Alcaz says, 'we should ";
		if the tablet is regarded:
			say "treat with [guard-name] to learn which archway we should enter";
		else:
			say "decipher the runes on the tablet";
		say ". Straying blindly might lead us into even deeper peril.' ";
	else if the tablet is regarded:
		say "'My lord,' Lydia says, 'these guards from the ethereal plane block our path. Should I turn them?'[paragraph break]'No need,' you answer, 'we need to walk the path of Justice, not the paths of Life or Death.' ";
	else:
		say "'My lord,' Lydia says, 'we should decipher the runes on the tablet, we do not know under which laws these guards operate.' ";

To say blackhole-guards:
	if Alcaz is in the location:
		say "'My Lord,' Alcaz says, 'we should follow the path of Life to the south. The question you asked was the right one; the answer would always be spoken in falsehood. The guard told us north, so we must go south.' ";
	else:
		say "'My Lord,' Lydia says, 'we should follow the path of Justice to the east.' ";

The description of r-guards is "At the center of the room stands a serene statue of the Goddess of Justice, holding a stone tablet etched with intricate runes[statue-room]. A small path leads upward into darkness. In the north and south sides of the room, open archways lead deeper into the unknown.[paragraph break]Guarding the open archways, [if the guard is uninjured]two formidable guards in tattered armor and gaunt faces with milky eyes stand at attention. They grip massive, notched axes[else]a formidable guard in tattered armor and a gaunt face with milky eyes stands at attention. He grips his massive, notched axe[end if] with an air of unyielding determination."
The refusals of r-guards are {
  [horizontal] "",
  [vertical] "There's no way to move in that direction.",
  [untimely] "[untimely-guards]",
  [blackhole] "[blackhole-guards]"}.

The hidden door is a closed unopenable not apparent scenery door.
The hidden door is east of r-guards.
The hidden door is west of r-hall.

r-blackhole is a room north of r-guards.
Some guards are scenery men in r-guards.
The denials of the guards are {
  [regard] "[guards-far]",
  [seize] "[guards-far]",
  [loot] "[guards-far]",
  [present] "",
  [treat] "[guards-far]",
  [smite] "[guards-far]"}.
Understand "formidable/-- undead/-- guards/guardians" and "gaunt/-- faces" as some guards.

Some axes are scenery in r-guards.
The denials of the axes are {
  [regard] "The notched axes look massive and well-suited for the task.",
  [seize] "[loot-guard]",
  [loot] "[loot-guard]",
  [present] "",
  [treat] "",
  [smite] "[loot-guard]"}.
Understand "massive/notched/-- notched/massive/-- axe/axes" as some axes.

The guard is a scenery man in r-guards.
The denials of the guard are {
  [regard] "The guard wears tattered armor and has a gaunt face with milky eyes.",
  [seize] "[seize-guard]",
  [loot] "[loot-guard]",
  [present] "",
  [treat] "[treat-guard]",
  [smite] "[smite-guard]"}.
Understand "formidable/-- undead/-- guard/guardian" as the guard.
Understand "north guard" as the guard when the guard is uninjured.
Understand "south guard" and "gaunt/-- face" as the guard.
Understand "tattered/-- armor" and "milky/-- eye/eyes" and "skeletal/-- finger/fingers" as the guard.

The tablet is scenery in r-guards.
The denials of the tablet are {
  [regard] "You regard the stone tablet held by the statue of the Goddess of Justice once more. As you try to decipher the complex symbols, you realize that these runes are far beyond your comprehension. Fortunately, [if alcaz is in the location]Alcaz told you that the tablet speaks of guardians who watch over the paths of Life and Death, providing answers to a single question, in truth or falsehood[else]Lydia told you that the tablet describes the path of Justice, which is guarded by the Goddess. 'Approach the Goddess and fulfill her request, and she will open the path of Justice,' she concluded[end if].",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "stone/-- rune/-- tablet etched/-- with/-- intricate/-- runes/--" and "intricate/-- rune/runes" as the tablet.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding tablet	1	0	"The words you exchanged with Alcaz dissolve, and memories fade. You no longer remember what the runes on the stone tablet say."	"You turn to Alcaz, requesting his assistance in deciphering the runes. As Alcaz touches the stone tablet, a jolt of energy courses through his body. You watch with concern as he steadies himself. With a deep, resonant voice noticeably different from his usual tone, Alcaz begins to speak about the text on the tablet.[paragraph break]'These guardians,' Alcaz intones, 'watch over the paths of Life and Death. One guard embodies truth, while the other embodies falsehood. You may ask but a single question to a single guardian. Heed his words wisely, for the path you choose shall determine your destiny.' As he finishes, Alcaz takes a moment to catch his breath, his voice returning to its familiar tone. He appears disoriented for a moment but recovers quickly."
Conan smiting guard	1	0	"A swirling cloud of ethereal particles appears, coalescing into the form of a guard. The guard then picks up his fallen axe and resumes his guard duty."	"You draw your axe and swiftly bring it down upon one of the guards. As your axe slices into the guard's body, a spray of ethereal sparks erupts from the guard. He lets out an unearthly scream and then disintegrates into a swirling cloud of particles, leaving no bodily remains.[paragraph break]Noticing the shocked expression of Alcaz, you tell him, 'With enemies from the ethereal plane I prefer to smite first and treat later, Alcaz. It keeps negotiations short and honest.' You can see Alcaz still seems taken aback by your swift action, but he nods in understanding."
Conan treating guard	1	0	"The words you exchanged with the guard dissolve, and memories fade. You no longer remember which way leads to the path of Life."	"'Now,' you say to the remaining guard, 'which way would your partner have indicated as the path of Life?'[paragraph break]'He would have directed you to the north,' the guard replies.[paragraph break]'Well,' you respond, 'since he is no longer with us, and I fear he may have spoken falsely, I shall choose to travel south.' "
Conan marching south	1	0	"You rise from the pit and step off the treacherous tile. Then, you retrace your steps, retreating back to the guard room."	"As you stride through the south archway, something feels amiss. You turn to check on Alcaz, only to see him suddenly pulled backward by an invisible force, his eyes wide with alarm as he vanishes into the darkness.[paragraph break]As your attention briefly shifts from the path ahead, your foot lands on a loose tile on the floor. With a sudden, treacherous lurch, the tile gives way beneath your weight, and you find yourself tumbling down into a concealed pit, surrounded by darkness.[paragraph break]Your resolute spirit remains undeterred. The enemy will come to you. You stand ready, eager to bestow upon him a most fiery welcome. Now might be a good time to combat your exhaustion with some liquid fire."
Conan transferring Alcaz to r-guards	0	0	""	""

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan regarding tablet	1	0	"The words you exchanged with Lydia dissolve, and memories fade. You no longer remember what the runes on the stone tablet say."	"You turn to Lydia, requesting her assistance in deciphering the runes. She touches the tablet and begins to speak about the text inscribed upon it. She explains that the tablet speaks of guardians who watch over the paths of Life and Death, providing answers to a single question, in truth or falsehood. Lydia then goes on to describe the path of Justice, which is guarded by the Goddess. 'Approach the Goddess and fulfill her request, and she will open the path of Justice,' Lydia concludes.[paragraph break]You ask Lydia why she had no trouble reading the tablet but could not bear to watch the runes on the markers of the Path of Order.[paragraph break]'Different languages,' Lydia replies, 'the markers bear runes of the Language of the Dead, the language of Corruption. This tablet bears runes of the Language of the Living, the language of Justice. Only agents of Corruption would have trouble reading it.'"
Conan treating statue	1	0	"The Goddess's presence recedes from the statue, and the words you exchanged with her dissolve, and memories fade. You no longer remember what the Goddess requested of you."	"'O Goddess of Justice,' you begin, 'I stand before you seeking your favor. I recognize the importance of your path, and I am ready to prove my worthiness to tread it.'[paragraph break]The statue seems to come to life, its marble features softening as if infused with the spirit of the Goddess herself. Then, an ethereal and commanding voice resonates in the chamber. 'To open the path of Justice,' it intones, 'you must present to me your greatest weapon, one embodying the true spirit of justice.'"
Conan presenting Conan to statue	1	0	""	"You step forward, offering not your weapon, but an oath: 'I present to you, O Goddess, my unwavering commitment to upholding justice and fairness.'[paragraph break]The voice of the Goddess resonates once more. 'The greatest weapon is the heart committed to justice. Your pledge has been heard, and your sincerity recognized. The path of Justice is now open to you.'"
Conan toggling hidden door	0	0	"The statue seems to come to life and shifts once more, hiding the entrance to the east."	"The Goddess's presence recedes from the statue, and as the statue shifts, an entrance behind it to the east is revealed."
Conan marching east	1	0	""	"With the path of Justice now revealed, you and Lydia step forward with a profound sense of purpose and determination."

Chapter - Pit of Darkness

The printed name of r-pit is "Pit of Darkness".
The description of r-pit is "The air is heavy, and the dim light filtering down from above barely illuminates the walls. There appear to be no exits, except for the way you fell in. The walls of the pit are slippery, and climbing them appears to be a futile effort.".
The refusals of r-pit are {
  [horizontal] "You are trapped, with no means of escape.",
  [vertical] "[if up]You cannot scale the slippery walls. [end if]You are trapped, with no means of escape.",
  [untimely] "",
  [blackhole] ""}.
r-pit is a room south of r-guards.

The pit-air is scenery in r-pit.
The printed name of pit-air is "heavy air".
The denials of the pit-air are {
  [regard] "",
  [seize] "[smite-gaseous]",
  [loot] "[smite-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
Understand "heavy/-- air" as pit-air.

The pit-light is scenery in r-pit.
The printed name of pit-light is "dim light".
The denials of the pit-light are {
  [regard] "",
  [seize] "[smite-gaseous]",
  [loot] "[smite-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
Understand "dim/-- light" as pit-light.

Understand "slippery pit/-- wall/walls" and "pit wall/walls" and "pit of/-- darkness/--" as the crypt-wall when the location is r-pit.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan looting bottle	1	0	"You see the wine gradually filling the bottle once more, and your head clears. Finally, the bottle closes itself, and it now looks as if you never drank from it."	"As you drain your bottle of wine, the liquid rolls down your throat, leaving behind a warm, soothing sensation. The alcohol begins to take effect, and a pleasant drowsiness washes over you. Your eyelids grow heavy, and soon, you drift into a deep slumber."
Conan transferring wine to r-blackhole	0	0	"You ignore the echo of the sage calling out to you and retreat into the gray mist. After a while, you wake up, feeling less drowsy."	"Through a swirling gray mist, you hear a curious call, faint and distant. Though you do not understand it, it seems beyond your power to ignore it. With axe in hand, you move through the gray mist. The voice becomes clearer as you proceed until you recognize the words it speaks - it is your own name being called."
Conan toggling secret door	0	0	""	""
Conan forcing south	0	0	""	""
Conan toggling secret door	0	0	""	""

Chapter - Dark Corridor

The printed name of r-corridor is "Dark Corridor".
The description of r-corridor is "The corridor appears to be cut from a single stone. It is unlit, but by some magic, you can see clearly. To the east is an archway curving to the north. To the west, the corridor ends in gray mist."
The refusals of r-corridor are {
  [horizontal] "The wall of the corridor is cut from a single stone, and you cannot pass through it.",
  [vertical] "The [if up]ceiling[else]floor[end if] of the corridor is cut from a single stone, and you cannot pass through it.",
  [untimely] "",
  [blackhole] ""}.
Understand "stone/-- corridor" and "stone" as the crypt-wall when the location is r-corridor.

Understand "gray mist" as the mist when the location is r-corridor.

The secret door is a closed unopenable not apparent scenery door.
The secret door is south of r-pit.
The secret door is north of r-corridor.
r-blackhole is a room west of r-corridor.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching east	1	0	""	""

Chapter - Crypt of the Sage

The printed name of r-crypt is "Crypt of the Sage".
The description of r-crypt is "An old white-bearded man sits cross-legged atop a moss-covered, phoenix-engraved tomb. To the south is an ornate archway leading back to the corridor. The sage [sage-room]."

The refusals of r-crypt are {
  [horizontal] "The sage warns you, 'hold, Child of Destiny. The path you seek is through the southern archway.'",
  [vertical] "The sage warns you, 'hold, Child of Destiny. The path you seek is through the southern archway.'",
  [untimely] "[if alcaz is in the location]The sage warns you, 'hold, Child of Destiny. I am not done with you yet.'[else if treat-counter of sage is 2]The sage warns you, 'hold, Child of Destiny. Walking back does not reverse the flow of time.'[else]The sage warns you, 'hold, Child of Destiny. I am not done with you yet.'[end if]",
  [blackhole] ""}.

South of r-crypt is east of r-corridor.

The sage is a scenery man in r-crypt.
The room-recipient of r-crypt is the sage.

The tomb is scenery in r-crypt.
The printed name of the tomb is "moss-covered tomb".
The denials of the tomb are {
  [regard] "The moss-covered tomb is engraved with a phoenix.",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "moss-covered/moss/phoenix-engraved/phoenix/-- covered/engraved/-- tomb covered/-- with/-- a/an/-- engraved/-- moss/phoenix/--" and "moss/phoenix" as the tomb.

Table of Order vs Chaos (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating sage	1	0	"The crypt briefly blurs and distorts, then returns to normal. The sage waves goodbye."	"'Hail, Child of Destiny,' the sage says, 'and listen. A primal force of Corruption has arisen and walks the mortal plane once more. I have summoned you here to guide and prepare you for your destiny. Show me the weapon you used to defeat the minions of our enemy.'"
Conan presenting axe to sage	1	0	"You hand your axe to the sage, who returns your axe to you."	"You present your axe to the sage. He touches the blade of the axe and the blade's edge becomes translucent for a moment, radiating ethereal energy, before it reverts to normal.[paragraph break]'You have faced entities of the ethereal plane,' he says. He hands the axe back to you and continues, 'Walk the Path of Justice with her blessing, fulfill your destiny, and bring balance to this world once more.'"
Conan treating sage	1	0	"The crypt briefly blurs and distorts, then returns to normal. The sage winks at you."	"You explain the paths of Order and Chaos that you have traveled together with Alcaz, and that in the end, only the paths of Life and Death remained.[paragraph break]'Peace!' the sage says. 'I fear your friend has been deceived. The paths of Order and Chaos are treacherous and cannot be fully trusted. Too often, the learned study writings shaping the reader's mind, ensnaring them to do their bidding.'[paragraph break]'Reverse the flow of time and find a companion who can guard themselves from spiritual interference. Only then will you stand a chance of finding the right path. When all paths seem lost, strike the monolith thrice, and I will open a path and provide you with protection against the force of Corruption,' the sage concludes."
Conan undoing	1	0	""	""

Chapter - Hall of Justice

The printed name of r-hall is "Hall of Justice".
The description of r-hall is "The grandeur of the Hall of Justice exudes an air of opulence and power. Ornate archways lead to the north, west, and south. The floor of the hall is shrouded in a dense, swirling mist, giving the sensation of walking atop the clouds."
The refusals of r-hall are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] ""}.
r-blackhole is a room north of r-hall.
r-blackhole is a room south of r-hall.

The reliquary is a closed openable container in r-hall. "In the heart of the hall, rising like a small island from the mist, stands a sacred reliquary. The reliquary, meticulously crafted from the purest gold, emanates an aura of divine reverence. Atop the reliquary, [if open]the golden clasps, shaped like flower petals, now stand empty[else]something is held securely in place by golden clasps shaped like flower petals, as if the reliquary is cradling it with the utmost care[end if].".
the denials of the reliquary are {
  [regard] "Atop the reliquary, [if open]the golden clasps stand open and empty[else]something is held within golden clasps shaped like flower petals[end if].",
  [seize] "[seize-heavy]",
  [loot] "There are no more spoils to be had from this reliquary, your nimble fingers already secured its prize.",
  [present] "",
  [treat] "",
  [smite] "[smite-furniture]"}.
Understand "sacred/-- golden/-- reliquary" and "golden/-- flower/-- clasp/clasps/petal/petals" and "golden/-- flower" as the reliquary.

The vial is a closed openable container in the reliquary.
The printed name of the vial is "vial of holy water".
The denials of the vial are {
  [regard] "[if open]The vial is open and empty[else]The vial holds a pure and luminescent liquid, casting a gentle, ethereal light around it[end if].",
  [seize] "",
  [loot] "[if open]The vial is open and empty, there is nothing left to loot from it[else]Keep the vial closed, you might need its contents later[end if].",
  [present] "[present-npc for second noun]",
  [treat] "",
  [smite] "[if open]The vial is open and empty, there is no need to smite it[else]Hold on to that vial, you might need its contents later[end if]."}.
Understand "holy/-- water/-- crystal/-- vial of/-- holy/-- water/--" and "holy/-- water" as the vial.

Some holy water is an edible thing in the vial.
The denials of the holy water are {
  [regard] "",
  [seize] "",
  [loot] "",
  [present] "",
  [treat] "",
  [smite] ""}.

The cavern door is a closed unopenable not apparent door. "[if location is r-hall]A circle of light is lit in the mist, leading down into the unknown[else]above[end if]."
The printed name of the cavern door is "circle of light".
The denials of the cavern door are {
  [regard] "The circle of light offers a portal down into the unknown.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
The cavern door is down of r-hall.
The cavern door is up of r-cavern-1.
Understand "circle/-- of/-- light" and "light/-- circle" and "portal" as the cavern door when the cavern door is open.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan looting reliquary	1	0	"You carefully place the vial of holy water in the reliquary and close the golden petals over it."	"With careful precision, you manipulate the golden clasps. A soft click fills the air as the clasps release their hold. With great care, you open the petals and lift a vial of holy water out of its resting place. Just as you secure the vial, a disembodied voice, cold and mocking, emerges from the swirling mist in the room."
Conan transferring Alcaz to r-hall	0	0	"Alcaz's accusations dissolve, and memories fade. You no longer remember what Alcaz accused you of. [alcaz-to-mist]"	"'Once a thief, always a thief,' the voice accuses. [alcaz-from-mist][paragraph break]'As the King, you merely legalize your thefts and call them taxes,' Alcaz continues, his voice once more a deep, resonant voice sounding familiar to you.[paragraph break]'My lord,' Lydia whispers, 'Alcaz is not himself. He's under control of the necromancer. Be cautious. Try to distract him while I probe his defenses.'"
Conan treating Alcaz	1	0	"Your banter with the necromancer dissolves, and memories fade. You no longer remember how you defended yourself against his accusations."	"'My rule is just,' you assert. 'Taxes are lower than ever, and the people are content. I dethroned my predecessor to restore peace and quiet in the kingdom.'[paragraph break]'You served justice in true barbaric fashion,' the necromancer retorts, 'by tearing the crown off his gory head and setting it on your own. Civilization is lost on you.'[paragraph break]'Civilized men are more discourteous than savages because they know they can be impolite without having their skulls split,' you reply. 'The more I see of what you call civilization, the more highly I think of what you call barbarism!'[paragraph break]'My lord,' Lydia whispers, 'I think I have found a way. Please give me that piece of steel you keep lugging around.'[paragraph break]'What is this,' the necromancer says, interrupting the conversation with you. 'Priestess, what are you doing? Trying to match your magic against mine?'"
Conan presenting handle to Lydia	1	0	"A spiked ball of ice forms around your defensive sphere, flies toward Alcaz, dissipates, and Alcaz and Lydia drop their mystical defenses. Lydia then returns the broken axe handle to you."	"You surreptitiously pass the broken axe handle to Lydia, who hides it in the folds of her dress. 'Ready when you are, my lord,' Lydia says as she recites a brief prayer, enveloping both you and herself in a sphere of mystical defense.[paragraph break]Alcaz notices the sphere enveloping you and promptly raises his own mystical defense with a brief gesture. He then creates a spiked ball of ice by making a circular throwing motion with his hands, sending it flying across the room toward you. It explodes upon impact with the defensive sphere, causing a deafening ringing in your ears."
Conan transferring handle to r-blackhole	0	0	""	""
Conan smiting Alcaz	1	0	"The lit circle in the center of the room fades, and darkness moves from it to the place where Alcaz fell before it, too, fades into the mist. Alcaz rises from the mist, the bump on the back of his head vanishing, and the broken axe handle flies back into Lydia's hand. Your battle cry fades as you retreat, wildly swinging your sword."	"You charge toward Alcaz, your sword poised for attack. A fierce battle cry erupts from deep within you, echoing through the room as you swing your sword with all your might. Alcaz snaps his fingers and teleports away from you, towards Lydia. Lydia recites a prayer, and Alcaz reinforces his mystical defense, while Lydia raises her hand, glowing with the power of her deity.[paragraph break]You charge at Alcaz again, who turns to face you just as Lydia lowers her hand. Surprise appears on Alcaz's face as he is struck by the broken axe handle thrown by Lydia. It passes right through his mystical defense, causing him to drop into the mist like a sack of sand, knocked unconscious. 'I cannot win against Alcaz when fighting magic with magic,' Lydia says. 'He did not expect a mundane attack from me, however.' Lydia smiles."
Conan transferring Alcaz to r-blackhole	0	0	""	""
Conan toggling cavern door	0	0	""	"The mist darkens where Alcaz fell, and the darkened area moves to the center of the room. Then, a circle lights up in the mist, and the darkness is swallowed by the circle. 'My lord!' Lydia says urgently. 'The necromancer's spirit is escaping! We'd better follow it before the portal closes!'"
Conan marching down	1	0	"The necromancer's sneer dissolves, and memories fade. You no longer remember how the necromancer greeted you. A circle of light appears far above you, and you and Lydia slowly ascend, accelerating, until you pass through the portal, emerging into the Hall of Justice."	"As you and Lydia jump through the portal, the world around you blurs. You emerge from a circle of light at the top of a colossal cavern, vast and dimly lit. Below you stretches a massive chamber filled with countless skeletal remains of creatures known and unknown to you.[paragraph break]Lydia recites a brief prayer, and your fall is slowed into a gentle descent, eventually landing amidst the skeletal remains with a soft thud. The circle of light far above you fades into the darkness.[paragraph break]'Welcome to my domain, [i]thief[r],' the necromancer sneers, his voice echoing through the cavern. 'There will be no escape for you this time.'[paragraph break]'My lord,' Lydia whispers, 'the necromancer is circling us. I sense his spirit traveling from the southwest to the north.'"
Conan toggling cavern door	0	0	""	""

Book - Cavern Region

The cavern-floor is a backdrop in the cavern-region.
The printed name of the cavern-floor is "cavern floor".
The denials of the cavern-floor are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "cavern/-- floor" as the cavern-floor.

The cavern-ceiling is a backdrop in the cavern-region.
The printed name of the cavern-ceiling is "cavern ceiling".
The denials of the cavern-ceiling are {
  [regard] "",
  [seize] "[seize-far]",
  [loot] "[loot-far]",
  [present] "",
  [treat] "",
  [smite] "[smite-far]"}.
Understand "cavern/-- ceiling" as the cavern-ceiling.

Some cavern-bones is a backdrop in the cavern-region.
The printed name of the cavern-bones is "skeletal remains".
The denials of the cavern-bones are {
  [regard] "",
  [seize] "[seize-solid]",
  [loot] "[loot-solid]",
  [present] "",
  [treat] "",
  [smite] "[smite-solid]"}.
Understand "skeletal/-- dry/old/-- old/dry/-- bone/bones/femur/femurs/skull/skulls/remains" as the cavern-bones.

To say bone-cavern:
	say "The skeletal remains of creatures, some known to you and others shrouded in mystery, are scattered around you in haphazard arrangements. Towering ribcages create skeletal arches, reminiscent of a labyrinth. An assortment of skulls, varying in size, is strewn across the ground, and their empty eye sockets appear to follow your every step";

Chapter - Cavern of Bones 1

The necromancer is a backdrop. It is in r-cavern-1, r-cavern-2, r-cavern-3, r-cavern-4.
The printed name of the necromancer is "necromancer".
The denials of the necromancer are {
  [regard] "You hear the necromancer, but you cannot see him. You need to get closer.",
  [seize] "You hear the necromancer, but you cannot see him. You need to get closer.",
  [loot] "You hear the necromancer, but you cannot see him. You need to get closer.",
  [present] "",
  [treat] "",
  [smite] "You hear the necromancer, but you cannot see him. You need to get closer."}.
Understand "accursed/-- necromancer" as the necromancer.

The printed name of r-cavern-1 is "Cavern of Bones".
The description of r-cavern-1 is "[bone-cavern]."
The refusals of r-cavern-1 are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] "'My lord,' Lydia whispers, 'the necromancer is circling us. We should try to predict where he goes and intercept him. I think he is somewhere north.' "}.
r-blackhole is a room north     of r-cavern-1.
r-cavern-2  is a room northeast of r-cavern-1.
r-blackhole is a room east      of r-cavern-1.
r-blackhole is a room southeast of r-cavern-1.
r-blackhole is a room south     of r-cavern-1.
r-blackhole is a room southwest of r-cavern-1.
r-blackhole is a room west      of r-cavern-1.
r-blackhole is a room northwest of r-cavern-1.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching northeast	1	0	"The necromancer's sneer fades from memory as you retrace your steps back to the southwest. "	"You and Lydia make your way to the northeast, attempting to intercept the necromancer.[paragraph break]'Stop your search, [i]thief[r],' the necromancer sneers. 'There are no treasures here, only old bones. No gold, no silver, no gems... just dry old bones.'[paragraph break]'My lord,' Lydia whispers, 'the necromancer is circling us. I sense his spirit moving from the east to the southwest.'[paragraph break]Out of the corner of your eye, you sense movement in the macabre landscape. Is that a skeletal claw you saw?"

Chapter - Cavern of Bones 2

The printed name of r-cavern-2 is "Cavern of Bones".
The description of r-cavern-2 is "[bone-cavern]. A faint mist clings to the floor, its wispy tendrils barely noticeable."
The refusals of r-cavern-2 are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] "'My lord,' Lydia whispers, 'the necromancer is circling us. We should try to predict where he goes and intercept him. I think he is somewhere west.' "}.
r-blackhole is a room north     of r-cavern-2.
r-blackhole is a room northeast of r-cavern-2.
r-blackhole is a room east      of r-cavern-2.
r-blackhole is a room southeast of r-cavern-2.
r-blackhole is a room south     of r-cavern-2.
r-blackhole is a room southwest of r-cavern-2.
r-cavern-3  is a room west      of r-cavern-2.
r-blackhole is a room northwest of r-cavern-2.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching west	1	0	"The necromancer's roar fades from memory as you retrace your steps back to the east."	"You and Lydia make your way to the west, attempting to intercept the necromancer.[paragraph break]'Show yourself, necromancer!' you shout. 'I am not here to play games with you.'[paragraph break]A roar echoes through the cavern. 'Puny mortal!' the necromancer shouts. 'How [i]DARE[r] you compare me to those petty humans who can raise a skeleton or two? Let me show you my true power!'"

Chapter - Cavern of Bones 3

The printed name of r-cavern-3 is "Cavern of Bones".
The description of r-cavern-3 is "[bone-cavern]. A hazy shroud partially obscures some skeletal remains."
The refusals of r-cavern-3 are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] "'My lord,' Lydia whispers, 'the necromancer is circling us. We should try to predict where he goes and intercept him. I think he is somewhere east.'"}.
r-blackhole is a room north     of r-cavern-3.
r-blackhole is a room northeast of r-cavern-3.
r-blackhole is a room east      of r-cavern-3.
r-cavern-4  is a room southeast of r-cavern-3.
r-blackhole is a room south     of r-cavern-3.
r-blackhole is a room southwest of r-cavern-3.
r-blackhole is a room west      of r-cavern-3.
r-blackhole is a room northwest of r-cavern-3.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan transferring skeleton to r-cavern-3	0	0	"The skeletons surrounding you on all sides collapse into themselves, and the ground beneath your feet grows quiet."	"The ground beneath your feet trembles. The skeletal remains scattered around you begin to stir. Skeletons of various shapes and sizes rise from the ground, surrounding you on all sides."
Conan transferring skeletons to r-cavern-3	0	0	""	""
Conan treating Lydia	1	0	"Your request for assistance from Lydia fades from memory."	"'Lydia,' you say, 'I need your help. I have fought countless battles against numerous enemies, but this enemy... this army is without end. Can you assist me with your magic?'"
Conan transferring skeletons to r-blackhole	0	0	"Lydia's blast converges from the far reaches of the cavern, ending up at her wrists, which she lowers. Skeletons of various shapes and sizes rise from the ground, surrounding you on all sides again."	"'I daresay I can,' Lydia replies. Lydia raises her arms above her head, crossing them at their wrists. She then speaks but a simple word, almost as a whisper:[paragraph break][i]TURN[r][paragraph break]An invisible power emanates from her figure, spreading like a powerful blast outwards to the far reaches of the cavern, causing the skeletons surrounding you to disintegrate into numerous fragments that collapse to the cavern floor.[paragraph break]'My lord,' Lydia whispers, 'the necromancer is still circling us. I sense his spirit moving from the northwest to the east.'[paragraph break]Out of the corner of your eye, you sense movement in the macabre landscape. Is that a skeletal tail you saw?"
Conan transferring skeleton to r-blackhole	0	0	""	""
Conan marching southeast	1	0	"The necromancer's sneer fades from memory as you retrace your steps back to the northwest."	"You and Lydia make your way to the southeast, attempting to intercept the necromancer.[paragraph break]'So, your priestess can mass turn the undead. How [i]fortunate[r] for you,' the necromancer sneers. 'Come to me, you and your priestess, and let her try to turn [i]ME[r].'[paragraph break]'My lord,' Lydia whispers, 'the necromancer has stopped circling us; he is now due north of us.'[paragraph break]Out of the corner of your eye, you sense movement in the macabre landscape. Is that a skeletal wing you saw?"

Chapter - Cavern of Bones 4

The printed name of r-cavern-4 is "Cavern of Bones".
The description of r-cavern-4 is "[bone-cavern]. Mist blankets the ground, obscuring some of the smaller skeletal remains."
The refusals of r-cavern-4 are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] "'My lord,' Lydia whispers, 'The necromancer is due north of us.'"}.
r-cavern-5  is a room north     of r-cavern-4.
r-blackhole is a room northeast of r-cavern-4.
r-blackhole is a room east      of r-cavern-4.
r-blackhole is a room southeast of r-cavern-4.
r-blackhole is a room south     of r-cavern-4.
r-blackhole is a room southwest of r-cavern-4.
r-blackhole is a room west      of r-cavern-4.
r-blackhole is a room northwest of r-cavern-4.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan marching north	1	0	"You retrace your steps back to the south."	"You and Lydia make your way to the north."

Chapter - Cavern of Bones 5

The printed name of r-cavern-5 is "Cavern of Bones".
The description of r-cavern-5 is "[bone-cavern]. Mist blankets the ground, obscuring some of the smaller skeletal remains."
The refusals of r-cavern-5 are {
  [horizontal] "",
  [vertical] "",
  [untimely] "",
  [blackhole] ""}.
r-blackhole is a room north     of r-cavern-5.
r-blackhole is a room northeast of r-cavern-5.
r-blackhole is a room east      of r-cavern-5.
r-blackhole is a room southeast of r-cavern-5.
r-blackhole is a room south     of r-cavern-5.
r-blackhole is a room southwest of r-cavern-5.
r-blackhole is a room west      of r-cavern-5.
r-blackhole is a room northwest of r-cavern-5.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan transferring dragon to r-cavern-5	0	0	"Lydia calms down as the massive form of the skeletal dragon disintegrates into countless skeletal fragments, which vanish into the shadows of the macabre landscape."	"In the heart of this dimly lit cavern, a sinister spectacle unfolds as you and Lydia step into view. Emerging from the shadows of the macabre landscape, an unsettling assembly begins to take shape. Countless skeletal fragments, ranging from colossal ribcages to jagged wing bones, converge and piece together like a grotesque puzzle.[paragraph break]As the skeletal dragon materializes before your eyes, its massive form is composed of these disjointed bones, coalescing into a nightmarish whole. The skeletal dragon then turns its massive head toward you and speaks, 'Welcome, O King of Thieves, welcome, and bear witness to my power!'[paragraph break]'My lord,' Lydia says, trembling, 'he's right. The power which emanates from him, it's... beyond anything I have ever felt. I... cannot turn him.'"
Conan looting vial	1	0	"The radiant energy of your sword diminishes as the holy water gradually refills the vial. Finally, the vial seals itself shut."	"You open the vial of holy water and trickle its contents onto the blade of your sword. A radiant energy surges through the weapon, illuminating it with a divine glow. The phoenix on the sword seems to come alive, its fiery outline dancing with newfound intensity."
Conan transferring holy water to r-blackhole	0	0	""	""
Conan smiting dragon	1	0	"The skeletal dragon drops its magical shield and regenerates its shattered bones, drawing dark energy from the macabre landscape. The fractured fragments reassemble, mending the damage you had inflicted."	"As you smite the skeletal dragon with your holy-imbued sword, the blade strikes true, creating a brilliant burst of radiant energy upon impact. The dragon's bony frame shudders and some of its skeletal fragments crack and break."
Conan smiting dragon	1	0	""	"As your blade, infused with the power of the phoenix, slashes through the skeletal dragon's bones once more, a shudder runs through its monstrous form. The relentless assault has taken a significant toll, causing it to struggle to maintain its terrifying visage.[paragraph break]'You will [i]NOT[r] defeat me,' the skeletal dragon roars. With a surge of dark magic, it summons a spherical shield, blocking all but the mist drifting across the floor."
Conan transferring dragon-shield to r-cavern-5	0	0	""	""
Conan smiting dragon	1	0	""	"As you swing your sword at the skeletal dragon, its dark magic shield halts your attack."
Conan transferring Alcaz to r-cavern-5	0	0	"Alcaz's offer of help dissolves, and memories fade. You no longer remember what Alcaz asked you. [alcaz-to-mist]"	"'Need some magic?' a familiar voice asks. [alcaz-from-mist] 'Seems you are facing quite the enemy here, can someone tell me what happened?'"
Conan treating alcaz	1	0	"The magical barrier restores itself, and the mist melts. Your conversation with Alcaz fades from memory."	"You briefly recount your adventures since you left the palace with Lydia, tactfully omitting the encounter with Alcaz at the Hall of Justice.[paragraph break]'I see,' Alcaz remarks as he studies the dragon's spherical shield. 'A nice shield, but it is flawed.' He then slams his staff forcefully into the ground. With a brief incantation, he channels his magic into the cavern, causing the mist to freeze. The mist inside the spherical shield freezes as well, shattering the magical barrier with its icy grip. 'He's all yours now.'"
Conan transferring dragon-shield to r-blackhole	0	0	""	""
Conan smiting dragon	1	0	"The countless fragments coalesce into the weakened form of the skeletal dragon."	"As you swing your sword at the skeletal dragon, its weakened form shudders and convulses. The blade, infused with the power of the phoenix and guided by your unwavering determination, slices through the remaining bones with ease. In a spectacular burst of radiant energy, the skeletal dragon disintegrates into countless fragments."
Conan transferring dragon to r-blackhole	0	0	"The force of Corruption gathers from the recesses of the cavern, and enters into the remains of the fallen dragon."	"The force of Corruption within the skeletal dragon starts to unravel. It forms a black cloud that rises from the remains of the fallen dragon and flees into the dimly lit recesses of the cavern, retreating from the overwhelming force of righteousness that has shattered its vessel."
Conan toggling crypt door	0	0	"[crypt-closes]"	"[crypt-opens]"
Conan marching down	1	0	"A surge of magical energy envelops your party, briefly blinding you before receding into darkness. As your vision clears, you find yourselves in the cavern of bones."	"A surge of magical energy envelops your party, briefly blinding you before receding into darkness. As your vision clears, you find yourselves in the crypt of the sage."
Conan toggling crypt door	0	0	"[crypt-opens]"	"[crypt-closes][paragraph break]'Welcome back,' says the sage, 'and just about time. Fighting the forces of Corruption is dry work.' The sage glances at your bottle of wine and clears his throat. '[i]Very[r] dry work.'"

[ Crypt of the Sage ]

To say crypt-opens:
	say "A magical sigil, shimmering with ethereal energy, manifests at [if location is r-cavern-5]your feet[else]the east wall[end if]. ";
To say crypt-closes:
	say "The magical sigil recedes until it is but a memory. ";

The crypt door is a closed unopenable not apparent door. "On the [if location is r-cavern-5]floor[else]east wall[end if], a magical sigil shimmers with eerie energy."
The printed name of the crypt door is "magical sigil".
The denials of the crypt door are {
  [regard] "The magical sigil is shimmering with eerie energy, a tear in reality.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
The crypt door is below r-cavern-5.
The crypt door is east of r-crypt.
Understand "magic/magical/shimmering/-- portal/rift/tear/sigil" as the crypt door when the crypt door is open.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan presenting bottle to sage	1	0	"Your bottle of wine reappears in the sage's hand, where it refills and closes itself. The sage then hands the bottle back to you, a sad expression on his face."	"The sage graciously accepts the bottle of wine, opens it, and takes a long sip, savoring the taste. He drains the bottle in one long gulp, lets out a contented belch, and then makes the empty bottle disappear with a flick of his fingers.[paragraph break]'Thank you,' the sage says, 'I feel much better now. Let's get down to business.'"
Conan transferring bottle to r-blackhole	0	0	""	""
Conan treating sage	1	0	"Your conversation with the sage fades from memory. You no longer remember why the sage started a religion and an order of mages."	"'It seems you are leading a busy life, old man,' you say, looking at your enraptured party members. 'Starting both a religion and an order of mages?'[paragraph break]'Child of Destiny, I have lived for over 1500 years, and well, after a few centuries, life tends to get pretty boring. But undertaking these endeavors was not a whim. I needed both mages and priests to combat the forces of Corruption from the ethereal plane.'[paragraph break]Lydia awakes from her admiration. 'Child of Destiny? You told me in my dreams that you are the vessel of Mitora, and that I was chosen as your Child of Destiny!' she says with an accusing tone.[paragraph break]Alcaz responds, 'This cannot be. Salcor here told me in my dreams that I was his Child of Destiny.'[paragraph break]Alcaz and Lydia both look at you and then at the sage. 'Why did you choose our King? He is a barbarian!'"
Conan treating sage	1	0	"Your conversation with the sage fades from memory. You no longer remember why the sage chose all three of you to be his Children of Destiny."	"'Muscle Power,' the sage responds. 'And he is a natural leader. Besides, the forces of Corruption are legion. Why should I limit myself to choosing a single Child of Destiny, hmm?'[paragraph break]The sage looks at Lydia. 'You, my Child, tend to attract attention to yourself at the most inopportune times, endangering yourself in the process.'[paragraph break]The sage turns to Alcaz. 'As for you, my Child, you tend to be overly curious, and you let yourself get corrupted by paying too much attention to scriptures written in the Language of the Dead.'[paragraph break]The sage turns to you. 'And as for you, my Child, you tend to blunder about, not paying attention to where you are going, and fall into the first pit you come across.'[paragraph break]'With such Children of Destiny to work with, it doesn't hurt to have a backup plan,' the sage chuckles."
Conan treating sage	1	0	"Your conversation with the sage fades from memory, and you no longer remember the extinction of the dragons."	"'Although we were victorious in sending the force of Corruption back to the ethereal plane, we have lost much,' the sage says. 'Magical creatures are special, and the dragons were the greatest of them all before they were all killed by humans. Your Dragon Throne is a testament to that.'[paragraph break]'No wonder the force of Corruption targeted you when it took possession of the skeletal dragon,' the sage continues. 'But there was no malice when the last of its kind died; I only felt an infinite sense of loss.'[paragraph break]The sage snaps his fingers, and another magical sigil, shimmering with ethereal energy, manifests at the north wall.[paragraph break]'Lydia,' the sage continues, 'take your King back to the Palace. He needs to recover from his fight with the dragon. As for you, Alcaz, I would like you to stay for a while. We need to discuss the future of magic within the realm.'"
Conan toggling bedroom door	0	0	""	""
Conan marching north	1	0	"A surge of magical energy envelops you and Lydia, briefly blinding you before receding into darkness. As your vision clears, you find yourselves in the crypt of the sage."	"A surge of magical energy envelops you and Lydia, briefly blinding you before receding into darkness. As your vision clears, you find yourselves in your bedroom."
Conan transferring Alcaz to r-blackhole	0	0	""	""
Conan toggling bedroom door	0	0	"[bedroom-opens]"	"[bedroom-closes]"

[ Royal Bedroom ]

The bedroom door is a closed unopenable not apparent scenery door.
The bedroom door is north of r-crypt.
The bedroom door is south of r-bedroom.

To say bedroom-opens:
	say "A magical sigil, shimmering with ethereal energy, manifests at the [if location is r-crypt]north[else]south[end if] wall. ";
To say bedroom-closes:
	say "The magical sigil recedes until it is but a memory. ";

The bedroom door is a closed unopenable not apparent door. "On the [if location is r-crypt]north[else]south[end if] wall, a magical sigil shimmers with eerie energy."
The printed name of the bedroom door is "magical sigil".
The denials of the bedroom door are {
  [regard] "The magical sigil is shimmering with eerie energy, a tear in reality.",
  [seize] "[seize-gaseous]",
  [loot] "[loot-gaseous]",
  [present] "",
  [treat] "",
  [smite] "[smite-gaseous]"}.
The bedroom door is south of r-bedroom.
The bedroom door is north of r-crypt.
Understand "magic/magical/shimmering/-- portal/rift/tear/sigil" as the bedroom door when the bedroom door is open.

Table of Good vs Evil (continued)
action	type	flag	undoes	redoes	object (an object)
Conan treating Lydia	1	0	""	"'My Lord,' Lydia says, 'I never had a chance to properly thank you for saving my life from the demon in the throne room. Do you believe there is an afterlife?'[paragraph break]'I do not know,' you admit. 'I do not look beyond death. Let me live deep while I live, and I am content.'[paragraph break]'My lord,' Lydia whispers as she embraces you, 'You may now sheathe your blade and rest your battle-weary soul.'"
Conan quitting the game	1	0	""	""

Book - Meta Commands - For the Author (not for release)

Chapter - Enabling testing

The testing mode is a feature flag that varies. [ Default ff-unknown. ]

Before reading a command:
	if the testing mode is ff-enabled:
		say "Allowed actions: [allowed-actions in brace notation].";

Enabling testing is an action out of world and applying to nothing.
Understand "testing on/--" as enabling testing.

When play begins:
	now the testing mode is ff-enabled;
	now the command prompt is "[short name of journey].[progress] >";
	now the walkthrough mode is ff-enabled;

Check enabling testing:
	if the testing mode is ff-disabled, make no decision;
	if the testing mode is ff-enabled, say "Testing mode has already been enabled. Use [b]TESTING OFF[r] to disable it." instead;
	say "Testing mode is not available in this release." instead;

Carry out enabling testing:
	now the testing mode is ff-enabled;
	now the command prompt is "[short name of journey].[progress] >";

Report enabling testing: say "Testing mode has been enabled. Use [b]TESTING OFF[r] to disable it. You now have the following commands available:[paragraph break][b]ALL:[i] Try all actions which do not progress the story.[line break][b]TABLE:[i] Show the contents of the current journey table.[paragraph break][r]The game now shows the list of all allowed actions at each turn.[line break]The prompt now reflects the game state (where we are in the storyline).";

Chapter - Disabling testing

Disabling testing is an action out of world and applying to nothing.
Understand "testing off" as disabling testing.

Check disabling testing:
	if the testing mode is ff-enabled, make no decision;
	if the testing mode is ff-disabled, say "Testing mode has already been disabled. Use [b]TESTING ON[r] to re-enable it." instead;
	say "Testing mode is not available in this release." instead;

Carry out disabling testing:
	now the testing mode is ff-disabled;
	now the command prompt is ">";

Report disabling testing: say "Testing mode has been disabled. Use [b]TESTING ON[r] to re-enable it.";

Chapter - Speedtesting

[ Try all actions which do not progress the story. ]

Speedtesting is an action applying to nothing.
Understand "all" as speedtesting when the testing mode is ff-enabled.

Carry out speedtesting:
	say "Available actions: [allowed-actions in brace notation].";
	repeat with D running through directions:
		if marching D is listed in the allowed-actions:
			say "# march [D](skipped)[line break]";
		else if D is not inside and D is not outside:
			say "# march [D]";
			try marching D;
	repeat with I running through visible things:
		if I is a closed door, next;
		say "[line break]--- ( press any key ) ---[line break]";
		wait for any key;
		say "# [b][I][r][line break]";
		if regarding I is listed in the allowed-actions:
			say "# regard [I](skipped)[line break]";
		else:
			say "# regard [I]";
			try regarding I;
		if seizing I is listed in the allowed-actions:
			say "# seize [I](skipped)[line break]";
		else:
			say "# seize [I]";
			try seizing I;
		if looting I is listed in the allowed-actions:
			say "# loot [I](skipped)[line break]";
		else:
			say "# loot [I]";
			try looting I;
		let R be the recipient;
		if presenting I to R is listed in the allowed-actions:
			say "# present [I] (to [R])(skipped)[line break]";
		else:
			say "# present [I] (to [R])";
			try presenting I to R;
		if your loincloth is held by Conan:
			say "# present your loincloth to [I]";
			try presenting your loincloth to I;
		if treating I is listed in the allowed-actions:
			say "# treat [I](skipped)[line break]";
		else:
			say "# treat [I]";
			try treating I;
		if smiting I is listed in the allowed-actions:
			say "# smite [I](skipped)[line break]";
		else:
			say "# smite [I]";
			try smiting I;

Chapter - Speedtabling

[ Show the contents of the current journey table. ]

Speedtabling is an action applying to nothing.
Understand "table" as speedtabling when the testing mode is ff-enabled.

Report speedtabling:
	let M be 6; [ Action ]
	repeat with R running from 1 to the number of rows in the journey:
		let A be the action in row R of journey;
		let T be "[A]";
		let S be the number of characters in T;
		if M is less than S, now M is S;
	say "[fixed letter spacing][journey][line break]Row | Action";
	let S be 6;
	while S is less than M:
		increment S;
		say " ";
	say " | Type | Flag | Object[line break]";
	repeat with R running from 1 to the number of rows in the journey:
		if R is less than 10, say " ";
		if R is less than 100, say " ";
		let A be the action in row R of journey;
		let T be "[A]";
		let S be the number of characters in T;
		say "[R] | [A]";
		while S is less than M:
			increment S;
			say " ";
		let U be the type in row R of journey;
		let F be the flag in row R of journey;
		say " |   ";
		if U >= 0, say " ";
		say "[U] |    [F] | ";
		if there is an object in row R of the journey:
			say "[object in row R of the journey]";
		else:
			say "--";
		say "[line break]";
	say "[variable letter spacing]";

